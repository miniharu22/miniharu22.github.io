---
layout : single
title: "[Verilog] UART Tx Design"
categories: 
  - Verilog HDL (Vivado)
toc: true
toc_sticky: true
use_math: true
---

UART의 Tx(송신부) 설계   

## 0. UART   

&nbsp;

<div align="center">
  <img src="/assets/images/verilog/1.jpg" width="60%" height="60%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **TX(Transmit)**   
  - data를 송신하는 부분, 즉 data가 UART를 통해 외부 장치로 전송될 때 Tx 라인이 사용됨   
  - **동작 방식**   
    - **데이터 레지스터r에 데이터 저장** : 마이크로컨트롤러나 컴퓨터의 소프트웨어가 전송할 데이터를 UART의 Tx 레지스터에 저장    
    - **비트 단위로 직렬화** : 병렬 데이터를 하나씩 직렬로 변환   
    - **스타트 비트 전송** : Tx 라인은 기본적으로 High state(IDLE) 상태로 유지되며, 전송이 시작되면 스타트 비트(Low)를 송출    
    - **데이터 비트 전송** : 데이터를 LSB(Least Significant Bit)부터 전송   
    - **패리티 비트** : 오류 검출용으로 패리티 비트 추가   
    - **스탑 비트 전송** : 전송 종료를 알리기 위해 스탑 비트(High)를 전송   

&nbsp;

## 1. Design Source   
### 1-1. Baudrate-generator   

```verilog
module baudrate_generator(
    
    input clk,
    input reset,

    output br_tick
    );

    // Baud rate: 1000000 bps
    // Clock frequency: 100 MHz
    reg [$clog2(100_000_000/100_000_00)-1:0] counter_reg, counter_next;
    reg tick_reg, tick_next;

    assign br_tick = tick_reg;

    always @(posedge clk, posedge reset)   // Synchronous reset
    begin   
        if (reset) begin        // Reset condition
            counter_reg <= 0;   // Reset counter
            tick_reg <= 1'b0;   // Reset tick signal
        end else begin
            counter_reg <= counter_next;    // Update counter
            tick_reg <= tick_next;          // Update tick signal
        end
    end

    always @(*) // Combinational logic for counter and tick signal
    begin
        counter_next = counter_reg; // Default next value is current value
        if (counter_reg == 100_000_000 / 100_000_00 - 1) // Check if counter reached the limit
        begin
            counter_next = 0;   // Reset counter
            tick_next = 1'b1;   // Set tick signal high
        end else begin                      // If not reached limit
            counter_next = counter_reg + 1; // Increment counter
            tick_next = 1'b0;               // Set tick signal low
        end
    end
    
endmodule
```
<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/22.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 


&nbsp;

### 1-2. Transmitter   

```verilog
module transmitter(

    input clk,
    input reset,
    input start,
    input br_tick,
    input [7:0] data,
    output tx,
    output tx_done
    );

    localparam IDLE = 0, START = 1, STOP = 10;
    localparam D0 = 2, D1 = 3, D2 = 4, D3 = 5, D4 = 6, D5 = 7, D6 = 8, D7 = 9;

    reg [3:0] state, state_next;
    reg [7:0] r_data;
    reg tx_reg, tx_next;
    reg tx_done_reg, tx_done_next;

    assign tx = tx_reg;
    assign tx_done = tx_done_reg;

    always @(posedge clk, posedge reset) // Synchronous reset
    begin
        if (reset) begin           // Reset condition
            state <= IDLE;         // Reset state to IDLE
            tx_reg <= 1'b0;        // Default tx line high (idle state)
            tx_done_reg <= 1'b0;   // Reset tx_done signal
        end else begin                      // Normal operation
            state <= state_next;            // Update state
            tx_reg <= tx_next;              // Update tx line
            tx_done_reg <= tx_done_next;    // Update tx_done signal
        end
    end

    always @(*) // Combinational logic for state transitions and tx line control
    begin
        state_next = state; // Default next state is current state

        case (state)    // State machine for transmitter
            IDLE : if (start) state_next = START;
            START : if (br_tick) state_next = D0;
            D0 : if (br_tick) state_next = D1;
            D1 : if (br_tick) state_next = D2;
            D2 : if (br_tick) state_next = D3;
            D3 : if (br_tick) state_next = D4;
            D4 : if (br_tick) state_next = D5;
            D5 : if (br_tick) state_next = D6;
            D6 : if (br_tick) state_next = D7;
            D7 : if (br_tick) state_next = STOP;
            STOP : if (br_tick) state_next = IDLE;
        endcase
    end        

    always @(*)  // Combinational logic for tx line and tx_done signal
    begin
        tx_next = tx_reg;   // Default next tx line value is current value
        tx_done_next = 1'b0;// Default tx_done signal is low

        case (state)
            IDLE : tx_next = 1'b1;  // Idle state, tx line high
            START : begin           // Start state, tx line low
                tx_next = 1'b0;     // Start bit is low
                r_data = data;      // Store data to be transmitted
            end
            D0 : tx_next = r_data[0]; 
            D1 : tx_next = r_data[1];
            D2 : tx_next = r_data[2];
            D3 : tx_next = r_data[3];
            D4 : tx_next = r_data[4];
            D5 : tx_next = r_data[5];
            D6 : tx_next = r_data[6];
            D7 : tx_next = r_data[7];
            STOP : begin        // Stop state, tx line high
                tx_next = 1'b1; // Stop bit is high
                if (state_next == IDLE) tx_done_next = 1'b1; 
                // Set tx_done signal high when returning to IDLE
            end 
        endcase
    end

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/23.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

&nbsp;

### 1-3. UART Tx System   

```verilog
module uart(

    input clk,
    input reset,
    input start,
    input [7:0] tx_data,

    output txd,
    output tx_done
    );

    wire w_br_tick;

    baudrate_generator U_BR_Gen(
        .clk(clk),
        .reset(reset),
        .br_tick(w_br_tick)
    );

    transmitter U_TxD(
        .clk(clk),
        .reset(reset),
        .start(start),
        .br_tick(w_br_tick),
        .data(tx_data),
        .tx(txd),
        .tx_done(tx_done)
    );

endmodule
```
<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/24.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 


&nbsp;

## 2. Testbench  

```verilog
module tb_uart ();

    reg clk;
    reg reset;
    reg start;
    reg [7:0] tx_data;

    wire txd;
    wire tx_done;

    uart dut (
        .clk(clk),
        .reset(reset),
        .start(start),
        .tx_data(tx_data),

        .txd(txd),
        .tx_done(tx_done)
    );


    always #5 clk = ~clk;

    initial begin
        clk = 1'b0;
        reset = 1'b1;
        start = 1'b0;
        tx_data = 0;
    end

    initial begin
        #20 reset = 1'b0;
        #20 tx_data = 8'haf; start = 1'b1;
        #10 start = 1'b0;
    end

endmodule
```

<div align="left">
    <strong>Simulation</strong>
  <img src="/assets/images/verilog/25.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice}   

- **결과 분석**   
  - FSM과 유사하게 state가 이동되는 것을 확인 가능하지만, 차이점은 UART_Tx에서는 `baudrate`를 기준으로 state가 전이됨    
  - 다만, 8bit의 state를 일일이 설정하는 것이 비효율적이라 이후에 개선할 예정   

&nbsp;


