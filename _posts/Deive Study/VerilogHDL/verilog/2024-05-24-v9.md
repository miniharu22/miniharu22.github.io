---
layout : single
title: "[Verilog] UART Tx/Rx Design"
categories: 
  - Verilog HDL (Vivado)
toc: true
toc_sticky: true
use_math: true
---

UART의 Tx(송신부)와 Rx(수신부) 설계 & 16 Oversampling을 이용한 신호의 무결성 개선  

## 0. UART Tx/Rx   

&nbsp;

<div align="center">
  <img src="/assets/images/verilog/33.png" width="60%" height="60%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **UART(Universal Asynchrous Receiver/Transmitter)**   
  - 원칙적으로 1:1 통신을 수행   
  - **비동기 신호 처리**를 수행하는데 특징은 다음과 같음   
    - 각 device에서 송수신 data timing을 알아서 맞춤    
    - 클럭을 전송하는 Master device가 없음    
    - device 내부에서 동기 신호를 생성해야 함    
    - data를 송수신할 때 Timing을 잘 맞춰야 함    

&nbsp;

<div align="center">
  <img src="/assets/images/verilog/3.jpg" width="60%" height="60%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **UART 이슈**   
  - 위 자료와 같이 start trigger의 타이밍에 따라 start signal이 baudrate 기준 1클럭이 안될 수 있음   
  - 이런 경우, 비동기 신호 처리를 수행하는 UART에 있어 신호의 위상 차가 발생할 확률이 높아지고 결과적으로 에러를 일으키게 됨    

&nbsp;

## 1. 16 Oversampling   

&nbsp;

<div align="center">
  <img src="/assets/images/verilog/34.png" width="60%" height="60%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **16 Oversampling**   
  - 각 데이터 비트를 16번으로 샘플링    
    - 16번 클럭이 들어온 후 다음 state로 전이 → baudrate 16배 증가시켜야함   
    - 각 비트의 중간지점(7번째 sampling 클럭)에서 샘플 채택    
  - 이를 통해 비트 타이밍을 더 정확하게 결정할 수 있음    

&nbsp;

- **Oversampling Tx/Rx**   
  - **시작 비트 검출**      
    - 수신기(UART Rx)는 라인이 낮은 상태(START 비트)를 16번 중 8번 이상 검출하면 시작 비트로 인식    
  - **비트 샘플링**   
    - 시작 비트 검출 후, 수신기는 데이터 비트를 sampling    
    - 각 비트의 중간 지점(= 8번째 sampling point)에서 sample을 채택 → 노이즈에 강함   
    - 다음 비트 == 16번 클럭 카운트한 이후     
  - **정확한 타이밍 유지**   
    - 매 비트를 16번 sampling하므로, 비트 타이밍의 정확성을 높임   
    - 클럭 차이로 인한 드리프트를 최소화할 수 있음    

&nbsp;

## 2. ASM Chart   

&nbsp;

<div align="center">
  <img src="/assets/images/verilog/40.png" width="80%" height="80%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

## 3. Design Source   
### 3-1. Baudrate-generator  

```verilog
module baudrate_generator #(
    parameter HERZ = 9600
) (
    input clk,
    input reset,

    output br_tick
);

    // $clog2(100_000_000 / HERZ / 16) = 3
    reg [$clog2(100_000_000/HERZ/16)-1:0] counter_reg, counter_next;
    reg tick_reg, tick_next;
    
    assign br_tick = tick_reg; // baudrate tick for baudrate with 16x oversampling

    always @(posedge clk, posedge reset) begin  // synchronous reset
        if (reset) begin            // reset the counter and tick
            counter_reg <= 0;       // reset counter
            tick_reg <= 1'b0;       // reset tick
        end else begin                      // on clock edge
            counter_reg <= counter_next;    // update counter
            tick_reg <= tick_next;          // update tick
        end
    end

    always @(*) begin                   // combinational logic for next state
        counter_next = counter_reg;     // default next counter value
        if (counter_reg == 3) begin     // if counter reaches 3 (for 16x oversampling)
            counter_next = 0;           // reset counter
            tick_next = 1'b1;           // set tick to 1
        end else begin                          // if counter is not at 3   
            counter_next = counter_reg + 1;     // increment counter
            tick_next = 1'b0;                   // set tick to 0   
        end
    end
endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/35.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

&nbsp;

### 3-2. Transmitter   

```verilog
module transmitter (
    input clk,
    input reset,
    input br_tick,
    input start,
    input [7:0] tx_data,

    output tx,
    output tx_done
);

    localparam IDLE = 0, START = 1, DATA = 2, STOP = 3; // states for the FSM

    reg [1:0] state, state_next;            // state register and next state
    reg tx_done_reg, tx_done_next;          // register for transmission done signal
    reg tx_reg, tx_next;                    // register for the tx signal 
    reg [7:0] data_tmp_reg, data_tmp_next;  // temporary register for data to be transmitted
    reg [3:0]
        br_cnt_reg, br_cnt_next;            // register for baudrate counter with 16x oversampling
    reg [2:0]
        data_bit_cnt_reg,                   // register for counting data bits transmitted
        data_bit_cnt_next;                  // next value for data bit count    


    assign tx = tx_reg;
    assign tx_done = tx_done_reg;


    always @(posedge clk, posedge reset) begin      // synchronous reset
        if (reset) begin
            state            <= IDLE;           
            tx_reg           <= 1'b1;
            tx_done_reg      <= 1'b0;
            br_cnt_reg       <= 0;
            data_bit_cnt_reg <= 0;
            data_tmp_reg     <= 0;
        end else begin
            state            <= state_next;
            tx_reg           <= tx_next;
            tx_done_reg      <= tx_done_next;
            br_cnt_reg       <= br_cnt_next;
            data_bit_cnt_reg <= data_bit_cnt_next;
            data_tmp_reg     <= data_tmp_next;
        end
    end


    always @(*) begin
        state_next        = state;
        tx_next           = tx_reg;
        tx_done_next      = tx_done_reg;
        br_cnt_next       = br_cnt_reg;
        data_bit_cnt_next = data_bit_cnt_reg;
        data_tmp_next     = data_tmp_reg;

        case (state)
            IDLE: begin
                tx_done_next = 1'b0;
                tx_next = 1'b1;
                if (start) begin
                    state_next        = START;
                    data_tmp_next     = tx_data;
                    br_cnt_next       = 0;
                    data_bit_cnt_next = 0;
                end
            end

            START: begin                    // transmit start bit    
                tx_next = 1'b0;             // start bit is always 0
                if (br_tick) begin          // on each baudrate tick
                    if (br_cnt_reg == 15) begin     // if baudrate counter reaches 15
                        state_next  = DATA;         // go to DATA state
                        br_cnt_next = 0;            // reset baudrate counter
                    end else begin                      // if baudrate counter is not at 15  
                        br_cnt_next = br_cnt_reg + 1;   // increment baudrate counter
                    end
                end
            end

            DATA: begin                             // transmit data bits
                tx_next = data_tmp_reg[0];          // transmit the least significant bit first
                if (br_tick) begin                  // on each baudrate tick
                    if (br_cnt_reg == 15) begin     // if baudrate counter reaches 15
                        if (data_bit_cnt_reg == 7) begin    // if all 8 bits are transmitted
                            state_next  = STOP;             // go to STOP state
                            br_cnt_next = 0;                // reset baudrate counter
                        end else begin                                      // if not all bits are transmitted
                            data_bit_cnt_next = data_bit_cnt_reg + 1;       // increment data bit count
                            data_tmp_next     = {1'b0, data_tmp_reg[7:1]};  // right shift the data to transmit the next bit
                            br_cnt_next       = 0;                          // reset baudrate counter      
                        end
                    end else begin                      // if baudrate counter is not at 15
                        br_cnt_next = br_cnt_reg + 1;   // increment baudrate counter
                    end
                end
            end

            STOP: begin                       // transmit stop bit
                tx_next = 1'b1;         // stop bit is always 1
                if (br_tick) begin      // on each baudrate tick
                    if (br_cnt_reg == 15) begin // if baudrate counter reaches 15
                        tx_done_next = 1'b1;    // set transmission done signal
                        state_next   = IDLE;    // go back to IDLE state
                    end else begin                      // if baudrate counter is not at 15  
                        br_cnt_next = br_cnt_reg + 1;   // increment baudrate counter
                    end
                end
            end
        endcase
    end

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/36.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

&nbsp;

### 3-3. Receiver   

```verilog
module receiver (
    input clk,
    input reset,
    input br_tick,
    input rx,

    output [7:0] rx_data,
    output rx_done
);

    localparam IDLE = 0, START = 1, DATA = 2, STOP = 3; // states for the FSM

    reg [1:0] state, state_next;            // state register and next state
    reg [7:0] rx_data_reg, rx_data_next;    // register for received data
    reg rx_done_reg, rx_done_next;          // register for reception done signal
    reg [4:0]   
        br_cnt_reg,                         // register for baudrate counter with 16x oversampling
        br_cnt_next;                        // next value for baudrate counter
    reg [2:0]
        data_bit_cnt_reg,                   // register for counting data bits received
        data_bit_cnt_next;                  // next value for data bit count    


    assign rx_data = rx_data_reg;
    assign rx_done = rx_done_reg;


    always @(posedge clk, posedge reset) begin      // synchronous reset
        if (reset) begin                    // reset the state machine and registers
            state            <= IDLE;       
            rx_data_reg      <= 0;
            rx_done_reg      <= 1'b0;
            br_cnt_reg       <= 0;
            data_bit_cnt_reg <= 0;
        end else begin
            state            <= state_next;
            rx_data_reg      <= rx_data_next;
            rx_done_reg      <= rx_done_next;
            br_cnt_reg       <= br_cnt_next;
            data_bit_cnt_reg <= data_bit_cnt_next;
        end
    end


    always @(*) begin           // combinational logic for next state
        state_next = state;
        br_cnt_next = br_cnt_reg;
        data_bit_cnt_next = data_bit_cnt_reg;
        rx_data_next = rx_data_reg;
        rx_done_next = rx_done_reg;

        case (state)
            IDLE: begin
                rx_done_next = 1'b0;
                if (rx == 1'b0) begin
                    br_cnt_next       = 0;
                    data_bit_cnt_next = 0;
                    rx_data_next      = 0;
                    state_next        = START;
                end
            end

            START: begin                // wait for start bit
                if (br_tick) begin              // on each baudrate tick
                    if (br_cnt_reg == 7) begin  // if baudrate counter reaches 7
                        br_cnt_next = 0;        // reset baudrate counter
                        state_next  = DATA;     // go to DATA state
                    end else begin                      // if baudrate counter is not at 7   
                        br_cnt_next = br_cnt_reg + 1;   // increment baudrate counter
                    end
                end
            end

            DATA: begin           // receive data bits
                if (br_tick) begin                              // on each baudrate tick
                    if (br_cnt_reg == 15) begin                 // if baudrate counter reaches 15
                        br_cnt_next  = 0;                       // reset baudrate counter
                        rx_data_next = {rx, rx_data_reg[7:1]};  // shift in the received bit
                        if (data_bit_cnt_reg == 7) begin    // if all 8 bits are received
                            state_next  = STOP;             // go to STOP state
                            br_cnt_next = 0;                // reset baudrate counter
                        end else begin                                  // if not all bits are received  
                            data_bit_cnt_next = data_bit_cnt_reg + 1;   // increment data bit count
                        end
                    end else begin                      // if baudrate counter is not at 15
                        br_cnt_next = br_cnt_next + 1;  // increment baudrate counter
                    end
                end
            end

            STOP: begin              // wait for stop bit
                if (br_tick) begin         // on each baudrate tick
                    if (br_cnt_reg == 23) begin     // if baudrate counter reaches 23
                        br_cnt_next  = 0;           // reset baudrate counter
                        state_next   = IDLE;        // go back to IDLE state
                        rx_done_next = 1'b1;        // set reception done signal
                    end else begin                      // if baudrate counter is not at 23
                        br_cnt_next = br_cnt_reg + 1;   // increment baudrate counter
                    end
                end
            end
        endcase
    end

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/37.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

&nbsp;

### 3-4. UART Tx/Rx System   

```verilog
module MyUART (
    input       clk,
    input       reset,
    input       tx_start,
    input [7:0] tx_data,
    input       rx,

    output       tx,
    output       tx_done,
    output [7:0] rx_data,
    output       rx_done
);

    wire w_br_tick;
    wire w_tx;


    baudrate_generator #(
        .HERZ(9600)
    ) U_BR_Gen (
        .clk  (clk),
        .reset(reset),

        .br_tick(w_br_tick)
    );


    transmitter U_TxD (
        .clk(clk),
        .reset(reset),
        .start(tx_start),
        .br_tick(w_br_tick),
        .tx_data(tx_data),

        .tx(tx),
        .tx_done(tx_done)
    );


    receiver U_RxD (
        .clk(clk),
        .reset(reset),
        .br_tick(w_br_tick),
        .rx(rx),

        .rx_data(rx_data),
        .rx_done(rx_done)
    );

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/38.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

&nbsp;

## 4. Testbench   
### 4-1. Tx System   

```verilog
module tb_MyUART ();
    reg        clk;
    reg        reset;
    reg        tx_start;
    reg  [7:0] tx_data;
    reg        rx;

    wire       tx;
    wire       tx_done;
    wire [7:0] rx_data;
    wire       rx_done;

    MyUART dut (
        .clk(clk),
        .reset(reset),
        .tx_start(tx_start),
        .tx_data(tx_data),
        .rx(rx),

        .tx(tx),
        .tx_done(tx_done),
        .rx_data(rx_data),
        .rx_done(rx_done)
    );


    always #5 clk = ~clk;

    initial begin
        clk = 1'b0;
        reset = 1'b1;
        tx_start = 1'b0;
        rx = 1'b1;
    end

    initial begin
        #20 reset = 1'b0;
        #100 tx_data = 8'b11000101;
        tx_start = 1'b1;
        #10 tx_start = 1'b0;
    end
endmodule
```

<div align="left">
    <strong>Simulation</strong>
  <img src="/assets/images/verilog/39.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice}   

&nbsp;

### 4-2. Rx System   

```verilog
module tb_MyUART ();
    reg        clk;
    reg        reset;
    reg        tx_start;
    reg  [7:0] tx_data;
    // reg        rx;

    wire       tx;
    wire       tx_done;
    wire [7:0] rx_data;
    wire       rx_done;

    MyUART dut (
        .clk(clk),
        .reset(reset),
        .tx_start(tx_start),
        .tx_data(tx_data),
        .rx(tx),

        .tx(tx),
        .tx_done(tx_done),
        .rx_data(rx_data),
        .rx_done(rx_done)
    );


    always #5 clk = ~clk;

    initial begin
        clk = 1'b0;
        reset = 1'b1;
        tx_start = 1'b0;
        // rx = 1'b1;
    end

    initial begin
        #20 reset = 1'b0;
        #100 tx_data = 8'b11000101;
        tx_start = 1'b1;
        #10 tx_start = 1'b0;
    end
endmodule
```

<div align="left">
    <strong>Simulation</strong>
  <img src="/assets/images/verilog/41.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice}  

- **결과 분석**   
  - `tx_start`가 High에 진입하는 시점에서 Tx system에서는 전송 data `11000101`을 Right shift를 통해 Rx system에 전달    
  - Right shift가 끝나기 직전에 data를 전달받은 Rx system은 수신 data `11000101`을 저장, 수신이 끝나는대로 `rx_done`을 High 상태로 설정하여 수신 완료를 표현   

&nbsp;

## 5. Oversampling Analysis   

&nbsp;

<div align="center">
  <img src="/assets/images/verilog/42.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
  <img src="/assets/images/verilog/43.png" width="70%" height="70%" alt=""/>
</div>

&nbsp;

- **Oversampling 미사용 case**   
  - 본래 start bit가 일정 클럭 유지되었을 때는 reciever의 출력이 입력 값과 동일함  
  - 다만, start bit가 일정 클럭 이상 유지되지 못해 위 결과와 같이 끊겼을 경우, 첫번째 수신 비트를 인식하지 못하고 stop bit(High)를 수신 비트로 인식해 **Miss Match error**가 발생함   

&nbsp;

<div align="center">
  <img src="/assets/images/verilog/44.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **Oversampling 사용 case**   
  - oversampling을 사용하는 경우 start bit가 무조건 일정 클럭 유지되기 때문에 receiver에서 정상적으로 데이터를 인식함    

&nbsp;

- **Oversampling을 사용하는 이유**   
  - **Noise 면역**   
    - Receiver가 각 비트에서 여러 번 sampling함으로써 noise와 data를 구분할 수 있음  
    - 데이트 비트 중간 sample의 양 옆 2~3 비트가 같은 값인지를 비교해서 noise와 data를 구분 (다만 이번에는 구현하지 않음)    
  - **클럭 오차 보상**   
    - UART는 기본적으로 비동기 통신이므로 양 통신 기기의 baudrate 위상 차이가 발생 가능    
    - Oversampling은 이 위상 차이의 간격을 좁혀주고 오차가 작은 경우 보상도 가능   
  - **Start & Stop bit 클럭 보장**   
    - UART의 start signal은 비동기 신호이고 이로 인해 start bit가 의도치 않게 끊길 수 있음, 이 경우 상기한 결과대로 Miss match error가 발생 가능    
    - Oversampling은 start bit를 일정 클럭 유지시켜줌으로써 Receiver가 start signal을 정상적으로 인지할 수 있도록 보장해줌      

&nbsp;