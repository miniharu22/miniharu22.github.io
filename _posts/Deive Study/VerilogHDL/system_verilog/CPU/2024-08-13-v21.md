---
layout : single
title: "[Verilog] CPU #2"
categories: 
  - Verilog HDL (Vivado)
toc: true
toc_sticky: true
use_math: true
---

Control Unit 모듈 설계, Dedicated Processor 모듈을 구현하여 D.P와 C.U 병합 설계   

## 0. Data path & ASM Chart   

&nbsp;

<div align="left">
  <img src="/assets/images/verilog/11.jpg" width="90%" height="90%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

<div align="left">
  <img src="/assets/images/verilog/65.png" width="40%" height="40%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

## 1. Control Unit Module    

```verilog  
// Control Unit module for managing states and control signals
module ControlUnit (
    input clk,
    input reset,        
    input ALt10,            // Comparator output : 1 if A < 10

    output reg ASrcMuxSel,  // Mux select signal 
    output reg ALoad,       // Load enable signal for A register
    output reg OutBufSel    // Output buffer enable signal   
);

    localparam S0 = 3'd0,   // state 0 : Initialization
               S1 = 3'd1,   // state 1 : Check if A < 10
               S2 = 3'd2,   // state 2 : Output A
               S3 = 3'd3,   // state 3 : Increment A 
               S4 = 3'd4;   // state 4 : HALT (stop state)

    reg [2:0] state, state_next; // State register


    always @(posedge clk, posedge reset) begin
        if (reset) state <= S0;
        else state <= state_next;
    end

    always @(*) begin
        state_next = state; // Default stay in current state

        case (state)
            S0: state_next = S1;    // Initialization done -> move to S1
            S1: begin               
                if (ALt10) 
                    state_next = S2;    // If A < 10, move to output state
                else 
                    state_next = S4;    // Else, go to HATT State
            end
            S2: state_next = S3;        // Output state -> Increment state
            S3: state_next = S1;        // After Increment -> check A < 10 again
            S4: state_next = S4;        // HALT state : remain
            default: state_next = S1;   // Default Fallback
        endcase
    end

    // Output Logic
    always @(*) begin
        // Default values : deassert all control signals
        ASrcMuxSel = 1'b0;
        ALoad = 1'b0;
        OutBufSel = 1'b0;

        case (state)
            S0: begin
                ASrcMuxSel = 1'b0;  // selcct 0 input for Mux
                ALoad = 1'b1;       // load 0 into A register
                OutBufSel = 1'b0;   // output buffer disabled
            end
            S1: begin               
                ASrcMuxSel = 1'b1;  // select adder output (A+1) for Mux
                ALoad = 1'b0;       // hold A regiseter value
                OutBufSel = 1'b0;   // output buffer disabled
            end
            S2: begin   
                ASrcMuxSel = 1'b1;  // selcet adder output (A+1)
                ALoad = 1'b0;       // hold A regiseter value
                OutBufSel = 1'b1;   // enable output buffer
            end
            S3: begin
                ASrcMuxSel = 1'b1;  // select adder output (A+1)
                ALoad = 1'b1;       // load incremented value into A register
                OutBufSel = 1'b0;   // output buffer disabled
            end
            S4: begin
                ASrcMuxSel = 1'b1;  // select add output (A+1)
                ALoad = 1'b0;       // Hold A register
                OutBufSel = 1'b0;   // output buffer disabled
            end
            default: begin
                ASrcMuxSel = 1'b0; 
                ALoad = 1'b0;
                OutBufSel = 1'b0;
            end
        endcase
    end
endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/67.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **Control Unit**   
  - 시스템 내 다른 장치의 작동을 지시하며 명령 실행을 관리하고 조정    
    - **Instruction Fetch** : 메모리에서 명령 검색   
    - **Instruction Decode** : 명령 해석   
    - **Control Signal Generation** : ALU, 레지스터, 메모리 및 I/O 장치와 같은 다른 구성 요소(Data Path)의 작동을 제어하는 신호 생성    
    - **Sequencing** : 작업 순서 결정   
    - **Synchronize** : 클럭 동기화    
  - Control Unit은 클럭에 따라 state를 결정하고 각 state마다 명령을 Data Path에 전송    


&nbsp;

## 2. Dedicated Processor (DP + CU)

```verilog
// Integrates Contol Unit and Data Path Modules
module DedicatedProcessor (
    input clk,
    input reset,

    output [7:0] out
);

    // Internal control and status signals
    wire w_ASrcMuxSel;  // Control: mux select for A register input
    wire w_ALoad;       // Control: load enable for A register
    wire w_OutBufSel;   // Control: output buffer enable
    wire w_ALt10;       // Status: A < 10 comparison result

    ControlUnit U_CU (
        .clk  (clk),
        .reset(reset),
        .ALt10(w_ALt10),

        .ASrcMuxSel(w_ASrcMuxSel),
        .ALoad(w_ALoad),
        .OutBufSel(w_OutBufSel)
    );


    DataPath U_DP (
        .clk(clk),
        .reset(reset),
        .ASrcMuxSel(w_ASrcMuxSel),
        .ALoad(w_ALoad),
        .OutBufSel(w_OutBufSel),

        .ALt10(w_ALt10),
        .out  (out)
    );

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/68.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 


&nbsp;

## 3. Test Bench     

```verilog
module tb_DedicatedProcessor ();
    reg clk;
    reg reset;

    wire [7:0] out;


    DedicatedProcessor dut (
        .clk  (clk),
        .reset(reset),

        .out(out)
    );

    always #5 clk = ~clk;

    initial begin
        clk = 0;
        reset = 1;

        #30 reset = 0;
    end
endmodule
```

<div align="left">
    <strong>Simulation</strong>
  <img src="/assets/images/verilog/69.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **결과 분석**   
  - `outBufSel==1`일 때 결과값 출력 (state == 2)   
  - `ASrcMuxSel == 1 & ALoad == 1`일 때 Adder 연산 수행 (state == 3)   
  - 1클럭마다 state가 변하기 때문에 한 클럭만 출력하고 두 클럭은 ZZ 상태    

&nbsp;

## 4. Final version    
### 4-1. Data Path   

&nbsp;

<div align="left">
  <img src="/assets/images/verilog/12.jpg" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

### 4-2. Data Path module 

```verilog
// DataPath Module
module DataPath(
    input clk,          
    input reset,
    input ASrcMuxSel,   // Multiplexer select signal for A register input
    input ALoad,        // Load enable signal for A register
    input OutBufSel,    // Output buffer enable signal

    output ALt10,       // Comparator output (A < 10)
    output [7:0] out    // Output data
    );

    // Internal wires
    // w_AdderResult: Output of the adder
    // w_MuxOut: Output of the 2-to-1 multiplexer
    // w_ARegOut: Output of the A register
    wire [7:0] w_AdderResult, w_MuxOut, w_ARegOut;

    // 2-to-1 Multiplexer
    mux_2x1 U_MUX(
            .sel(ASrcMuxSel),   // Select signal: 0 for 0, 1 for adder result
            .a(8'b0),           // Input a: constant zero
            .b(w_AdderResult),  // Input b: result of adder (A + 1)
        
            .y(w_MuxOut)        // Output: multiplexer output
        );

    // A Register
    register U_A_Reg(   
        .clk(clk),
        .reset(reset),
        .load(ALoad),   // Load enable signal
        .d(w_MuxOut),   // Data input from multiplexer
        .q(w_ARegOut)   // Output of the A register
    );

    // Comparator: checks if A < 10
    comparator U_Comp(
        .a(w_ARegOut),  // Input a: output of A register
        .b(8'd10),      // Input b: constant value 10

        .lt(ALt10)      // Output: less than signal (A < 10)
    );

    // Adder: adds 1 to the output of A register
    adder U_Adder(
        .a(w_ARegOut),  // Input a: output of A register
        .b(8'b1),       // Input b: constant value 1
        
        .y(w_AdderResult)   // Output: result of the addition (A + 1)
    );

    // Output Buffer: outputs the value of A register when enabled
    // outBuff U_OutBuf(
    //     .en(OutBufSel), // Enable signal for output buffer
    //     .a(w_ARegOut),  // Input: output of A register

    //     .y(out)         // Output: final output of the data path
    // );

    register U_Out_Reg(
        .clk(clk),
        .reset(reset),
        .load(OutBufSel),
        .d(w_ARegOut),
        .q(out)
    );

endmodule
```

&nbsp;

### 4-3. Control Unit module   

```verilog
// Control Unit module for managing states and control signals
module ControlUnit (
    input clk,
    input reset,        
    input ALt10,            // Comparator output : 1 if A < 10

    output reg ASrcMuxSel,  // Mux select signal 
    output reg ALoad,       // Load enable signal for A register
    output reg OutBufSel    // Output buffer enable signal   
);

    localparam S0 = 3'd0,   // state 0 : Initialization
               S1 = 3'd1,   // state 1 : Check if A < 10
               S2 = 3'd2,   // state 2 : Output A
               S3 = 3'd3,   // state 3 : Increment A 
               S4 = 3'd4;   // state 4 : HALT (stop state)

    reg [2:0] state, state_next; // State register


    always @(posedge clk, posedge reset) begin
        if (reset) state <= S0;
        else state <= state_next;
    end

    always @(*) begin
        state_next = state; // Default stay in current state

        case (state)
            S0: state_next = S1;    // Initialization done -> move to S1
            // S1: begin               
            //     if (ALt10) 
            //         state_next = S2;    // If A < 10, move to output state
            //     else 
            //         state_next = S4;    // Else, go to HATT State
            // end
            S1: begin
                if (ALt10) state_next = S2;
                else state_next = S0;
            end
            S2: state_next = S3;        // Output state -> Increment state
            S3: state_next = S1;        // After Increment -> check A < 10 again
            S4: state_next = S4;        // HALT state : remain
            default: state_next = S1;   // Default Fallback
        endcase
    end

    // Output Logic
    always @(*) begin
        // Default values : deassert all control signals
        ASrcMuxSel = 1'b0;
        ALoad = 1'b0;
        OutBufSel = 1'b0;

        case (state)
            S0: begin
                ASrcMuxSel = 1'b0;  // selcct 0 input for Mux
                ALoad = 1'b1;       // load 0 into A register
                OutBufSel = 1'b0;   // output buffer disabled
            end
            S1: begin               
                ASrcMuxSel = 1'b1;  // select adder output (A+1) for Mux
                ALoad = 1'b0;       // hold A regiseter value
                OutBufSel = 1'b0;   // output buffer disabled
            end
            S2: begin   
                ASrcMuxSel = 1'b1;  // selcet adder output (A+1)
                ALoad = 1'b0;       // hold A regiseter value
                OutBufSel = 1'b1;   // enable output buffer
            end
            S3: begin
                ASrcMuxSel = 1'b1;  // select adder output (A+1)
                ALoad = 1'b1;       // load incremented value into A register
                OutBufSel = 1'b0;   // output buffer disabled
            end
            S4: begin
                ASrcMuxSel = 1'b1;  // select add output (A+1)
                ALoad = 1'b0;       // Hold A register
                OutBufSel = 1'b0;   // output buffer disabled
            end
            default: begin
                ASrcMuxSel = 1'b0; 
                ALoad = 1'b0;
                OutBufSel = 1'b0;
            end
        endcase
    end
endmodule
```

<div align="left">
    <strong>Simulation : Increment Process</strong>
  <img src="/assets/images/verilog/71.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **결과 분석**  
  - 기본 버전과 동작 방식은 동일      
  -  `outBufSel==1`일 때 결과값 출력 (state == 2)   
  - `ASrcMuxSel == 1 & ALoad == 1`일 때 Adder 연산 수행 (state == 3) 
  - 다만, Buffer를 register로 대체하였기 때문에 state를 저장 가능하며 이를 통해 `ALT10`이 Z 상태로 진입하지 않음   


&nbsp;

<div align="left">
    <strong>Simulation : Infinie Loop</strong>
  <img src="/assets/images/verilog/72.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice}    

- **결과 분석**   
  - Control Unit module의 Comparator state 조건문에서 무한 루프를 설정    
  - 그 결과, HALT state에 진입하지 않음    
  - 이를 통해 0~9까지 증가하고 다시 0부터 시작하는 시스템을 구현 가능    

&nbsp;

