---
layout : single
title: "[Verilog] CPU #1"
categories: 
  - Verilog HDL (Vivado)
toc: true
toc_sticky: true
use_math: true
---

기본적인 CPU(Central Processing Unit) 개념 정리, Data Path 모듈 설계   

## 0. About CPU 
### 0-1. CISC vs RISC     

&nbsp;

<div align="left">
  <img src="/assets/images/verilog/63.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **CISC(Complex Instruction Set Computer)**   
  - **복잡한 명령어**   
    - CISC 프로세서에는 매우 복잡하고 단일 명령어   
    - 여러 개의 Low-Level 작업(예: 산술과 결합된 메모링 액세스)을 수행할 수 있는 대규모 명령어 세트를 보유   
  - **가변 명렁어 길이**   
    - CISC 아키텍쳐의 명령어는 길이가 다양함   
    - 이때, 더 복잡한 작업이 더 적은 명령어로 인코딩되므로 프로그램의 크기가 축소될 수 있음    
  - **적은 레지스터**   
    - 각 명령어의 복잡성과 기능으로 인해 CISC는 더 적은 수의 레지스터를 가지며 메모리 작업에 더 많이 의존    
  - **Microcode**   
    - 복잡한 명령어를 프로세서에서 내부적으로 처리하는 간단한 단계로 분해됨   

&nbsp;

- **CISC의 장단점**   
  - **장점**   
    - **High-Level 작업**   
      - (고급) 명령어가 많아 높은 수준의 작업이 가능   
    - **프로그램 크기의 효율성**   
      - 복잡한 명렁어를 사용하면 프로그램을 작성하는데 요구되는 명령어의 수가 줄어듦   
      - 그 결과, 프로그램의 크기가 더 가벼워지며 이는 컴파일러의 단순화로 이어짐   
  - **단점**   
    - **전력 소비**   
      - 복잡한 명령에는 더 많은 연산 능력을 요구하므로 더 많은 전력을 소비   
    - **복잡한 구조**   
    - **느린 클럭 속도**   
      - CPU 구조가 복잡해지므로 상대적으로 클럭 속도가 느릴 수 있음    

&nbsp;
&nbsp;

- **RISC(Reduced Instruction Set Computer)**   
  - **간단한 명령어**   
    - RISC 프로세서는 한 클록 사이클 내에서 실행될 수 있는 작고 간단한 명령어 세트를 사용, 이러한 단순성은 프로그램을 보다 효율적으로 실행할 수 있게 함  
    - CISC에서 많이 사용하는 명령어들로 구성됨    
    - 명령어가 적기 때문에 RISC에 존재하지 않는 명령을 구현하기 위해 소프트웨어적으로 더 무거워짐   
  - **Load/Store 아키텍쳐**   
    - 일반적으로 레지스터 간에 작업이 수행되며 특정 로드 및 저장 명령어를 통해 메모리에 액세스함    
    - 산술/논리 연산에서 메모리 액세스를 분리하면 명령어 설계가 단순화됨    
  - **고정 명령어 길이**   
    - RISC 명령어는 일반적으로 길이가 고정되어 명령어 디코딩 프로세스를 단순화함   
  - **많은 수의 레지스터**   
    - 메모리 액세르를 최소화하기 위해 더 많은 수의 레지스터를 가짐   

&nbsp;

- **RISC의 장단점**   
  - **장점**   
    - **속도**   
      - 명령 길이가 일정하고 명령어 세트가 간단하여 대부분의 명령이 하나의 클록 주기에서 실행되므로 처리속도가 빠름   
    - **단순한 구조**  
    - **전력 소비 효율**   
    - **최적화**    
      - 단순한 명령에 대해서 컴파일러를 단순화할 수 있음   
  - **단점**   
    - **적은 명령어**   
      - 각 명령어의 작업이 적으므로 CISC 프로세서가 더 적은 수의 복잡한 명령어로 수행할 수 있는 작업을 RISC가 수행하려면 더 많은 명령어가 요구됨    
      - 이로 인해 코드 작성이 복잡해짐    
    - **호환성**   
      - 단순함은 속도와 전력 측면에서는 이점이 있지만, RISC 아키텍쳐가 기본적으로 지원하지 않는 복잡한 명령어가 필요할 경우 호환성이 저하됨   
    - **컴파일러에 대한 의존성**   
      - 고급 명령어를 RISC의 적은 명령어로 구현하기에 컴파일러에 크게 의존됨   
      - 이로 인해 SW가 무거워질 수 있음    

&nbsp;

### 0-2. Von-Neumann vs Harvard

&nbsp;

<div align="left">
  <img src="/assets/images/verilog/64.png" width="60%" height="60%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **폰 노이만 구조**   
  - 1개의 메모리   
  - 일반적인 PC   
  - 프로그램 구동 명령어를 외부 메모리(SSD, HDD)에 저장    
  - 외부 메모리에 저장된 것을 RAM으로 가져오는 Laoding 작업이 요구됨    

&nbsp;

- **하버드 구조**   
  - 2개의 메모리(명령어 메모리 & 데이터 메모리)   
  - 임베디드 구조   
  - Program memory : ROM, Flash    
  - Data memory : RAM    

&nbsp;

## 1. Data path & ASM Chart   

&nbsp;

<div align="left">
  <img src="/assets/images/verilog/11.jpg" width="90%" height="90%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

<div align="left">
  <img src="/assets/images/verilog/65.png" width="40%" height="40%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;


## 2. Data Path Module    

```verilog
// DataPath Module
module DataPath(
    input clk,          
    input reset,
    input ASrcMuxSel,   // Multiplexer select signal for A register input
    input ALoad,        // Load enable signal for A register
    input OutBufSel,    // Output buffer enable signal

    output ALt10,       // Comparator output (A < 10)
    output [7:0] out    // Output data
    );

    // Internal wires
    // w_AdderResult: Output of the adder
    // w_MuxOut: Output of the 2-to-1 multiplexer
    // w_ARegOut: Output of the A register
    wire [7:0] w_AdderResult, w_MuxOut, w_ARegOut;

    // 2-to-1 Multiplexer
    mux_2x1 U_MUX(
            .sel(ASrcMuxSel),   // Select signal: 0 for 0, 1 for adder result
            .a(8'b0),           // Input a: constant zero
            .b(w_AdderResult),  // Input b: result of adder (A + 1)
        
            .y(w_MuxOut)        // Output: multiplexer output
        );

    // A Register
    register U_A_Reg(   
        .clk(clk),
        .reset(reset),
        .load(ALoad),   // Load enable signal
        .d(w_MuxOut),   // Data input from multiplexer
        .q(w_ARegOut)   // Output of the A register
    );

    // Comparator: checks if A < 10
    comparator U_Comp(
        .a(w_ARegOut),  // Input a: output of A register
        .b(8'd10),      // Input b: constant value 10

        .lt(ALt10)      // Output: less than signal (A < 10)
    );

    // Adder: adds 1 to the output of A register
    adder U_Adder(
        .a(w_ARegOut),  // Input a: output of A register
        .b(8'b1),       // Input b: constant value 1
        
        .y(w_AdderResult)   // Output: result of the addition (A + 1)
    );

    // Output Buffer: outputs the value of A register when enabled
    outBuff U_OutBuf(
        .en(OutBufSel), // Enable signal for output buffer
        .a(w_ARegOut),  // Input: output of A register

        .y(out)         // Output: final output of the data path
    );

endmodule

// 2-to-1 Multiplexer Module
module mux_2x1 (
        input [7:0] a,     
        input [7:0] b,     
        input sel,         
        
        output reg [7:0] y  
    );

    // 2-to-1 Multiplexer: selects between two 8-bit inputs based on sel
    always @(*) begin       
        case(sel)           // sel signal determines which input to select
            1'b0 : y = a;   // If sel is 0, output is a
            1'b1 : y = b;   // If sel is 1, output is b
        endcase
    end
endmodule

// Register Module
module register (
    input clk,
    input reset,
    input load,
    input [7:0] d,
    output [7:0] q
);

    // Register: stores an 8-bit value with load and reset functionality
    reg [7:0] d_reg, d_next;
    assign q = d_reg;

// Sequential logic: updates d_reg on clock edge or reset
    always @(posedge clk , posedge reset) begin
        if (reset) d_reg <= 0;  // Reset the register to 0
        else d_reg <= d_next;   // Update register with next value
end

always @(*) begin
    if (load) d_next = d;   // If load is high, next value is d
    else d_next = d_reg;    // If load is low, next value remains the same
end
endmodule


// Comparator module: outputs 1 if a < b
module comparator (
    input [7:0] a,
    input [7:0] b,

    output lt           // Output: 1 if a < b, else 0
);
    assign lt = a < b;  // Compare a and b, output 1 if a is less than b
endmodule


// Adder module: outputs a + b
module adder (
    input [7:0] a,
    input [7:0] b,
    
    output [7:0] y
);
    assign y = a + b;   // Perform addition of a and b, output the result

endmodule

// Output Buffer module: outputs a when enabled, otherwise high impedance
module outBuff(
    input en,
    input [7:0] a,

    output [7:0] y
);

    // If en=1, output a; else output high-impedance
    assign y = en ? a : 8'bz; 
endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/66.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **Data Path**   
  - 데이터 처리 작업(연산)을 수행   
  - 데이터의 이동 경로는 Control Unit이 제어하고 Data Path는 Control Unit의 신호에 따라 연산과 데이터 이동을 수행    

&nbsp;

- **Data Path의 구성요소**   
  - **Multiplexr**   
    - A 레지스터로 입력될 데이터를 선택   
    - 초기화할 때 A=0으로 세팅   
    - 이후에는 A+1 값을 계속 선택해서 카운트업 가능    
  - **A register**   
    - 현재 A 값 저장   
    - A 값을 계속 저장/업데이트    
  - **Adder**    
    - A를 1씩 증가시키는 연산 계산 수행    
    - Multiplxer를 통해 A 레지스터로 다시 저장됨    
  - **Comparator**   
    - A 값이 10 미만인지 비교    
    - 컨트롤 유닛이 A < 10 상태를 감지하여 루프 제어에 사용    
  - **Output Buffer**   
    - A 값을 외부로 출력할 지 결정    
    - 마지막 출력 단계에서만 활성화   

&nbsp;

- **동작 과정**   
  - **초기화/시작**   
    - `ASrcMuxSel=0`, `ALoad=1`로 설정    
    - Mux는 0을 선택 → A 레지스터에 0 저장   
    - A = 0으로 초기화   
  - **루프 동작**   
    - A < 10인지 comparator가 계속 확인      
    - A가 10미만일 경우, `ASrcMuxSel=1`, `ALoad=1`로 설정 → Mux는 `y = A+1`를 선택   
    - Adder가 A + 1 수행, Mux를 통해 A 레지스터에 저장   
    - 루프 반복    
  - **A=10 도달**   
    - comparator가 A=10에서 `ALt10=0` 출력    
    - 루프 종료 후, `OutBufSel=1`으로 설정    
    - 최종 A 값 출력     

&nbsp;

