---
layout : single
title: "[System Verilog] RISC-V : RV32I R-type"
categories: 
  - Verilog HDL (Vivado)
toc: true
toc_sticky: true
use_math: true
---

정수형 명령어 세트인 RV321의 R-type 구현   

## 0. About RV32I    

&nbsp;

<div align="left">
  <img src="/assets/images/verilog/73.png" width="30%" height="30%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **RV32I**    
  - Base Integer Instruction Set    
  - 곱셈, 나눗셈 연산이 없는 정수형 명령어 세트    

&nbsp;

<div align="left">
  <img src="/assets/images/verilog/74.png" width="90%" height="90%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **Instruction 구조**   
  - RV32I의 Register File : 32개의 레지스터 구성   
  - 명령어 구조는 32bit 고정
    - 이는 RISC 아키텍쳐의 특징    
  - R ~ J-type이 존재    

&nbsp;

## 1. CPU Basic Module   

&nbsp;

<div align="left">
  <img src="/assets/images/verilog/13.jpg" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **CPU 기본 모듈**
  - **Control Unit**  
    - Machine Code에 따라 Register를 enable시키거나 ALU 동작을 결정        
  - **ALU**  
    - Control Unit의 명령에 따라 산술 연산을 수행    
    - 연산의 대상이 되는 값의 주소는 Register File로부터 전달 받음    
  - **Register File**    
    - 32개 레지스터  
    - Instruction Memory로부터의 Machine Code의 RD, RS1, RS2를 입력받음     
  - **PC Register**   
    - Program Counter 레지스터   
    - Instruction이 실행할 위치(Instruction Memory)를 가지고 있는 레지스터   
  - **Instruction Memory** 
    - Machine Code가 저장되어 있음    
    - Program Counter로부터 주소 포인터를 입력받아 해당 주소의 Machine Code 출력     
  - **Data Memory**   
    - 읽기 전용 Memory(ROM, Read Only Memory)   

&nbsp;

## 2. RV32I R-Type Instruction    

&nbsp;

<div align="left">
  <img src="/assets/images/verilog/75.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **About Instruction**    
  - `OP Code` : Type을 나타내는 코드   
  - `Funct7`, `Funct3` : 해당 Type에서 어떤 연산을 수행하는지 설정(ALU 동작)   
  - `rd` : 연산할 값을 저장할 주소   
  - `rs` : 연산할 소스의 주소    

&nbsp;
    
## 3. RV32I R-Type Total Stucture    

&nbsp;

<div align="left">
  <img src="/assets/images/verilog/14.jpg" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

## 4. Data Path module

```verilog
`timescale 1ns / 1ps

// Data Path module for a simplified RV32I CPU
module DataPath (
    input logic        clk,
    input logic        reset,
    input logic [31:0] machineCode,     // 32bit Machine Instruction
    input logic        regFile_wr_en,   // Register File write enable signal
    input logic [2:0] ALUControl,      // ALU Control Signal (Operation Select)

    output logic [31:0] instrMemRAddr   // Instruction memory read address
                                        // Program Counter
);

    wire [31:0] w_ALUResult, w_RegFileRData1, w_RegFileRData2;
    // w_ALUResult : Result from ALU
    // w_RegFileData1 : Read data 1 from register file  
    // w_RegFileData2 : Read data 2 from register file

    // Program Counter Register
    Register U_PC (  
        .clk  (clk),
        .reset(reset),
        .d    (32'b0),  

        .q(instrMemRAddr)   // Output current PC address
    );

    // Register File stores 32 general-purpose 32bit registers
    RegisterFile U_RegisterFile (
        .clk   (clk),
        .wr_en (regFile_wr_en),         // Write enable signal 
        .RAddr1(machineCode[19:15]),    // Source register 1 address (rs1)
        .RAddr2(machineCode[24:20]),    // Source register 2 address (rs2)
        .WAddr (machineCode[11:7]),     // Destination register address (rd)
        .WData (w_ALUResult),           // Data to write to destination register

        .RData1(w_RegFileRData1),       // Read data 1 output
        .RData2(w_RegFileRData2)        // Read data 2 output
    );

    // ALU performs arithmetic and logic operations
    ALU U_ALU (
        .a         (w_RegFileRData1),   // Operand a from register file
        .b         (w_RegFileRData2),   // Operand b from register file
        .ALUControl(ALUControl),        // ALU operation selector

        .result(w_ALUResult)            // Output result
    );
endmodule

// Register File module (32x32)
module RegisterFile (
    input logic        clk,
    input logic        wr_en,
    input logic [ 4:0] RAddr1,  // Read address 1 (rs1)
    input logic [ 4:0] RAddr2,  // Read address 2 (rs2)
    input logic [ 4:0] WAddr,   // Write address (rd)
    input logic [31:0] WData,   // Write data

    output logic [31:0] RData1, // Output read data 1
    output logic [31:0] RData2  // Output read data 2
);
    // Register file memory (32 registers, 32bit wide)
    logic [31:0] RegFile[0:31]; 

    // Initialize some registers for demo
    initial begin     
        RegFile[0] = 32'd0;
        RegFile[1] = 32'd1;
        RegFile[2] = 32'd2;
        RegFile[3] = 32'd3;
        RegFile[4] = 32'd4;
        RegFile[5] = 32'd5;
    end

    // Write operation : synchronous on clock edge
    always_ff @(posedge clk) begin
        if (wr_en) RegFile[WAddr] <= WData; // Write data to WAddr register
    end

    // Read operation (combinational)
    // If read address is 0, always output 0
    assign RData1 = (RAddr1 != 0) ? RegFile[RAddr1] : 0;
    assign RData2 = (RAddr2 != 0) ? RegFile[RAddr2] : 0;
endmodule


// Single 32bit Register module (Program Counter)
module Register (
    input logic        clk,
    input logic        reset,
    input logic [31:0] d,   // Data input

    output logic [31:0] q   // Data output
);

    // Sequential logic : update q in clock edge or reset
    always_ff @(posedge clk, posedge reset) begin : register  
        if (reset) begin
            q <= 0;     // Reset output to 0
        end else begin
            q <= d;     // Update with input data
        end
    end : register
endmodule

// ALU module for 32bit operations
module ALU (
    input logic [31:0] a,           // Operand a
    input logic [31:0] b,           // Operand b
    input logic [ 2:0] ALUControl,  // ALU control code

    output logic [31:0] result      // Output result
);
    // Define possible ALU operations
    enum logic [2:0] {
        ADD = 3'b000,
        SUB = 3'b001,
        AND = 3'b010,
        OR  = 3'b011
    } alu_op_t;

    // Comnination logic for ALU operation
    always_comb begin
        case (ALUControl)
            ADD:     result = a + b;    // Addition
            SUB:     result = a - b;    // Substraction
            AND:     result = a & b;    // Bitwise AND
            OR:      result = a | b;    // Bitwise OR
            default: result = 32'bx;    // Undefine operation
        endcase
    end
endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/76.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice}  


&nbsp;

## 5. Core module
### 5-1. Control Unit    

```verilog
// Control Unit module
// Generates control signals for the data path based on the opcode & function fields
module ControlUnit (  
    input logic [6:0] op,       // Opcode field from instruction memory
    input logic [2:0] funct3,   // funct3 field
    input logic [6:0] funct7,   // funct7 field

    output logic       regFile_wr_en,   // Output control : Register File write enable
    output logic [2:0] ALUControl       // Output control : ALU operation selector
);

    logic controls; // Internal control signal for regfile write enable

    assign regFile_wr_en = controls;

    // Determine register file write enable based on instruction opcode
    always_comb begin
        case (op)
            7'b0110011: controls = 1'b1;  // R-Type (register-register instructions)
            7'b0000011: controls = 1'b0;  // IL-Type (load instructions)
            7'b0010011: controls = 1'b0;  // I-Type (immediate instructions)
            7'b0100011: controls = 1'b0;  // S-Type (store instructions)
            7'b1100011: controls = 1'b0;  // B-Type (branch instructions)
            7'b0110111: controls = 1'b0;  // LUI-Type (load upper immediate)
            7'b0010111: controls = 1'b0;  // AUIPC-Type (add upper immediate to PC)
            7'b1101111: controls = 1'b0;  // J-Type (jump instructions)
            7'b1100111: controls = 1'b0;  // JI-Type (jump & link register)
            default: controls = 1'b0;  // JI-Type (disble register file write)
        endcase
    end

    // Determine ALU operation based on funct7 & funct3 fields
    always_comb begin
        // Create 4bit selector
        case ({funct7[5], funct3})  
            4'b0000: ALUControl = 3'b000; // ADD
            4'b1000: ALUControl = 3'b001; // SUB
            4'b0110: ALUControl = 3'b011; // OR   
            4'b0111: ALUControl = 3'b010; // AND
            default: ALUControl = 3'bx;   // Undefined operation
        endcase
    end

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/77.png" width="80%" height="80%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

&nbsp;

### 5-2. Combined Data Path & Control Unit for Core    

```verilog
module CPU_Core (
    input logic        clk,
    input logic        reset,
    input logic [31:0] machineCode,

    output logic [31:0] instrMemRAddr
);

    logic w_regFile_wr_en;
    logic [2:0] w_ALUControl;

    ControlUnit U_ControlUnit (  
        .op(machineCode[6:0]),
        .funct3(machineCode[14:12]),
        .funct7(machineCode[31:25]),

        .regFile_wr_en(w_regFile_wr_en),
        .ALUControl(w_ALUControl)
    );

    DataPath U_DataPath (
        .clk(clk),
        .reset(reset),
        .machineCode(machineCode),
        .regFile_wr_en(w_regFile_wr_en),
        .ALUControl(w_ALUControl),

        .instrMemRAddr(instrMemRAddr)
    );
endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/78.png" width="80%" height="80%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/84.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

&nbsp;

## 6. RV321    
### 6-1. Instruction Memory module   

```verilog
// Storing Machine Code
module InstructionMemory (  
    input logic [31:0] addr,

    output logic [31:0] data
);

    logic [31:0] rom[0:63];

    initial begin
        rom[0] = 31'h005201b3;  // Add x6 x4 x5
        rom[1] = 31'h401184b3;  // Sub x7 x2 x1 => 1
        rom[2] = 31'h0020f433;  // And x8 x1 x2 => 0
    end

    assign data = rom[addr[31:2]];
endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/79.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

&nbsp;

- **rom[addr[31:2]] → 히위 2비트 무시하는 이유**   
  - **주소 크기**   
    - `addr`은 32bit 주소 입력    
    - `rom`은 32bit(= 4byte) 크기의 요소 64개로 구성   
  - **4byte 단위 접근**   
    - ROM 배열의 각 요소는 32bit, 즉 4byte로 구성됨   
    - 일반적으로 메모리는 byte 단위로 주소를 할당함   
      - 그러나 이 경우에는 각 명령어가 4byte이기 때문에 주소를 4byte 단위로 접근해야 함   
  - **Address bit shift**   
    - `addr[31:2]`는 주소의 상위 30bit를 사용한다는 의미    
    - 이는 주소를 4로 나누는 것과 같음   
      - 예를 들어, 주소가 `32'h000000004`이면, 이는 ROM 배열의 두 번째 요소를 가리킴   
      - `32'h00000004`를 4로 나누면 `32'h00000001`이므로 따라서 `rom[1]`이 선택됨   

&nbsp;

- **예시**   
  - 주소가 `32'h00000000`인 경우   
    - `addr[31:2]` == `32'h0000 0000 ~ 32'h0000 0003`    
    - 결과 : `rom[0]` 선택   
  - 주소가 `32'h00000004`인 경우   
    - `addr[31:2]` == `32'h0000 0004 ~ 32'h0000 0007`     
    - 결과 : `rom[1]` 선택   
  - 주소가 `32'h00000008`인 경우   
    - `addr[31:2]` == `32'h0000 0008 ~ 32'h0000 000B`   
    - 결과 : `rom[2]` 선택   

&nbsp;

### 6-2. Combined Core & Instruction Memory for RV32I    

```verilog
module RV32I (
    input logic clk,
    input logic reset
);

    logic [31:0] w_InstrMemAddr, w_InstrMemData;

    CPU_Core U_CPU_Core (
        .clk(clk),
        .reset(reset),
        .machineCode(w_InstrMemData),

        .instrMemRAddr(w_InstrMemAddr)
    );

    InstructionMemory U_ROM ( 
        .addr(w_InstrMemAddr),

        .data(w_InstrMemData)
    );

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/80.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/82.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

&nbsp;

## 7. Test Bench   

```verilog
module tb_RV32I ();

    logic clk;
    logic reset;

    RV32I dut (
        .clk  (clk),
        .reset(reset)
    );

    always #5 clk = ~clk;

    initial begin
        clk   = 0;
        reset = 1'b1;
        #40 reset = 1'b0;
    end
endmodule
```

<div align="left">
    <strong>Simulation : Adder Operand</strong>
  <img src="/assets/images/verilog/85.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice}    


- **결과 분석**   
  - Machine Code `31'h005201b3` == `add x6 x4 x5` == `9` 출력 확인   
  - Program Counter에서 Address `32'b0`를 전송    
    - Instruction Memory에서 해당 주소에 저장된 값(add x6 x4 x5 machine code)를 CPU_Core로 전송하여 프로세스를 진행한 것을 알 수 있음   

&nbsp;

