---
layout : single
title: "[System Verilog] Block RAM (SRAM)"
categories: 
  - Verilog HDL (Vivado)
toc: true
toc_sticky: true
use_math: true
---

Block RAM (SRAM) 설계    

## 0. About Block RAM   

&nbsp;

<div align="left">
  <img src="/assets/images/verilog/56.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **Block RAM 동작**   
  - RW = 0일때, write mode로 동작하며 입력한 write data가 입력한 address에 저장됨    
  - RW = 1일때, read mode로 동작하며 입력한 address에 저장된 값이 read data로 출력됨   

&nbsp;

## 1. RAM Module   

```verilog
module ram (
    input       clk,
    input [9:0] address,
    input [7:0] wdata,
    input       wr_en,

    output [7:0] rdata
);

    reg [7:0] mem[0:2**10-1];  // n space with 8bit memory   

    integer i;

    initial begin   // Initialize memory to zero
        for (i = 0; i < 2 ** 10 - 1; i = i + 1) begin
            mem[i] = 0;
        end
    end

    always @(posedge clk) begin // Write data on the positive edge of the clock
        if (!wr_en) begin       // If write enable is low, do nothing
            mem[address] <= wdata;  // Write data to memory at the specified address
        end
    end

    // Read data from memory at the specified address
    assign rdata = mem[address];    

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/57.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

&nbsp;

## 2. Verification      

```
interface ram_interface;
    logic       clk;
    logic       wr_en;
    logic [9:0] addr;
    logic [7:0] wdata;

    logic [7:0] rdata;
endinterface


class transaction;          
    rand bit       wr_en;   // bit : 2state type
    rand bit [9:0] addr;
    rand bit [7:0] wdata;
    bit      [7:0] rdata;

    task display(string name);
        $display("[%s] wr_en: %x, addr: %x, wdata: %x, rdata: %x", name, wr_en,
                 addr, wdata, rdata);
    endtask

    // Constaints -> able to set randomization rules regardless with name
    constraint c_addr {addr inside {[10:19]};}   // addr: 10 ~ 19
    constraint c_wdata1 {wdata < 100;}           // wdata: 0 ~ 99
    constraint c_wdata2 {wdata > 10;}            // wdata: 11 ~ 255

    constraint c_wr_en {wr_en dist {0:/80, 1:/20};} // wr_en: 0 ~ 80%, 1 ~ 20%
endclass


class generator;
    transaction            trans;

    // mailbox: communication channel between generator and driver
    mailbox #(transaction) gen2drv_mbox;

    // event: used to synchronize between generator and scoreboard
    event                  gen_next_event;

    function new(mailbox#(transaction) gen2drv_mbox, event gen_next_event);
        this.gen2drv_mbox   = gen2drv_mbox;
        this.gen_next_event = gen_next_event;

    endfunction

    task run(int count);
        repeat(count) begin     /*Rransaction class instance data is automatically
                                  organized by garbage collection*/
                                // So, Memory Leak is not a problem 
            trans = new();
            assert (trans.randomize())
            else $error("[GEN] trans.radomize() error!");
            gen2drv_mbox.put(trans);
            trans.display("GEN");
            @(gen_next_event);  // Wait for scoreboard to finish processing
        end
    endtask
endclass


class driver;
    transaction trans;

    // mailbox: communication channel between generator and driver
    mailbox #(transaction) gen2drv_mbox;

    // virtual interface: used to access the RAM interface
    virtual ram_interface ram_intf;

    function new(virtual ram_interface ram_intf,
                 mailbox#(transaction) gen2drv_mbox);
        this.ram_intf = ram_intf;
        this.gen2drv_mbox = gen2drv_mbox;

    endfunction

    task reset ();      // Reset the RAM interface
        ram_intf.wr_en <= 0;
        ram_intf.addr  <= 0;
        ram_intf.wdata <= 0;
        repeat (5) @(posedge ram_intf.clk);
    endtask

    task run();
        forever begin
            gen2drv_mbox.get(trans);        // Get transaction from generator
            ram_intf.wr_en <= trans.wr_en;  // Set write enable signal
            ram_intf.addr  <= trans.addr;   // Set address signal
            ram_intf.wdata <= trans.wdata;  // Set write data signal

            trans.display("DRV");
            @(posedge ram_intf.clk);
        end
    endtask
endclass


class monitor;
    transaction trans;

    // mailbox: communication channel between monitor and scoreboard
    mailbox #(transaction) mon2scb_mbox;

    // virtual interface: used to access the RAM interface
    virtual ram_interface ram_intf;

    function new(virtual ram_interface ram_intf,
                 mailbox#(transaction) mon2scb_mbox);
        this.ram_intf = ram_intf;
        this.mon2scb_mbox = mon2scb_mbox;
    endfunction

    task run();
        forever begin
            trans = new();
            @(posedge ram_intf.clk);
            trans.wr_en = ram_intf.wr_en;   // Capture write enable signal
            trans.addr  = ram_intf.addr;    // Capture address signal
            trans.wdata = ram_intf.wdata;   // Capture write data signal
            trans.rdata = ram_intf.rdata;   // Capture read data signal

            mon2scb_mbox.put(trans);   // Put transaction into mailbox for scoreboard
            trans.display("MON");
        end
    endtask
endclass


class scoreboard;
    transaction trans;

    // mailbox: communication channel between monitor and scoreboard
    mailbox #(transaction) mon2scb_mbox;

    // event: used to synchronize between scoreboard and generator
    event gen_next_event;

    int total_cnt, pass_cnt, fail_cnt, write_cnt;
    logic [7:0] mem[0:2**10-1];     // Memory array to store data

    function new(mailbox#(transaction) mon2scb_mbox, event gen_next_event);
        this.mon2scb_mbox = mon2scb_mbox;
        this.gen_next_event = gen_next_event;
        total_cnt = 0;
        pass_cnt = 0;
        fail_cnt = 0;
        write_cnt = 0;

        for (int i = 0; i < 2 ** 10 - 1; i++) begin
            mem[i] = 0;
        end
    endfunction

    task run();
        forever begin
            mon2scb_mbox.get(trans);    // Get transaction from monitor
            trans.display("SCB");
            if (trans.wr_en) begin 
                if (mem[trans.addr] == trans.rdata) begin
                    $display(" --> READ PASS! mem[%x] == %x", mem[trans.addr],
                             trans.rdata);
                    pass_cnt++;
                end else begin
                    $display(" --> READ FAIL! mem[%x] == %x", mem[trans.addr],
                             trans.rdata);
                    fail_cnt++;
                end
            // There is no stored data in Initial Logic mem becaus of its intialization
            // It's the same with reg mem in DUT 
            /* Before writing, they are compared on a condition of 0 == 0
               After writing, the output & storage values are properly visible*/
            
            end else begin  // write operation
                mem[trans.addr] = trans.wdata; // Store data in memory
                $display(" --> WRITE! mem[%x] == %x", trans.addr, trans.wdata);
                write_cnt++; // Increment write count
            end

            total_cnt++;
            ->gen_next_event; // Signal that scoreboard has finished processing
        end
    endtask
endclass


class environment;
    generator              gen;
    driver                 drv;
    monitor                mon;
    scoreboard             scb;

    event                  gen_next_event;

    mailbox #(transaction) gen2drv_mbox;
    mailbox #(transaction) mon2scb_mbox;


    function new(virtual ram_interface ram_intf);
        gen2drv_mbox = new();
        mon2scb_mbox = new();

        gen = new(gen2drv_mbox, gen_next_event);
        drv = new(ram_intf, gen2drv_mbox);
        mon = new(ram_intf, mon2scb_mbox);
        scb = new(mon2scb_mbox, gen_next_event);
    endfunction

    task pre_run();
        drv.reset();
    endtask

    task report();
        $display("================================");
        $display("       Final Report       ");
        $display("================================");
        $display("Total Test      : %d", scb.total_cnt);
        $display("READ Pass Count : %d", scb.pass_cnt);
        $display("READ Fail Count : %d", scb.fail_cnt);
        $display("WRITE Count     : %d", scb.write_cnt);
        $display("================================");
        $display("  test bench is finished! ");
        $display("================================");
    endtask


    task run();

        fork
            gen.run(10000);
            drv.run();
            mon.run();
            scb.run();
        join_any

        report();
        #10 $finish;
    endtask

    task run_test();
        pre_run();
        run();
    endtask
endclass


module tb_ram ();
    environment env;
    ram_interface ram_intf ();

    ram dut (
        .clk(ram_intf.clk),
        .address(ram_intf.addr),
        .wdata(ram_intf.wdata),
        .wr_en(ram_intf.wr_en), // Write enable signal

        .rdata(ram_intf.rdata)
    );

    always #5 ram_intf.clk = ~ram_intf.clk;

    initial begin
        ram_intf.clk = 0;
    end

    initial begin
        env = new(ram_intf);
        env.run_test();
    end

endmodule
```

**검증 결과**      
run 200000 ns  
[GEN] wr_en: 0, addr: 011, wdata: 0f, rdata: 00  
[DRV] wr_en: 0, addr: 011, wdata: 0f, rdata: 00  
[MON] wr_en: 0, addr: 011, wdata: 0f, rdata: 00  
[SCB] wr_en: 0, addr: 011, wdata: 0f, rdata: 00  
--> WRITE! mem[011] == 0f  
...  
[GEN] wr_en: 0, addr: 012, wdata: 1a, rdata: 00  
[DRV] wr_en: 0, addr: 012, wdata: 1a, rdata: 00  
[MON] wr_en: 0, addr: 012, wdata: 1a, rdata: 0f  
[SCB] wr_en: 0, addr: 012, wdata: 1a, rdata: 0f  
--> WRITE! mem[012] == 1a  
================================  
       Final Report         
================================  
Total Test      :       10000  
READ Pass Count :        1993  
READ Fail Count :           0  
WRITE Count     :        8007  
================================  
  test bench is finished!   
================================  
{: .notice}


<div align="left">
    <strong>Simulation</strong>
  <img src="/assets/images/verilog/58.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **데이터 처리 과정**   
  - Generator가 transaction 생성 → mailbox를 경유하여 Driver에 전달   
  - Driver는 전달받은 transaction을 DUT 인터페이스에 할당 
  - DUT는 `wr_en`의 0/1 유무에 따라 write/read 동작을 수행   
  - Monitor는 DUT의 입출력을 캡쳐, mailbox를 통해 Scoreboard로 전송    
  - Scoreboard는 결과 데이터를 바탕으로 검증/통계 집계 → Generator에 transaction 재생성 event 트리거    

&nbsp;

- **제약 조건**
  - SystemVerilog의 `randomize()` 메서드는 변수에 랜덤 값을 할당하지만, 특별한 제약조건이 없으면 모든 범위의 값을 뽑음    
  - RAM의 경우, 전체 address의 범위를 전부 검증하기 힘들기 때문에 제약 조건을 활용하여 특정 test 시나리오를 구현함   

&nbsp;

- **로그 분석**   
  - generator와 driver에서는 `rdata`를 따로 설정 안했기 때문에 0이 출력됨   
  - monitor와 scoreboard에서는 DUT의 출력이 나옴   

&nbsp;

