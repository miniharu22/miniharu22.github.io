---
layout : single
title: "[System Verilog] FIFO"
categories: 
  - Verilog HDL (Vivado)
toc: true
toc_sticky: true
use_math: true
---

FIFO(First In First Out) 모듈을 설계    

## 0. FIFO(First In First Out)    

&nbsp;

<div align="left">
  <img src="/assets/images/verilog/9.jpg" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **FIFO(First In First Out)**   
  - 선입선출(Cricular Queue)   
  - push & pop할 때 buffer의 어디를 가리킬지 **포인터**가 필요   
  - 메모리의 read/write 속도에 차이가 있을 때 주로 FIFO를 사용하여 최적화를 수행   

&nbsp;

- **Push algorithm**   
  - **초기 상태**  
    - `wr_ptr == rd_ptr`, `empty = 1`, `full = 0`    
      - memory에 data가 존재하지 않음     
  - **'A' Push**   
    - `mem[wr_ptr] = 'A'`     
    - `wr_ptr++`   
    - `wr_ptr != rd_ptr`, `empty = 0`, `full = 0`       
      - memory에 data가 저장되어 있음    
  - **B,C,D Push**   
    - `wr_ptr == rd_ptr`, `empty = 0`, `full = 1`    
      - Push할 때 `wr_ptr == rd_ptr`이면 `full = 1` 상태   
  - **주의사항** : push할 때 memory가 full이면 안됨    

&nbsp;

- **Pop algorithm**   
  - **Pop 1번**   
    - `rdata = mem[rd_ptr]`   
    - `rd_ptr++`    
    - `wr_ptr != rd_ptr`, `empty = 0`, `full = 0`
      - memory에 data가 저장되어 있음   
    - **Pop 3번**   
      - `wr_ptr == rd_ptr`, `empty = 1`, `full = 0`    
        - Pop할 때 `wr_ptr == rd_ptr`이면 `empty = 1` 상태   
    - **주의사항** : pop할 때 memory가 empty이면 안됨   

&nbsp;

## 1. Design Source    
### 1-1. Register Module   

```verilog
module register_file #(
    parameter ADDR_WIDTH = 3,
    DATA_WIDTH = 8  // 3bit address == 2^3 memory locations, 8bit data width   
) (
    input                  clk,
    input                  reset,
    input                  wr_en,
    input [ADDR_WIDTH-1:0] waddr,   // write address
    input [DATA_WIDTH-1:0] wdata,   // write data
    input [ADDR_WIDTH-1:0] raddr,   // read address

    output [DATA_WIDTH-1:0] rdata   // read data
);

    // Memory array
    reg [DATA_WIDTH-1:0] mem[0:2**ADDR_WIDTH];

    // Write operation
    always @(posedge clk) begin         // On clock edge
        if (wr_en) mem[waddr] <= wdata; // Write data to memory at waddr
    end

    // Read operation
    assign rdata = mem[raddr];  // Read data from memory at raddr

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/59.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **Register**   
  - 데이터를 저장하는 스토리지   
  - 데이터의 크기(Data Width)만큼의 저장공간이 2^(주소 크기)개만큼 존재    
  - write enable 신호를 받으면 write address 주소에 데이터를 저장    
  - read address 주소에 저장된 데이터 반환    

&nbsp;

### 1-2. FIFO Control Module   

```verilog
module fifo_control_unit #(
    parameter ADDR_WIDTH = 3    // 3bit address == 2^3 memory locations
) (
    input clk,
    input reset,

    // wrte
    input                   wr_en,  // write enable
    output                  full,   // full flag
    output [ADDR_WIDTH-1:0] waddr,  // write address

    // read
    input rd_en,                    // read enable
    output empty,                   // empty flag    
    output [ADDR_WIDTH-1:0] raddr   // read address
);

    reg [ADDR_WIDTH-1:0] wr_ptr_reg, wr_ptr_next;   // Write pointer
    reg [ADDR_WIDTH-1:0] rd_ptr_reg, rd_ptr_next;   // Read pointer
    reg full_reg, full_next, empty_reg, empty_next; // Full and empty flags


    assign waddr = wr_ptr_reg;
    assign raddr = rd_ptr_reg;
    assign full  = full_reg;
    assign empty = empty_reg;

    // Sequential logic for pointers and status flags
    always @(posedge clk, posedge reset) begin  
        if (reset) begin
            wr_ptr_reg <= 0;
            rd_ptr_reg <= 0;
            full_reg   <= 1'b0;
            empty_reg  <= 1'b0;
        end else begin                  // On clock edge
            wr_ptr_reg <= wr_ptr_next;  // Update write pointer
            rd_ptr_reg <= rd_ptr_next;  // Update read pointer
            full_reg   <= full_next;    // Update full flag
            empty_reg  <= empty_next;   // Update empty flag
        end
    end

    // Combinational logic for next pointer and status updates
    always @(*) begin
        // Default: keep current state
        wr_ptr_next = wr_ptr_reg;   
        rd_ptr_next = rd_ptr_reg;   
        full_next   = full_reg;
        empty_next  = empty_reg;

        case ({
            wr_en, rd_en
        })
            2'b01: begin  // Read operation
                if (!empty_reg) begin   // If not empty, read data
                    full_next   = 1'b0; // Clear full flag
                    rd_ptr_next = rd_ptr_reg + 1;   // Increment read pointer
                    if (rd_ptr_next == wr_ptr_reg) begin  // If read pointer meets write pointer
                        empty_next = 1'b1;  // Set empty flag
                    end
                end
            end

            2'b10: begin  // Write operation
                if (!full_reg) begin    // If not full, write data
                    empty_next  = 1'b0; // Clear empty flag
                    wr_ptr_next = wr_ptr_reg + 1;   // Increment write pointer
                    if (wr_ptr_next == rd_ptr_reg) begin  // If write pointer meets read pointer   
                        full_next = 1'b1;   // Set full flag
                    end
                end
            end

            2'b11: begin  // Simultaneous read and write
                if (empty_reg) begin            // If empty, cannot read
                    wr_ptr_next = wr_ptr_reg;   // Keep write pointer unchanged
                    rd_ptr_next = rd_ptr_reg;   // Keep read pointer unchanged
                end else begin                      // If not empty, perform read and write
                    wr_ptr_next = wr_ptr_reg + 1;   // Increment write pointer
                    rd_ptr_next = rd_ptr_reg + 1;   // Increment read pointer
                end
            end
        endcase
    end
endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/60.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **FIFO Control**   
  - 어떤 주소에 저장되어 있는 데이터를 읽고 쓸 것인지를 제어하는 유닛   
  - `read`   
    - 레지스터가 비어있지 않으면 read address 포인터 1씩 증가    
    - read address == write address가 되면 Empty, 더이상 데이터를 읽을 수 없음    
  - `write`  
    - 레지스터가 가득 차 있지 않으면 write address 포인터 1씩 증가   
    - read address == write address가 되면 Full, 더이상 데이터를 쓸 수 없음    
  - Control unit에서 주소에 대한 포인터를 반환하면 Register에서 해당 주소의 데이터를 read/write 수행    

&nbsp;

### 1-3. FIFO Module   

```verilog
module fifo #(
    parameter ADDR_WIDTH = 3,
    DATA_WIDTH = 8
) (
    input clk,
    input reset,

    input                   wr_en,  // write enable
    output                  full,   // full flag
    input  [DATA_WIDTH-1:0] wdata,  // write data

    input                   rd_en,  // read enable
    output                  empty,  // empty flag
    output [DATA_WIDTH-1:0] rdata   // read data
);

    wire w_full;
    wire [ADDR_WIDTH-1:0] w_waddr, w_raddr;

    // Register file module to store data
    register_file #(
        .ADDR_WIDTH(ADDR_WIDTH),
        .DATA_WIDTH (DATA_WIDTH)  
    ) U_RegFile (
        .clk  (clk),
        .reset(reset),
        .wr_en(wr_en & ~full), // Write enable is active when not full
        .waddr(w_waddr),    // Write address
        .wdata(wdata),      // Write data
        .raddr(w_raddr),    // Read address

        .rdata(rdata)   // Read data
    );

    // FIFO control unit to manage pointers and status
    fifo_control_unit #(
        .ADDR_WIDTH(ADDR_WIDTH)
    ) U_FIFO_CU (
        .clk  (clk),
        .reset(reset),

        // wrte
        .wr_en(wr_en),
        .full (full),
        .waddr(w_waddr),

        // read
        .rd_en(rd_en),
        .empty(empty),
        .raddr(w_raddr)
    );

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/61.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

&nbsp;


