---
layout : single
title: "[System Verilog] FIFO Testbench Verifiaction"
categories: 
  - Verilog HDL (Vivado)
toc: true
toc_sticky: true
use_math: true
---

FIFO Testbench Verification을 구축하여 SW의 FIFO와 HW의 FIFO를 비교하여 DUT의 무결성을 검증   

## 0. Verification Testbench Flow   

&nbsp;

<div align="left">
  <img src="/assets/images/verilog/10.jpg" width="80%" height="80%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;


## 1. interface.sv   

```verilog
`ifndef __INTERFACE_SV_ 
`define __INTERFACE_SV_

`timescale 1ns / 1ps

interface fifo_interface;   // FIFO interface definition
    logic clk;              
    logic reset;

    logic wr_en;        // Write enable signal
    logic full;         // FIFO full signal
    logic [7:0] wdata;  // Write data signal
    
    logic rd_en;        // Read enable signal
    logic empty;        // FIFO empty signal
    logic [7:0] rdata;  // Read data signal
endinterface

`endif
```

&nbsp;

## 2. transaction.sv   

```verilog
`ifndef __TRANSACTION_SV_
`define __TRANSACTION_SV_

`timescale 1ns / 1ps

class transaction;
    rand bit       wr_en;
    bit            full;
    rand bit [7:0] wdata;

    rand bit       rd_en;
    bit            empty;
    logic      [7:0] rdata;

    // Ensure that only one operation (write or read) is active at a time
    constraint c_oper {wr_en != rd_en;} 
    constraint c_wr_en {    // Write enable must be 1 when writing
        wr_en dist {    // 50% chance of being 1, 50% chance of being 0
            1 :/ 50,
            0 :/ 50
        };
    }

    task display(string name);
        $display(
            "[%s] wr_en: %x, wdata: %x, full: %x  ||  rd_en: %x, rdata: %x, empty: %x",
            name, wr_en, wdata, full, rd_en, rdata, empty);
    endtask
endclass


`endif
```

&nbsp;

## 3. generator.sv   

```verilog
`timescale 1ns / 1ps

`include "transaction.sv"   // Include the transaction class

class generator;
    transaction trans;

    // Mailbox to send transactions to the driver
    mailbox #(transaction) gen2drv_mbox;

    // Event to signal the next transaction generation
    event gen_next_event;

    function new(mailbox#(transaction) gen2drv_mbox, event gen_next_event);
        this.gen2drv_mbox = gen2drv_mbox;
        this.gen_next_event = gen_next_event;
    endfunction

    task run(int count);
        repeat(count) begin
            trans = new();

            assert(trans.randomize())
            else $error("[GEN] trans.randomize() error!");

            gen2drv_mbox.put(trans); // Send the transaction to the driver
            trans.display("GEN");   
            @(gen_next_event);       // Wait for the next generation event
        end
    endtask
endclass
```

&nbsp;

## 4. driver.sv   

```verilog
`timescale 1ns / 1ps

`include "transaction.sv"   // Include the transaction class
`include "interface.sv"     // Include the FIFO interface definition

class driver;
    transaction trans;

    // Mailbox to receive transactions from the generator
    mailbox #(transaction) gen2drv_mbox;

    // Virtual interface to the FIFO interface
    virtual fifo_interface fifo_intf;

    function new(mailbox#(transaction) gen2drv_mbox,
                 virtual fifo_interface fifo_intf);
        this.fifo_intf = fifo_intf;
        this.gen2drv_mbox = gen2drv_mbox;
    endfunction

    task reset();
        fifo_intf.wr_en <= 1'b0;
        fifo_intf.wdata <= 0;
        fifo_intf.rd_en <= 1'b0;
        fifo_intf.reset <= 1'b1;
        repeat (5) @(posedge fifo_intf.clk);
        fifo_intf.reset <= 1'b0;
    endtask

    task run();
        forever begin
            gen2drv_mbox.get(trans);        // Receive a transaction from the generator
            fifo_intf.wr_en = trans.wr_en;  // Set write enable signal
            fifo_intf.wdata = trans.wdata;  // Set write data signal
            fifo_intf.rd_en = trans.rd_en;  // Set read enable signal

            trans.display("DRV");
            @(posedge fifo_intf.clk);       // Wait for the next clock cycle
        end
    endtask
endclass
```

&nbsp;

## 5. monitor.sv   

```verilog
`timescale 1ns / 1ps

`include "transaction.sv"   // Include the transaction class
`include "interface.sv"     // Include the FIFO interface definition

class monitor;
    transaction trans;

    // Mailbox to send transactions to the scoreboard
    mailbox #(transaction) mon2scb_mbox;

    // Virtual interface to the FIFO interface
    virtual fifo_interface fifo_intf;

    function new(mailbox#(transaction) mon2scb_mbox,
                 virtual fifo_interface fifo_intf);
        this.mon2scb_mbox = mon2scb_mbox;
        this.fifo_intf = fifo_intf;
    endfunction

    task run();
        forever begin
            trans = new(); // Create a new transaction object
            #1; // Delay to ensure the transaction is ready
            trans.wr_en = fifo_intf.wr_en;  // Get the write enable signal
            trans.rd_en = fifo_intf.rd_en;  // Get the read enable signal
            trans.wdata = fifo_intf.wdata;  // Get the write data signal
            trans.rdata = fifo_intf.rdata;  // Get the read data signal

            @(posedge fifo_intf.clk);       // Wait for the next clock cycle
            trans.full  = fifo_intf.full;   // Get the FIFO full signal
            trans.empty = fifo_intf.empty;  // Get the FIFO empty signal

            mon2scb_mbox.put(trans);    // Send the transaction to the scoreboard
            trans.display("MON");
        end
    endtask
endclass
```

&nbsp;

## 6. scoreboard.sv   

```verilog 
`timescale 1ns / 1ps

`include "transaction.sv"   // Include the transaction class

class scoreboard;
    transaction trans;

    // Mailbox to receive transactions from the monitor
    mailbox #(transaction) mon2scb_mbox;

    // Event to signal the next transaction generation
    event gen_next_event;

    int total_cnt, pass_cnt, fail_cnt, write_cnt, full_cnt, empty_cnt;
    reg [7:0] scb_fifo[$];      // FIFO to store data for comparison
    reg [7:0] scb_fifo_data;    // Variable to hold data read from the FIFO
    int max = 8;                // Maximum size of the FIFO

    function new(mailbox#(transaction) mon2scb_mbox, event gen_next_event);
        this.mon2scb_mbox   = mon2scb_mbox;
        this.gen_next_event = gen_next_event;

        total_cnt           = 0;
        pass_cnt            = 0;
        fail_cnt            = 0;
        write_cnt           = 0;
        full_cnt = 0;
        empty_cnt = 0;
    endfunction

    task run();
        forever begin
            mon2scb_mbox.get(trans);    // Receive a transaction from the monitor
            trans.display("SCB");

            if (trans.wr_en) begin  // If write enable is set
                if (scb_fifo.size() < max) begin        // Check if FIFO is not full
                    scb_fifo.push_back(trans.wdata);    // Write data to the FIFO
                    $display(" ---> WRITE! fifo_data %x, queue size: %x, %p\n",
                             trans.wdata, scb_fifo.size(), scb_fifo);
                    write_cnt++;    // Increment write count
                end else begin      // If FIFO is full, display an error message
                    $display(" ---> FIFO FULL! Cannot write data. fifo_data %x, queue size: %x, %p\n",
                             trans.wdata, scb_fifo.size(), scb_fifo);
                             full_cnt++;
                end

            end else if (trans.rd_en) begin // If read enable is set
                if (scb_fifo.size() > 0) begin  // Check if FIFO is not empty
                    scb_fifo_data = scb_fifo.pop_front();  // Read data from the FIFO

                    if (scb_fifo_data == trans.rdata) begin // Compare FIFO data with read data
                        $display(" ---> PASS! fifo_data %x == rdata %x, queue size: %x, %p\n",
                                 scb_fifo_data, trans.rdata, scb_fifo.size(), scb_fifo);
                        pass_cnt++; // Increment pass count
                    end else begin 
                        $display(" ---> FAIL! fifo_data %x != rdata %x, queue size: %x, %p\n",
                                 scb_fifo_data, trans.rdata, scb_fifo.size(), scb_fifo);
                        fail_cnt++;
                    end
                end else begin  //
                    $display(" ---> FIFO EMPTY! Cannot read data.");
                    empty_cnt++;
                end

            end
            total_cnt++;
            ->gen_next_event; // Signal the next transaction generation
        end
    endtask
endclass
```

&nbsp;

## 7. environment.sv   

```verilog
`timescale 1ns / 1ps

`include "transaction.sv"   // Include the transaction class
`include "generator.sv"     // Include the generator class
`include "driver.sv"        // Include the driver class
`include "monitor.sv"       // Include the monitor class
`include "scoreboard.sv"    // Include the scoreboard class
`include "interface.sv"     // Include the FIFO interface definition


  class environment;  
    generator              gen;
    driver                 drv;
    monitor                mon;
    scoreboard             scb;

    mailbox #(transaction) gen2drv_mbox;    // Mailbox to send transactions from generator to driver
    mailbox #(transaction) mon2scb_mbox;    // Mailbox to send transactions from monitor to scoreboard

    event                  gen_next_event;  // Event to signal the next transaction generation

    function new(virtual fifo_interface fifo_intf);
        gen2drv_mbox = new();
        mon2scb_mbox = new();

        gen = new(gen2drv_mbox, gen_next_event);
        drv = new(gen2drv_mbox, fifo_intf);
        mon = new(mon2scb_mbox, fifo_intf);
        scb = new(mon2scb_mbox, gen_next_event);
    endfunction


    task report();
        $display("=============================");
        $display("==       Final Report      ==");
        $display("=============================");
        $display("Total Test : %d", scb.total_cnt);
        $display("Pass Count : %d", scb.pass_cnt);
        $display("Fail Count : %d", scb.fail_cnt);
        $display("Write Count : %d", scb.write_cnt);
        $display("FULL Count : %d", scb.full_cnt);
        $display("EMPTY Count : %d", scb.empty_cnt);
        $display("=============================");
        $display("== test bench is finished! ==");
        $display("=============================");
    endtask


    task pre_run();
        drv.reset();
    endtask

    task run(int count);
        fork
            gen.run(count);
            drv.run();
            mon.run();
            scb.run();
        join_any

        report();
        #10 $finish;
    endtask

    task run_test(int count);
        pre_run();
        run(count);
    endtask
endclass
```

&nbsp;

## 8. tb_fifo.sv    

```verilog
`timescale 1ns / 1ps

`include "environment.sv"   // Include the environment class

module tb_fifo ();
    fifo_interface fifo_intf (); // Declare the FIFO interface
    environment env;             // Declare the environment


    fifo #(
        .ADDR_WIDTH(3), // Address width for the FIFO
        .DATA_WIDTH(8)  // Data width for the FIFO
    ) dut (
        .clk  (fifo_intf.clk),
        .reset(fifo_intf.reset),

        .wr_en(fifo_intf.wr_en),
        .full (fifo_intf.full),
        .wdata(fifo_intf.wdata),

        .rd_en(fifo_intf.rd_en),
        .empty(fifo_intf.empty),
        .rdata(fifo_intf.rdata)
    );

    always #5 fifo_intf.clk = ~fifo_intf.clk;

    initial begin
        fifo_intf.clk = 0;
    end

    initial begin
        env = new(fifo_intf);
        env.run_test(1000);
    end
endmodule
```

**검증 결과**   
run 200000 ns   
[GEN] wr_en: 1, wdata: d5, full: 0  ||  rd_en: 0, rdata: xx, empty: 0  
[DRV] wr_en: 1, wdata: d5, full: 0  ||  rd_en: 0, rdata: xx, empty: 0  
[MON] wr_en: 1, wdata: d5, full: 0  ||  rd_en: 0, rdata: xx, empty: 0  
[SCB] wr_en: 1, wdata: d5, full: 0  ||  rd_en: 0, rdata: xx, empty: 0  
 ---> WRITE! fifo_data d5, queue size: 00000001, '{213}    
[GEN] wr_en: 0, wdata: 9e, full: 0  ||  rd_en: 1, rdata: xx, empty: 0  
[DRV] wr_en: 0, wdata: 9e, full: 0  ||  rd_en: 1, rdata: xx, empty: 0  
[MON] wr_en: 0, wdata: 9e, full: 0  ||  rd_en: 1, rdata: d5, empty: 0  
[SCB] wr_en: 0, wdata: 9e, full: 0  ||  rd_en: 1, rdata: d5, empty: 0  
 ---> PASS! fifo_data d5 == rdata d5, queue size: 00000000, '{}  
[GEN] wr_en: 0, wdata: e3, full: 0  ||  rd_en: 1, rdata: xx, empty: 0  
[DRV] wr_en: 0, wdata: e3, full: 0  ||  rd_en: 1, rdata: xx, empty: 0  
[MON] wr_en: 0, wdata: e3, full: 0  ||  rd_en: 1, rdata: xx, empty: 1  
[SCB] wr_en: 0, wdata: e3, full: 0  ||  rd_en: 1, rdata: xx, empty: 1  
 ---> FIFO EMPTY! Cannot read data.  
...  
[GEN] wr_en: 1, wdata: 7b, full: 0  ||  rd_en: 0, rdata: xx, empty: 0  
[DRV] wr_en: 1, wdata: 7b, full: 0  ||  rd_en: 0, rdata: xx, empty: 0  
[MON] wr_en: 1, wdata: 7b, full: 0  ||  rd_en: 0, rdata: 4c, empty: 0  
[SCB] wr_en: 1, wdata: 7b, full: 0  ||  rd_en: 0, rdata: 4c, empty: 0  
 ---> WRITE! fifo_data 7b, queue size: 00000007, '{76,114,251,113,47,190,123}  
=============================  
==       Final Report      ==  
=============================  
Total Test :        1000  
Pass Count :         449  
Fail Count :           0  
Write Count :         456  
FULL Count :          34  
EMPTY Count :          61  
=============================  
== test bench is finished! ==  
=============================  
{: .notice}

<div align="left">
    <strong>Simulation</strong>
  <img src="/assets/images/verilog/62.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

&nbsp;
