---
layout : single
title: "[System Verilog] Adder Testbench #1"
categories: 
  - Verilog HDL (Vivado)
toc: true
toc_sticky: true
use_math: true
---

System Verilog를 이용하여 Adder(가산기) testbench 구현    

## 0. Adder Testbench    

&nbsp;

<div align="center">
  <img src="/assets/images/verilog/6.jpg" width="60%" height="60%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- Valid signal이 들어가면 다음 클럭에서 결과 값 출력      

&nbsp;

## 1. Design Source        

```verilog
module adder (
    input       clk,
    input       reset,
    input       valid,
    input [3:0] a,
    input [3:0] b,

    output [3:0] sum,
    output       carry
);

    reg [3:0] sum_reg, sum_next;
    reg carry_reg, carry_next;


    // output combinational logic
    assign sum   = sum_reg;
    assign carry = carry_reg;


    // state register
    always @(posedge clk, posedge reset) // synchronous reset
    begin  
        if (reset) begin        // reset state
            sum_reg   <= 0;     // reset sum register
            carry_reg <= 1'b0;  // reset carry register
        end else begin                  // next state
            sum_reg   <= sum_next;      // update sum register
            carry_reg <= carry_next;    // update carry register
        end
    end


    // next state combinational logic
    always @(*) begin          
        carry_next = carry_reg; // carry register holds the previous carry value
        sum_next   = sum_reg;   // sum register holds the previous sum value

        if (valid) begin        // if valid signal is high, perform addition
            {carry_next, sum_next} = a + b; // perform addition
        end
    end

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/verilog/47.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 


&nbsp;

## 2. Random Number Generatation Test   

```verilog
interface adder_intf;  // Interface for adder
    logic       clk;
    logic       reset;
    logic       valid;
    logic [3:0] a;
    logic [3:0] b;

    logic [3:0] sum;
    logic       carry;
endinterface 


class transaction;      // Class for transaction with dataset fot test

    rand logic [3:0] a; // Randomized inputs
    rand logic [3:0] b; // Randomized inputs

    task display(string name);
        $display("[%s] a:%d, b:%d", name, a, b);
    endtask
endclass 


class generator;    // Class for generating transactions
    transaction tr; // Transaction object

    function new(); // Constructor
        tr = new(); // Create a new transaction object
    endfunction 

    task run();     // Task to run the generator
        repeat (10) begin   // Generate 10 transactions
            assert (tr.randomize()) // Randomize the transaction
            else $error("tr.randomize() error!");  // Check if randomization was successful
            tr.display("GEN"); // Display the transaction
        end
    endtask  
endclass 


module tb_adder ();

    generator gen;

    initial begin
        gen = new();
        gen.run();      // Run the generator to create transactions
    end

endmodule
```

**실행 결과**     
[GEN] a:13, b:13   
[GEN] a: 3, b: 6   
[GEN] a:10, b: 9   
[GEN] a: 5, b: 8   
[GEN] a: 7, b: 9   
[GEN] a:14, b: 1   
[GEN] a: 2, b: 3   
[GEN] a: 9, b: 9   
[GEN] a:12, b: 7   
[GEN] a:11, b:15   
{: .notice}

&nbsp;

## 3. Generator & Driver    

```verilog
interface adder_intf;   // Interface for adder module
    logic       clk;
    logic       reset;
    logic       valid;
    logic [3:0] a;
    logic [3:0] b;

    logic [3:0] sum;
    logic       carry;
endinterface


class transaction;          // Class to represent a transaction
    rand logic [3:0] a;     // Randomized input values
    rand logic [3:0] b;     // Randomized input values

    task display(string name);
        $display("[%s] a:%d, b:%d", name, a, b);
    endtask

endclass


class generator;    // Class to generate random transactions
    transaction tr; // Instance of transaction class

    // Mailbox to communicate with driver
    // The mailbox allows the generator to send transactions to the driver
    mailbox #(transaction) gen2drv_mbox;    

    function new();  // Constructor for generator class
        tr = new();  // Create a new transaction instance
    endfunction

    task run();     // Task to generate random transactions
        repeat (10) begin   // Generate 10 transactions
            assert (tr.randomize())  // Randomize the transaction
            else $error("tr.randomize() error!"); // Check if randomization was successful

            gen2drv_mbox.put(tr); // Put the transaction into the mailbox   
            tr.display("GEN");  // Display the transaction details
        end
    endtask

endclass


class driver;   // Class to drive the adder interface
    virtual adder_intf adder_if;    // Virtual interface to the adder
                                    // This allows the driver to interact with the adder module
    
    // Mailbox to receive transactions from the generator   
    mailbox #(transaction) gen2drv_mbox;
    transaction tr; // Instance of transaction class to access the transaction data

    function new(virtual adder_intf adder_if);  // Constructor for driver class
        this.adder_if = adder_if;   // Initialize the virtual interface
    endfunction

    task reset();               // Task to reset the adder interface
        adder_if.a     = 0;     // Reset input a
        adder_if.b     = 0;     // Reset input b
        adder_if.valid = 1'b0;  // Reset valid signal
        adder_if.reset = 1'b1;  // Set reset signal high
        repeat (5) @(adder_if.clk); // Wait for 5 clock cycles
        adder_if.reset = 1'b0;  // Set reset signal low
    endtask

    task run();     // Task to run the driver
        forever begin   // Infinite loop to continuously process transactions
            
            gen2drv_mbox.get(tr);   // Get a transaction from the mailbox
            // This blocks until a transaction is available
            // blocking code -> waits for a transaction to be available

            adder_if.a     = tr.a;      // Set input a from the transaction
            adder_if.b     = tr.b;      // Set input b from the transaction
            adder_if.valid = 1'b1;      // Set valid signal high to indicate inputs are ready
            tr.display("DRV");          // Display the transaction details
            @(posedge adder_if.clk);    // Wait for the next clock edge
            adder_if.valid = 1'b0;      // Set valid signal low to indicate inputs are no longer valid
            @(posedge adder_if.clk);    // Wait for the next clock edge to allow the adder to process the inputs
        end
    endtask

endclass


module tb_adder ();

    adder_intf adder_if ();     // Instantiate the adder interface
    generator gen;  // Instance of generator class to generate
    driver drv;     // Instance of driver class to drive the adder interface

    mailbox #(transaction) gen2drv_mbox; 
    // Mailbox to communicate between generator and driver

    adder dut (
        .clk(adder_if.clk),  
        .reset(adder_if.reset),
        .valid(adder_if.valid),
        .a(adder_if.a),
        .b(adder_if.b),

        .sum  (adder_if.sum),
        .carry(adder_if.carry)
    );

    always #5 adder_if.clk = ~adder_if.clk;

    initial begin
        adder_if.clk   = 1'b0;
        adder_if.reset = 1'b1;
    end

    initial begin
        gen2drv_mbox = new();   // Create a new mailbox for communication

        gen = new();         // Create a new generator instance
        drv = new(adder_if); // Create a new driver instance with the adder interface
        
        // Connect the generator's mailbox to the driver's mailbox
        gen.gen2drv_mbox = gen2drv_mbox;
        // Connect the driver's mailbox to the generator's mailbox
        drv.gen2drv_mbox = gen2drv_mbox;    

        drv.reset();    // Reset the adder interface

        fork
            gen.run();  // Start the generator to produce transactions
            drv.run();  // Start the driver to process transactions
        join
        #10 $finish;
    end

endmodule
```

**검증 결과**  
run 2000 ns   
[GEN] a: 8, b: 8  
[GEN] a: 6, b:13  
[GEN] a:10, b:11  
[GEN] a: 4, b: 2  
[GEN] a: 3, b:10  
[GEN] a:15, b:13  
[GEN] a: 5, b: 4  
[GEN] a:15, b: 8  
[GEN] a:14, b:14  
[GEN] a: 5, b: 4  
[DRV] a: 5, b: 4  
[DRV] a: 5, b: 4  
[DRV] a: 5, b: 4  
[DRV] a: 5, b: 4  
[DRV] a: 5, b: 4  
[DRV] a: 5, b: 4  
[DRV] a: 5, b: 4  
[DRV] a: 5, b: 4  
[DRV] a: 5, b: 4  
[DRV] a: 5, b: 4  
{: .notice}

<div align="left">
    <strong>Simulation</strong>
  <img src="/assets/images/verilog/48.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **데이터 처리 과정**   
  - Main에서 `adder_intf adder_if`를 통해 물리적 interface 생성    
  - Driver에서 `this adder_if = adder_if`를 통해 물리 Interface와 가상 Interface를 연결    
  - Generator에서 생성한 transaction은 mailbox에 저장되며, 이를 경유하여 Driver로 전달   
  - 연결된 Interface를 통해 transaction은 실제 signal로 변환되어 adder module에 전달되어 처리됨    

&nbsp;

- **결과 분석**   
  - 이론 상으로는 GEN → DRV → GEN → DRV 순서로 나와야 하는데, random number를 전부 생성한 후에야 Driver가 처리하는 것을 확인할 수 있음   
  - Generator와 Driver의 동작 순서에 조건을 추가해줘야 하는 것으로 판단됨   

&nbsp;


