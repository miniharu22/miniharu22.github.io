---
layout : single
title: "[SVisual] fmax Extraction"  
categories: 
  - RF Characteristics in GAA Transistor
toc: true
toc_sticky: true
use_math: true
---

$$f_{max}$$를 추출하는 Sentaurus Visual 커맨드 작성    

## 0. Load Library

```tcl
#setdep @previous@

load_library extract
load_library rfx
```

- `extract` : 특성 추출 관련 커맨드 라이브러리  
- `rfx` : RF 특성의 연산 관련 커맨드 라이브러리  

&nbsp; 

## 1. Create Two-Port Network Dataset

```tcl

rfx::Load -dataset "ACPLT" -file "RF_@acplot@" -port1 g -port2 d -biasport "v(g)" 

```

- Two-Port Network 구축  
  - Input Port(`port1`) : Gate로 지정  
  - Output Port(`port2`) : Drain으로 지정  

- SDevice로 작성한 RF Simulation File을 이용하여 Two-Port Network Data를 저장하고 있는 `ACPLT` Dataset을 구축  

&nbsp; 

## 2. Fmax & Power Gain(MUG) Extraction

```
lib::SetInfoDef 0

rfx::GetPowerGain -out gain -dataset "Pgain_freq" -powergain "MUG" -xaxis "frequency" -target 10

rfx::GetFmax -out Fmax0 -method "unit-gain-point" -scale 1e9 -xscale "log" \
    -dataset "fmax0_bias" -slopedataset "MUG_slope_freq" 
```

- Power Gain(MUG, Mason Unilateral Gain)의 추출의 경우, frequency 뿐만 아니라 `target`을 통해 특정 Bias Point를 지정해줘야 함   

- `ACPLT` Dataset 구축 후, `rfx::GetFmax` 커맨드를 활용하여 Voltage Sweep에 따른 $$f_{max}$$의 추출을 진행  
- `scale`의 경우, GHz 단위로 치환하기 위함  

&nbsp; 

## 3. Visualization Setting

```tcl
set N     @node@
set i     @node:index@

set COLORS  [list brown purple red orange yellow green blue magenta violet black]
set NCOLORS [llength $COLORS]

set SYMBOL  [list square circle diamond squaref circlef diamondf plus x]
set NSYMBOLS [llength $SYMBOL]
set symbol   [lindex  $SYMBOL [expr $i%$NSYMBOLS]]
```

- Node의 구분에 사용할 변수와 반복문에 사용할 변수를 정의  
- Plot에 사용할 Color와 Symbol을 정의  

&nbsp; 

## 4. Power Gain[dB] vs Frequency Plot  

```python
# Set window style to grid for better visualization
windows_style -style grid

# Define power gain metric to plot
set Pgain "MUG"

# Create plot if it doesn't already exist
if {[lsearch [list_plots] Plot_${Pgain}_dB_frequency] == -1} {

    # Create a new 1D plot
    create_plot -1d -name Plot_${Pgain}_dB_frequency

    # Unlink this plot from others to keep it independent
    link_plots [list_plots] -unlink

    # Create a 0 dB reference line across all frequency points
    set 0dB [list]
    foreach fq $rfx::freq {
        lappend 0dB 0.0
    }

    # Define dataset variables for plotting the 0 dB line
    create_variable -name "0dB" -dataset "PUnityGain" -values $0dB
    create_variable -name "frequency" -dataset "PUnityGain" -values $rfx::freq

    # Create the 0 dB line curve
    create_curve -name 0dBline -dataset PUnityGain -axisX "frequency" -axisY "0dB"

    # Set visual properties of the 0 dB line
    set_curve_prop 0dBline -label "0 dB line" -color black -line_style dash -line_width 3	

    # Set plot properties: title, grid, and legend
    set_plot_prop -title "MUG [dB] Vs Frequency" -title_font_size 20 -show_legend -show_grid

    # Set X-axis properties (log scale for frequency)
    set_axis_prop -axis x -title {Frequency [Hz]} -scale_format scientific -title_font_size 16 -scale_font_size 14 -type log 

    # Set Y-axis properties (linear scale for MUG in dB)
    set_axis_prop -axis y -title "MUG [dB]" -title_font_size 16 -scale_font_size 14 -type linear

    # Set legend properties
    set_legend_prop -label_font_size 12 -location top_right -label_font_att bold	
    }

# Initialize index for bias points
set i 0

# Loop through all bias points to generate MUG curves
foreach BiasPoint $rfx::bias {

    # Pick a color for the current bias point curve
    set color [lindex $COLORS [expr $i % $NCOLORS]]	

    # Generate Power Gain data for the given bias point
    rfx::GetPowerGain -out PowerGain -xaxis frequency -target $BiasPoint -powergain MUG -dataset ${Pgain}_frequency($N,$i)
	
    # Select the plot for adding the new curve
    select_plots Plot_${Pgain}_dB_frequency

    # Create the MUG curve using generated dataset
    create_curve -name ${Pgain}_dB($N,$i) -dataset "${Pgain}_frequency($N,$i)" -axisX "frequency" -axisY "${Pgain}_dB"	

    # Set curve visual properties: label, color, style, width
    set_curve_prop ${Pgain}_dB($N,$i) -label "V<sub>gs</sub>=[format %.2f $BiasPoint]" -color $color -line_style solid -line_width 3	

    # Increment the bias point index
    incr i 		
    }
```

&nbsp; 

## 5. fmax vs Bias Plot  

```python
# Check if the plot named 'Plot_fmaxs' already exists
if {[lsearch [list_plots] Plot_fmaxs] == -1} {

    # Create a new 1D plot for fmax vs Vgs
    create_plot -1d -name Plot_fmaxs

    # Unlink this plot from all others to make it independent
    link_plots [list_plots] -unlink

    # Set overall plot properties: title, legend, and grid display
    set_plot_prop -title "f<sub>max</sub> Vs V<sub>gs</sub>" -title_font_size 20 \
        -show_legend -show_grid

    # Set X-axis properties: Gate voltage (Vgs)
    set_axis_prop -axis x -title {Gate Voltage [V]} -title_font_size 16 \
        -scale_font_size 14 -type linear 

    # Set Y-axis properties: fmax in GHz
    set_axis_prop -axis y -title {f<sub>max</sub> [GHz]} -title_font_size 16 \
        -scale_font_size 14 -type linear 

    # Set legend properties: font size, style, and position
    set_legend_prop -label_font_size 12 -label_font_att bold -location bottom_right	
}

# Select the plot for adding new curve
select_plots Plot_fmaxs

# Create a curve named Fmax0(N) using 'fmax0_bias' dataset
# X-axis: Vgs (bias), Y-axis: fmax
create_curve -name Fmax0($N) -dataset "fmax0_bias" -axisX bias -axisY fmax	

# Set curve visual properties: label, color, line style, marker properties
set_curve_prop Fmax0($N) -label "unit-gain-point" -color red \
    -line_style solid -line_width 3  -show_markers -markers_size 10 \
    -markers_type $symbol
```

&nbsp; 

## 6. Extract Peak value of fmax

```tcl
lib::SetInfoDef 1	

ext::ExtractExtremum -out fmaxPeakdB 	-name "out" -x $Fmax0(bias) -y $Fmax0(fmax) -type "max"	  -f "%.2f"
ext::ExtractValue 	 -out Vg_fmaxPeakdB -name "out" -x $Fmax0(fmax) -y $rfx::bias 	  -xo $fmaxPeakdB -f "%.2f"
```

&nbsp; 