---
layout : single
title: "[Verilog] 3bit Compression Adder Design"
categories: 
  - Universal NPU-CNN Accelarator
toc: true
toc_sticky: true
use_math: true
---

Convolution 연산에서 Adder Stage의 기본적인 가산기(HalfAdder, FullAdder)와 3bit 압축을 위한 기본 Adder module 설계    

## 0. Half Adder   

```verilog
module HA(A, B, S, C); 

input A, B;
output S, C;

assign S = A ^ B;
assign C = A & B;

endmodule
```
<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/9.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- Half Adder(반가산기)의 자세한 설명은 [[Verilog] Half Adder Design](https://miniharu22.github.io/verilog%20hdl%20(vivado)/v1/) 참고   

&nbsp;

## 1. Full Adder   

```verilog
module FA(A, B, Cin, S, Cout);

input A, B, Cin;
output S, Cout;

wire w1, w2, w3;

assign w1 = A ^ B;
assign w2 = w1 & Cin;
assign w3 = A & B;

assign S = w1 ^ Cin;
assign Cout = w2 | w3;

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/10.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- Full Adder(전가산기)의 자세한 설명은 [[Verilog] Full Adder Design](https://miniharu22.github.io/verilog%20hdl%20(vivado)/v2/) 참고   

&nbsp;

## 2. Basic Adder module for Compression      
### 2-1. adder_4to3 module    

```verilog
// 4-to-3 Adder module
// Compress 4 input bits into 3 output bits
module adder_4to3(in, cout, carry, sum);

    input [3:0] in;             // 4bit input
    wire x0, x1, x2, x3;        // individual input bits
    output cout, carry, sum;    // output bits : final carry out, intermediate carry, sum

    // Assign input bits to individual wires
    assign {x0, x1, x2, x3} = in;

    wire s0, c0, c1;    // intermediate signals


    FA FA0(x0, x1, x2, s0, c0); // 1st stage : Full Adder for x0, x1, x2
    HA HA0(x3, s0, sum, c1);    // 2nd stage : Half Adder for x3 and sum from first stage
    HA HA1(c0, c1, carry, cout);// 3rd stage : Half Adder for carries from first & second stages

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/11.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Total Schematic</strong>
  <img src="/assets/images/npu/12.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **adder_4to3**   
  - 이름 그대로 4bit의 입력을 3bit의 출력으로 압축하는 역할    
  - **데이터 처리 과정**   
    - 3개의 입력 (`x0`,`x1`,`x2`) → Full Adder    
      - 출력 : 합 `s0`, 자리올림 `c0`    
    - 나머지 1개의 입력 `x3` + FA의 합 `s0` → Half Adder    
      - 출력 : 합 `sum`, 자리올림 `c1`   
    - FA의 자리올림 `c0` + HA의 자리올림 `c1` → Half Adder    
      - 출력 : 중간 자리올림 `carry`, 최종 자리올림 `cout`    

&nbsp;

### 2-2. adder_5to3 module   

```verilog
// 5-to-3 Adder module
// Compress 5 input bits into 3 output bits
module adder_5to3(in, cout, carry, sum);

    input [4:0] in;             // 5bit input
    wire x0, x1, x2, x3, x4;
    output cout, carry, sum;

    assign {x0, x1, x2, x3, x4} = in;

    // Intermediate OR/AND calculations for pairs
    wire y0, y1, y2, y3;

    assign y0 = x4 | x3;    // OR of top two bits
    assign y1 = x4 & x3;    // AND of top two bits
    assign y2 = x2 | x1;    // OR of next two bits
    assign y3 = x2 & x1;    // AND of next two bits

    // Intermediate signals for compressing logic
    wire sand0, sand1, sxor0;

    assign sand0 = y2 & (~y3);      // AND of y2 & ~y3
    assign sand1 = y0 & (~y1);      // AND of y0 & ~y1
    assign sxor0 = sand0 ^ sand1;   // XOR of intermediate signals
    assign sum = sxor0 ^ x0;        // sum output : final XOR with lowest bit

    // Multiplexer-like selection based on sxor0
    wire mux0;
    assign mux0 = sxor0 ? x0 : y3;  // if sxor0 == 1, x0; else y3

    // Final carry and carry-out calculations
    wire cor0, cand0;
    assign cor0 = y1 | y2;      // OR of y1 & y2
    assign cand0 = y0 & cor0;   // AND of y0 & cor0

    assign carry = mux0 ^ cand0;    // intermediate carry output
    assign cout = mux0 & cand0;     // final carry-out output

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/13.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **adder_5to3**   
  - 5bit의 입력을 3bit의 출력으로 압축하는 역할    
  - **데이터 처리 과정**
    - 상위 2개의 입력 (`x4`, `x3`) → OR 및 AND
      - 출력: OR 결과 `y0`, AND 결과 `y1`
    - 그 아래 2개의 입력 (`x2`, `x1`) → OR 및 AND
      - 출력: OR 결과 `y2`, AND 결과 `y3`
    - 중간 신호 생성
      - `y2` & (~`y3`) → `sand0`
      - `y0` & (~`y1`) → `sand1`
      - `sand0` ^ `sand1` → `sxor0`
    - 최종 합 생성
      - `sxor0` ^ `x0` → `sum`
    - MUX 역할 (선택)
      - `mux0` = (`sxor0` ? `x0` : `y3`)
    - 자리올림 계산
      - `y1` \| `y2` → `cor0`
      - `y0` & `cor0` → `cand0`
      - `carry` = `mux0` ^ `cand0`
      - `cout` = `mux0` & `cand0`


&nbsp;

### 2-3. adder_6to3 module   

```verilog
// 6-to-3 Adder module   
// Compress 6 input bits into 3 output bits
module adder_6to3(in, cout, carry, sum);

    input [5:0] in;     // 6bit input
    wire x1, x2, x3, x4, x5, x6;
    output cout, carry, sum;

    assign {x1, x2, x3, x4, x5, x6} = in;

    // Pairwise AND & XOR for 3 pairs
    wire and12, xor12, and34, xor34, and56, xor56;

    assign and12 = x1 & x2; // AND of first pair
    assign xor12 = x1 ^ x2; // XOR of first pair
    assign and34 = x3 & x4; // AND of second pair
    assign xor34 = x3 ^ x4; // XOR of second pair
    assign and56 = x5 & x6; // AND of third pair
    assign xor56 = x5 ^ x6; // XOR of third pair

    // Intermediate signals for compression logic
    wire  and4, xor4, and5, xor5, xor6;

    assign and4 = and12 & and34;    // AND of two ANDs
    assign xor4 = and12 ^ and34;    // XOR of two ANDs
    assign and5 = xor12 & xor34;    // AND of two XORs
    assign xor5 = xor12 ^ xor34;    // XOR of two XORs
    assign xor6 = xor56;            // XOR of third pair

    // Further compression of intermediate results
    wire xor10, xor7;          

    assign xor10 = xor4 ^ and5; // XOR compression result
    assign xor7 = and56;        // direct pass-through of third pair AND

    // Additional intermediate logic
    wire and8, xor8, and9, xor9;

    assign and8 = xor5 & xor6;  // AND of second stage XORs
    assign xor8 = xor5 ^ xor6;  // sum output
    assign and9 = xor10 & xor7; // AND compression 
    assign xor9 = xor10 ^ xor7; // intermediate compression

    // Final carry & carry out calculations
    wire xor12_, and11, xor11, xor13;

    assign xor12_ = and9 ^ and11;   // intermediate AND
    assign and11 = xor9 & and8;     // XOR of intermediate ANDs
    assign xor11 = xor9 ^ and8;     // carry output
    assign xor13 = xor12_ ^ and4;   // final carry out output

    // Final outputs
    assign sum = xor8;      // sum output (LSB)
    assign carry = xor11;   // intermediate carry output
    assign cout = xor13;    // final carry out output (MSB)

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/14.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **adder_6to3**   
  - 6bit의 입력을 3bit의 출력으로 압축하는 역할  
  - **데이터 처리 과정**
    - **1단계: 입력 분리**
      - 입력: 6개의 입력 비트 (`x1`, `x2`, `x3`, `x4`, `x5`, `x6`)
    - **2단계: Pairwise 연산**
      - 첫 번째 쌍 (`x1`, `x2`)
        - AND: `and12`
        - XOR: `xor12`
      - 두 번째 쌍 (`x3`, `x4`)
        - AND: `and34`
        - XOR: `xor34`
      - 세 번째 쌍 (`x5`, `x6`)
        - AND: `and56`
        - XOR: `xor56`
    - **3단계: 중간 압축 연산**
      - 두 쌍의 AND 결과
        - AND: `and4` = `and12` & `and34`
        - XOR: `xor4` = `and12` ^ `and34`
      - 두 쌍의 XOR 결과
        - AND: `and5` = `xor12` & `xor34`
        - XOR: `xor5` = `xor12` ^ `xor34`
      - 세 번째 쌍의 XOR: `xor6` = `xor56`
    - **4단계: 중간 결합**
      - XOR 압축 결과: `xor10` = `xor4` ^ `and5`
      - 세 번째 쌍의 AND 결과: `xor7` = `and56`
    - **5단계: 추가 압축 연산**
      - AND: `and8` = `xor5` & `xor6`
      - XOR: `xor8` = `xor5` ^ `xor6` (최종 `sum`으로 출력됨)
      - AND: `and9` = `xor10` & `xor7`
      - XOR: `xor9` = `xor10` ^ `xor7`
    - **6단계: 최종 carry 및 cout 생성**
      - AND: `and11` = `xor9` & `and8`
      - XOR: `xor12_` = `and9` ^ `and11`
      - XOR: `xor11` = `xor9` ^ `and8` (최종 `carry`로 출력됨)
      - XOR: `xor13` = `xor12_` ^ `and4` (최종 `cout`로 출력됨)
    - **최종 출력**
      - `sum` = `xor8`
      - `carry` = `xor11`
      - `cout` = `xor13`


&nbsp;

### 2-4. adder_7to3 module   

```verilog
// 7-to-3 Adder module
// Compress 7 input bits into 3 output bits
module adder_7to3(in, cout, carry, sum);

    input [6:0] in;     // 7bit input
    wire x1, x2, x3, x4, x5, x6, x7;
    output cout, carry, sum;

    assign {x1, x2, x3, x4, x5, x6, x7} = in;

    // Pairwise AND & XOR for the first three pairs
    wire and12, xor12, and34, xor34, and56, xor56;

    assign and12 = x1 & x2;    // AND of first pair
    assign xor12 = x1 ^ x2;    // XOR of first pair
    assign and34 = x3 & x4;    // AND of second pair
    assign xor34 = x3 ^ x4;    // XOR of second pair
    assign and56 = x5 & x6;    // AND of third pair
    assign xor56 = x5 ^ x6;    // XOR of third pair

    // Intermediate signals for compression logic
    wire  and4, xor4, and5, xor5, and6, xor6;

    assign and4 = and12 & and34;  // AND of two ANDs (first and second pairs)
    assign xor4 = and12 ^ and34;  // XOR of two ANDs
    assign and5 = xor12 & xor34;  // AND of two XORs (first and second pairs)
    assign xor5 = xor12 ^ xor34;  // XOR of two XORs
    assign and6 = x7 & xor56;     // AND of x7 and third pair XOR
    assign xor6 = x7 ^ xor56;     // XOR of x7 and third pair XOR

    // Further compression of intermediate results
    wire xor10, xor7;

    assign xor10 = xor4 ^ and5;   // XOR compression result
    assign xor7 = and56 ^ and6;   // XOR of ANDs from third pair and x7

    // Additional intermediate logic
    wire and8, xor8, and9, xor9;

    assign and8 = xor5 & xor6;    // AND of second stage XORs
    assign xor8 = xor5 ^ xor6;    // sum output
    assign and9 = xor10 & xor7;   // AND compression of intermediate XORs
    assign xor9 = xor10 ^ xor7;   // intermediate XOR compression

    // Final carry and carry-out calculations
    wire xor12_, and11, xor11, xor13;

    assign and11 = xor9 & and8;   // intermediate AND for carry
    assign xor12_ = and9 ^ and11; // XOR of intermediate ANDs
    assign xor11 = xor9 ^ and8;   // carry output
    assign xor13 = xor12_ ^ and4; // final carry-out output

    // Final outputs
    assign sum = xor8;            // sum output (LSB)
    assign carry = xor11;         // intermediate carry output
    assign cout = xor13;          // final carry-out output (MSB)

endmodule
```
<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/15.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **adder_7to3**   
  - 7bit의 입력을 3bit의 출력으로 압축하는 역할  
  - **데이터 처리 과정**
    - **1단계: 입력 분리**
      - 입력: 7개의 입력 비트 (`x1`, `x2`, `x3`, `x4`, `x5`, `x6`, `x7`)
    - **2단계: Pairwise 연산**
      - 첫 번째 쌍 (`x1`, `x2`)
        - AND: `and12`
        - XOR: `xor12`
      - 두 번째 쌍 (`x3`, `x4`)
        - AND: `and34`
        - XOR: `xor34`
      - 세 번째 쌍 (`x5`, `x6`)
        - AND: `and56`
        - XOR: `xor56`
    - **3단계: 중간 압축 연산**
      - 두 쌍의 AND 결과
        - AND: `and4` = `and12` & `and34`
        - XOR: `xor4` = `and12` ^ `and34`
      - 두 쌍의 XOR 결과
        - AND: `and5` = `xor12` & `xor34`
        - XOR: `xor5` = `xor12` ^ `xor34`
      - 세 번째 쌍의 XOR와 `x7`의 결합
        - AND: `and6` = `x7` & `xor56`
        - XOR: `xor6` = `x7` ^ `xor56`
    - **4단계: 중간 결합**
      - XOR 압축 결과: `xor10` = `xor4` ^ `and5`
      - 세 번째 쌍의 AND와 `x7`의 AND 결과: `xor7` = `and56` ^ `and6`
    - **5단계: 추가 압축 연산**
      - AND: `and8` = `xor5` & `xor6`
      - XOR: `xor8` = `xor5` ^ `xor6` (최종 `sum`으로 출력됨)
      - AND: `and9` = `xor10` & `xor7`
      - XOR: `xor9` = `xor10` ^ `xor7`
    - **6단계: 최종 carry 및 cout 생성**
      - AND: `and11` = `xor9` & `and8`
      - XOR: `xor12_` = `and9` ^ `and11`
      - XOR: `xor11` = `xor9` ^ `and8` (최종 `carry`로 출력됨)
      - XOR: `xor13` = `xor12_` ^ `and4` (최종 `cout`로 출력됨)
    - **최종 출력**
      - `sum` = `xor8`
      - `carry` = `xor11`
      - `cout` = `xor13`

&nbsp;