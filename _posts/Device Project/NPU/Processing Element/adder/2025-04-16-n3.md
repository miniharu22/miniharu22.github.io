---
layout : single
title: "[Verilog] 4bit Compression Adder Design #1"
categories: 
  - Universal NPU-CNN Accelarator
toc: true
toc_sticky: true
use_math: true
---

8~11bit를 4bit로 압축하기 위한 Adder module 설계   

## 0. adder_8to4 module   

```verilog
// 8-to-4 Adder module
// Compress 8 input bits into 4 output bits
module adder_8to4(in, O3, O2, O1, O0);

    input [7:0] in;         // 8bit input
    wire x0, x1, x2, x3, x4, x5, x6, x7;
    output O3, O2, O1, O0;  // 4bit output

    // Assign input bits to individual wires
    assign {x0, x1, x2, x3, x4, x5, x6, x7} = in;

    // Intermediate signals for partial sums and carries
    wire s00, s01, s10, s11, s2, c0, c1; 

    // First stage : Compress the top 3bits using a Full Adder
    FA FA0(x5, 
           x6, 
           x7, 
           s00,     // partial sum output
           s10);    // partial carry output

    // First stage : Compress the lower 5bits using a 5-to-3 compressor adder
    adder_5to3 A530( {x0, x1, x2, x3, x4}, 
                      s2,   // most significant carry-out
                      s11,  // intermediate carry
                      s01); // partial sum

    // Second stage : Add partial sums from first stage
    HA HA0(s00, 
           s01, 
           O0,  // output bit 0 (LSB)
           c0); // carry out

    // Second stage : Add intermediate carries & carry out
    FA FA1(s10, 
           s11, 
           c0, 
           O1,  // output bit 1
           c1); // carry out

    // Final stage : Add most significant carry & carry out  
    HA HA1(s2, 
           c1, 
           O2,  // output bit 2
           O3); // output bit 3 (MSB)

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/16.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Total Schematic</strong>
  <img src="/assets/images/npu/17.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **adder_8to4**   
  - 8bit의 입력을 4bit의 출력으로 압축하는 역할    
  - **데이터 처리 과정**
    - **1단계: 입력 분리**
      - 입력: 8개의 입력 비트 (`x0`, `x1`, `x2`, `x3`, `x4`, `x5`, `x6`, `x7`)
    - **2단계: 상위 3비트 처리**
      - Full Adder (`FA0`)로 상위 3비트 (`x5`, `x6`, `x7`)를 합산
        - 출력:
          - `s00`: partial sum
          - `s10`: partial carry
    - **3단계: 하위 5비트 처리**
      - 5-to-3 Compressor (`adder_5to3`)로 하위 5비트 (`x0`, `x1`, `x2`, `x3`, `x4`)를 압축
        - 출력:
          - `s01`: partial sum
          - `s11`: intermediate carry
          - `s2`: most significant carry
    - **4단계: partial sum 합산**
      - Half Adder (`HA0`)로 `s00` + `s01`를 계산
        - 출력:
          - `O0`: 최하위 출력 비트 (LSB)
          - `c0`: carry-out
    - **5단계: 중간 carry 및 carry-out 처리**
      - Full Adder (`FA1`)로 `s10` + `s11` + `c0`를 계산
        - 출력:
          - `O1`: 두 번째 출력 비트
          - `c1`: carry-out
    - **6단계: 최종 carry 처리**
      - Half Adder (`HA1`)로 `s2` + `c1`를 계산
        - 출력:
          - `O2`: 세 번째 출력 비트
          - `O3`: 최상위 출력 비트 (MSB)
    - **최종 출력**
      - `O0`, `O1`, `O2`, `O3` = 4비트 출력 (합산된 결과)

&nbsp;

## 1. adder_9to4 module   

```verilog
// 9-to-4 Adder module   
// Compress 9 input bits into 4 output bits
module adder_9to4(in, O3, O2, O1, O0);

    input [8:0] in;         // 9bit input
    wire x0, x1, x2, x3, x4, x5, x6, x7, x8;
    output O3, O2, O1, O0;  // 4bit output

    // Assign input bits to individual wires
    assign {x0, x1, x2, x3, x4, x5, x6, x7, x8} = in;

    // Intermediate signals for partial sums & carries
    wire s00, s01, s10, s11, s2, c0, c1; 

    // First stage : Compress the top 2bit using a Half adder
    HA HA0(x7, 
           x8, 
           s00,     // partial sum output
           s10);    // partial carry output

    // First stage : Compress the lower 7bits using a 7-to-3 compressor 
    adder_7to3 A730( {x0, x1, x2, x3, x4, x5, x6}, 
                      s2,   // most significant carry out
                      s11,  // intermediate carry
                      s01); // partial sum

    // Second stage : Add partial sums from first stage
    HA HA1(s00, 
           s01, 
           O0,  // output bit 0 (LSB)
           c0); // carry out
    
    // Second stage : Add intermediate carries & carry out
    FA FA0(s10, 
           s11, 
           c0,  
           O1,  // output bit 1
           c1); // carry out

    // Final stage : Add most significant carry & carry out
    HA HA2(s2, 
           c1, 
           O2,  // output bit 2
           O3); // output bit 3 (MSB)

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/18.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Total Schematic</strong>
  <img src="/assets/images/npu/19.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **adder_9to4**   
  - 9bit의 입력을 4bit의 출력으로 압축하는 역할    
  - **데이터 처리 과정**
    - **1단계: 입력 분리**
      - 입력: 9개의 입력 비트 (`x0`, `x1`, `x2`, `x3`, `x4`, `x5`, `x6`, `x7`, `x8`)
    - **2단계: 상위 2비트 처리**
      - Half Adder (`HA0`)로 상위 2비트 (`x7`, `x8`)를 합산
        - 출력:
          - `s00`: partial sum
          - `s10`: partial carry
    - **3단계: 하위 7비트 처리**
      - 7-to-3 Compressor (`adder_7to3`)로 하위 7비트 (`x0` ~ `x6`)를 압축
        - 출력:
          - `s01`: partial sum
          - `s11`: intermediate carry
          - `s2`: most significant carry
    - **4단계: partial sum 합산**
      - Half Adder (`HA1`)로 `s00` + `s01`를 계산
        - 출력:
          - `O0`: 최하위 출력 비트 (LSB)
          - `c0`: carry-out
    - **5단계: 중간 carry 및 carry-out 처리**
      - Full Adder (`FA0`)로 `s10` + `s11` + `c0`를 계산
        - 출력:
          - `O1`: 두 번째 출력 비트
          - `c1`: carry-out
    - **6단계: 최종 carry 처리**
      - Half Adder (`HA2`)로 `s2` + `c1`를 계산
        - 출력:
          - `O2`: 세 번째 출력 비트
          - `O3`: 최상위 출력 비트 (MSB)
    - **최종 출력**
      - `O0`, `O1`, `O2`, `O3` = 4비트 출력 (합산된 결과)

&nbsp;

## 2. adder_10to4 module   

```verilog
// 10-to-4 Adder module
// Compress 10 input bits into 4 output bits
module adder_10to4(in, O3, O2, O1, O0);

    input [9:0] in;         // 10bit input
    wire x0, x1, x2, x3, x4, x5, x6, x7, x8, x9;
    output O3, O2, O1, O0;  // 4bit output (sum)

    // Assign input bits to individual wires
    assign {x0, x1, x2, x3, x4, x5, x6, x7, x8, x9} = in;

    // Intermediate partial sum & carry frmo 5-to-3 compressors
    wire A[2:0];    // Partial sum & carry output for second 5-to-3 adder
    wire B[2:0];    // Partial sum & carry output for first 5-to-3 adder

    // First 5-to-3 adder
    // Compress lower 5bit
    adder_5to3 A530({x0, x1, x2, x3, x4}, 
                     B[2],  // most significant carry out
                     B[1],  // intermediate carry
                     B[0]); // partial sum
    
    // Second 5-to-3 adder
    // Compress upper 5bit
    adder_5to3 A531({x5, x6, x7, x8, x9}, 
                     A[2],  // most significant carry out
                     A[1],  // intermediate carry   
                     A[0]); // partial sum

    // Intermediate carries for final adders
    wire C1, C2;

    // Final stage : Add partial sums & carries to produce 4bit output
    HA HA0(A[0], 
           B[0],   
           O0,  // output bit 0 (LSB)
           C1); // carry out
    
    FA FA0(A[1], 
           B[1], 
           C1,   
           O1,  // output bit 1
           C2); // carry out

    FA FA1(A[2], 
           B[2], 
           C2, 
           O2,  // output bit 2
           O3); // output bit 3 (MSB)

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/20.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Total Schematic</strong>
  <img src="/assets/images/npu/21.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **adder_10to4**   
  - 10bit의 입력을 4bit의 출력으로 압축하는 역할    
  - **데이터 처리 과정 (10-to-4 Adder)**
    - **1단계: 입력 분리**
      - 입력: 10개의 입력 비트 (`x0`, `x1`, `x2`, `x3`, `x4`, `x5`, `x6`, `x7`, `x8`, `x9`)
    - **2단계: 하위 5비트 압축**
      - 5-to-3 Compressor (`adder_5to3`)로 하위 5비트 (`x0`~`x4`)를 압축
        - 출력:
          - `B[0]`: partial sum
          - `B[1]`: intermediate carry
          - `B[2]`: most significant carry
    - **3단계: 상위 5비트 압축**
      - 5-to-3 Compressor (`adder_5to3`)로 상위 5비트 (`x5`~`x9`)를 압축
        - 출력:
          - `A[0]`: partial sum
          - `A[1]`: intermediate carry
          - `A[2]`: most significant carry
    - **4단계: partial sum 합산**
      - Half Adder (`HA0`)로 `A[0]` + `B[0]`를 계산
        - 출력:
          - `O0`: 최하위 출력 비트 (LSB)
          - `C1`: carry-out
    - **5단계: 중간 carry 및 carry-out 처리**
      - Full Adder (`FA0`)로 `A[1]` + `B[1]` + `C1`를 계산
        - 출력:
          - `O1`: 두 번째 출력 비트
          - `C2`: carry-out
    - **6단계: 최종 carry 처리**
      - Full Adder (`FA1`)로 `A[2]` + `B[2]` + `C2`를 계산
        - 출력:
          - `O2`: 세 번째 출력 비트
          - `O3`: 최상위 출력 비트 (MSB)
    - **최종 출력**
      - `O0`, `O1`, `O2`, `O3` = 4비트 출력 (합산된 결과)

&nbsp;

## 3. adder_11to4 module  

```verilog
// 11-to-4 Adder module
// Compress 11 input bits into 4 output bits
module adder_11to4(in, O3, O2, O1, O0);

    input [10:0] in;        // 11bit input
    wire x0, x1, x2, x3, x4, x5, x6, x7, x8, x9, x10;
    output O3, O2, O1, O0;  // 4bit output (sum)

    // Assign input bits to individual wires
    assign {x0, x1, x2, x3, x4, x5, x6, x7, x8, x9, x10} = in;

    // Intermediate partial sum & carry outputs from compressors
    wire A[2:0];    // Partial sum & carry outputs for upper 5-to-3 adder
    wire B[2:0];    // Partial sum & carry outputs for lower 6-to-3 adder

    // Fisrt compressor : Compress lower 6bits
    adder_6to3 A630({x0, x1, x2, x3, x4, x5}, 
                     B[2],   // most significant carry out
                     B[1],  // intermediate carry
                     B[0]); // partial sum

    // Second compressor : Compress upper 5bits
    adder_5to3 A531({x6, x7, x8, x9, x10}, 
                     A[2],  // most significant carry out
                     A[1],  // intermediate carry
                     A[0]); // partial sum

    // Intermediate carries for final adders
    wire C1, C2;

    // Final stage : Add partial sums & carries to produce 4bit output
    HA HA0(A[0], 
           B[0], 
           O0,  // output bit 0 (LSB)
           C1); // carry out

    FA FA0(A[1], 
           B[1], 
           C1, 
           O1,  // output bit 1
           C2); // carry out

    FA FA1(A[2], 
           B[2], 
           C2, 
           O2,  // output bit 2
           O3); // output bit 3 (MSB)

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/22.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Total Schematic</strong>
  <img src="/assets/images/npu/23.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **adder_11to4**   
  - 11bit의 입력을 4bit의 출력으로 압축하는 역할 
  - **데이터 처리 과정**
    - **1단계: 입력 분리**
      - 입력: 11개의 입력 비트 (`x0`, `x1`, `x2`, `x3`, `x4`, `x5`, `x6`, `x7`, `x8`, `x9`, `x10`)
    - **2단계: 하위 6비트 압축**
      - 6-to-3 Compressor (`adder_6to3`)로 하위 6비트 (`x0`~`x5`)를 압축
        - 출력:
          - `B[0]`: partial sum
          - `B[1]`: intermediate carry
          - `B[2]`: most significant carry
    - **3단계: 상위 5비트 압축**
      - 5-to-3 Compressor (`adder_5to3`)로 상위 5비트 (`x6`~`x10`)를 압축
        - 출력:
          - `A[0]`: partial sum
          - `A[1]`: intermediate carry
          - `A[2]`: most significant carry
    - **4단계: partial sum 합산**
      - Half Adder (`HA0`)로 `A[0]` + `B[0]`를 계산
        - 출력:
          - `O0`: 최하위 출력 비트 (LSB)
          - `C1`: carry-out
    - **5단계: 중간 carry 및 carry-out 처리**
      - Full Adder (`FA0`)로 `A[1]` + `B[1]` + `C1`를 계산
        - 출력:
          - `O1`: 두 번째 출력 비트
          - `C2`: carry-out
    - **6단계: 최종 carry 처리**
      - Full Adder (`FA1`)로 `A[2]` + `B[2]` + `C2`를 계산
        - 출력:
          - `O2`: 세 번째 출력 비트
          - `O3`: 최상위 출력 비트 (MSB)
    - **최종 출력**
      - `O0`, `O1`, `O2`, `O3` = 4비트 출력 (합산된 결과)

&nbsp;


