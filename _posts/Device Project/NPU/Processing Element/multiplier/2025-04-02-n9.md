---
layout : single
title: "[Verilog] Partial Product Generator & Multiplier Testbench"
categories: 
  - Universal NPU-CNN Accelarator
toc: true
toc_sticky: true
use_math: true
---

설계한 Partial Product Generator와 Multiplier가 정상적으로 동작하는지 Testbench로 검증    

## 0. PPG Testbench

```verilog
`timescale 1ns/10ps

// Testbench for PPG Module
module tb_ppg;

	reg clk, reset;
    reg signed [7:0] multiplicand, multiplier;  // 8bit signed multiplicand & multiplier
    reg [15:0] mat_in[0:140];   // Array to hold test vectors from input_ppg.txt
    
    wire [10:0] pp0;            // PPG outputs : partial products
    wire [8:0] pp1, pp2, pp3;   
    wire neg0, neg1, neg2, neg3;

    // Instantiate the PPG module
    PPG P0(multiplicand, multiplier, pp0,  pp1,  pp2,  pp3, neg0, neg1, neg2, neg3);

    // Intial reset & input assignments
	initial
	begin
		clk = 1;
        multiplier = 8'b0000_0000;
        multiplicand = 8'b0000_0000;
	end
	
	always #5 clk = ~clk;

    wire [18:0] Ob;             // Sum of partial products with bias
    wire [16:0] O, Om;          // PPG result vs direct multiplication
    wire [18:0] O10;            // Extended test result
    wire signed [18:0] O10m;    // Reference value : multiplier * multiplicand * 9

    // Calculte the expected PPG-based product
    assign Om = multiplier * multiplicand;

    assign Ob = pp0 + (pp1<<2) + (pp2<<4) + (pp3<<6) + (neg0) + (neg1<<2) + (neg2<<4) + (neg3<<6) + 
                    19'b111_1010_1000_0000_0000;

    assign O = Ob[16:0];    // Lower 17bits of the result
    
    // Extended test : Sum of partial products repeated multiple times
    assign O10 = pp0 + (pp1<<2) + (pp2<<4) + (pp3<<6) + (neg0) + (neg1<<2) + (neg2<<4) + (neg3<<6) + 
                pp0 + (pp1<<2) + (pp2<<4) + (pp3<<6) + (neg0) + (neg1<<2) + (neg2<<4) + (neg3<<6) + 
                pp0 + (pp1<<2) + (pp2<<4) + (pp3<<6) + (neg0) + (neg1<<2) + (neg2<<4) + (neg3<<6) + 
                pp0 + (pp1<<2) + (pp2<<4) + (pp3<<6) + (neg0) + (neg1<<2) + (neg2<<4) + (neg3<<6) + 
                pp0 + (pp1<<2) + (pp2<<4) + (pp3<<6) + (neg0) + (neg1<<2) + (neg2<<4) + (neg3<<6) + 

                pp0 + (pp1<<2) + (pp2<<4) + (pp3<<6) + (neg0) + (neg1<<2) + (neg2<<4) + (neg3<<6) + 
                pp0 + (pp1<<2) + (pp2<<4) + (pp3<<6) + (neg0) + (neg1<<2) + (neg2<<4) + (neg3<<6) + 
                pp0 + (pp1<<2) + (pp2<<4) + (pp3<<6) + (neg0) + (neg1<<2) + (neg2<<4) + (neg3<<6) + 
                pp0 + (pp1<<2) + (pp2<<4) + (pp3<<6) + (neg0) + (neg1<<2) + (neg2<<4) + (neg3<<6) + 
                19'b100_1110_1000_0000_0000;    // Additional contant (bias for testing)
    
    // Reference value for extended test
    assign O10m = multiplier * multiplicand * 9;


    integer err = 0, i = 0, j = 0, err10 = 0;

    initial
	begin		
        // Read test vectors from file into mat_in array
		$readmemh("C://MY/Vivado/UniNPU/UniNPU.srcs/data/input_ppg.txt", mat_in);
        #10
		begin
			for (i=0; i<20; i=i+1)
			begin
				{multiplier, multiplicand} = mat_in[i]; // Load test input
                #(1);
                if (O != Om) err = err + 1; // Compare PPG result wuth direct multiplication
				#(9);   // Wait 10ns for next test
			end
		end

        // Exhaustive test : iterate through all possible 8bit input paris (256x256)

        i = 0;
        multiplicand = 8'b1111_1111;    // Start from -1 (Two's complement)
        multiplier = 8'b1111_1111;

        begin
			for (i=0; i<256; i=i+1)
			begin
                multiplier = multiplier + 1;    // Increment multiplier
                for (j=0; j<256; j=j+1)
                begin
                    multiplicand = multiplicand + 1;    // Increment multiplicand
                    #(1);
                    if (O != Om) err = err + 1;         // Check PPG result vs direct multiplication
                    if (O10 != O10m) err10 = err10 + 1; // Check extended test result
                    #(9);
                end
			end
		end

	end

endmodule
```

<div align="left">
    <strong>Simulation : Compare PPG with Direct </strong>
  <img src="/assets/images/npu/54.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulation : 256 x 256 case</strong>
  <img src="/assets/images/npu/55.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 


- **tb_ppg (Testbench for PPG)**   
  - PPG 모듈의 동작을 검증하기 위한 테스트벤치  
  - 다양한 입력 케이스에 대해 PPG 모듈의 Partial Product 결과를 비교/검증  
  - 최종적으로 PPG 모듈의 정확성과 Booth 알고리즘 기반 곱셈 동작의 일치 여부를 확인  

  - **데이터 처리 과정**  
    - **1단계: 입력 및 초기화**  
      - 8비트 signed 입력: `multiplicand`, `multiplier`  
      - 파일(`input_ppg.txt`)에서 16비트 데이터 읽어와서 `mat_in` 배열(141개) 저장  
      - Clock (`clk`) 생성: 10ns 주기 (`always #5 clk=~clk`)  

    - **2단계: PPG 모듈 인스턴스화**  
      - `PPG` 모듈에 8비트 입력을 연결  
      - PPG의 출력:  
        - Partial Product 4개 (`pp0`, `pp1`, `pp2`, `pp3`)  
        - Partial Product의 부호 보정 플래그 (`neg0`, `neg1`, `neg2`, `neg3`)  

    - **3단계: 참조 결과 생성 (Expected Result)**  
      - 직접 곱셈 결과: `Om = multiplicand * multiplier`  
      - PPG의 Partial Product를 합산하여 실제 결과와 비교할 참조값 계산:  
        - `Ob`: Partial Product들의 시프트·덧셈 결과 + 보정 상수  
        - `O`: `Ob`의 하위 17비트 (PPG 연산 결과)  
      - **확장 테스트용 참조 결과:**  
        - `O10`: Partial Product를 여러 번 중첩해 합산한 결과 (스트레스 테스트)  
        - `O10m`: `multiplier * multiplicand * 9` (확장 테스트 참조값)  

    - **4단계: 첫 번째 테스트 루프 (파일 기반)**  
      - `input_ppg.txt`의 앞 20개의 벡터를 순서대로 입력  
      - 각각의 입력에 대해:  
        - PPG 결과(`O`) vs 직접 곱셈(`Om`) 비교  
        - 불일치 시 `err`를 1씩 증가  
      - 각 벡터마다 10ns씩 지연  

    - **5단계: 두 번째 테스트 루프 (exhaustive 테스트)**  
      - `multiplier`와 `multiplicand`를 -128~127 범위로 모두 순회 (256×256=65536 케이스)  
      - 각 케이스마다:  
        - PPG 결과(`O`) vs 직접 곱셈(`Om`) 비교 → 오류 시 `err` 증가  
        - 확장 테스트 결과(`O10`) vs 참조값(`O10m`) 비교 → 오류 시 `err10` 증가  
      - 각 테스트 케이스마다 10ns씩 지연  

    - **최종 출력**  
      - `err`: PPG 곱셈 오류 개수  
      - `err10`: 확장 테스트 결과 오류 개수  
      - 모든 결과를 통해 PPG의 정확성을 검증 

&nbsp;

## 1. Multiplier Testbench   

```verilog
`timescale 1ns/10ps

// Testbench for the multiplier module
module tb_multiplier;

	reg clk, reset;
    wire [8*9-1:0] multiplicand9, multiplier9;  // 72bits input
                                                // 9 copies of 8bit multiplicand & multiplier
    reg signed [7:0] multiplicand, multiplier;  // Single 8bit signed multiplicand & multiplier
    reg [15:0] mat_in[0:140];                   // Test vectors from input_ppg.txt

    // Outputs from the multiplier module
    wire [8:0] O14, O13;
    wire [17:0] O12, O11;
    wire [35:0] O10, O9, O8, O7;
    wire [44:0] O6;
    wire [26:0] O5;
    wire [35:0] O4;
    wire [17:0] O3;
    wire [26:0] O2;
    wire [8:0] O1;
    wire [17:0] O0;

    // Instantiate the multiplier module
    multiplier M0(multiplicand9, multiplier9,
                        O14, O13, O12, O11, O10, O9, O8, O7, O6, O5, O4, O3, O2, O1, O0);

    // Expand the 8bit multiplicand & multiplier into 72bit inputs for parallel operation
    assign multiplier9 = {9{multiplier}};
    assign multiplicand9 = {9{multiplicand}};

	initial
	begin
		clk = 1;
        multiplier = 8'b0000_0000;  // Intialize input values
        multiplicand = 8'b0000_0000;
	end
	
	always #5 clk = ~clk;

    // Functions to compute the sum of bits in partial product slices
    wire [5:0] Oa[14:0];

    // Sum of bits for a single 9bit input vector
    function [5:0] addport1;
    input [8:0] in;
    begin
        addport1 = in[0] + in[1] + in[2] + 
              in[3] + in[4] + in[5] + 
              in[6] + in[7] + in[8] ;
    end
    endfunction

    // Sum of bits for two 9bit input vectors
    function [5:0] addport2;
    input [9-1:0] in1, in2;
    begin
        addport2 = addport1(in1) + addport1(in2);
    end
    endfunction

    // Sum of bits for three 9bit input vectors
    function [5:0] addport3;
    input [9-1:0] in1, in2, in3;
    begin
        addport3 = addport1(in1) + addport1(in2) + addport1(in3);
    end
    endfunction

    // Sum of bits for four 9bit input vectors
    function [5:0] addport4;
    input [9-1:0] in1, in2, in3, in4;
    begin
        addport4 = addport1(in1) + addport1(in2) + addport1(in3) + addport1(in4);
    end
    endfunction

    // Sum of bits for five 9bit input vectors
    function [5:0] addport5;
    input [9-1:0] in1, in2, in3, in4, in5;
    begin
        addport5 = addport1(in1) + addport1(in2) + addport1(in3) + addport1(in4) + addport1(in5);
    end
    endfunction

    
    // Compute bit sums for each output block using the appropriate addport function
    //14, 13, 1 - 1
    assign Oa[14] = addport1(O14);
    assign Oa[13] = addport1(O13);
    assign Oa[1] = addport1(O1);

    //12, 11, 3, 0 - 2
    assign Oa[11] = addport2(O11[17:9], O11[8:0]);
    assign Oa[12] = addport2(O12[17:9], O12[8:0]);
    assign Oa[3] = addport2(O3[17:9], O3[8:0]);
    assign Oa[0] = addport2(O0[17:9], O0[8:0]);


    // 10, 9, 8, 7, 4 - 4
    assign Oa[10] = addport4(O10[35:27], O10[26:18], O10[17:9], O10[8:0]);
    assign Oa[9] = addport4(O9[35:27], O9[26:18], O9[17:9], O9[8:0]);
    assign Oa[8] = addport4(O8[35:27], O8[26:18], O8[17:9], O8[8:0]);
    assign Oa[7] = addport4(O7[35:27], O7[26:18], O7[17:9], O7[8:0]);
    assign Oa[4] = addport4(O4[35:27], O4[26:18], O4[17:9], O4[8:0]);

    //6 - 5
    assign Oa[6] = addport5(O6[44:36], O6[35:27], O6[26:18], O6[17:9], O6[8:0]);

    //5, 2 - 3
    assign Oa[5] = addport3(O5[26:18], O5[17:9], O5[8:0]);
    assign Oa[2] = addport3(O2[26:18], O2[17:9], O2[8:0]);

    // Compute the final result by shifting & summing bit-slice results
    wire signed [18:0] O9o, O9m;
    assign O9o = (Oa[14]<<14) + (Oa[13]<<13) + (Oa[12]<<12) + (Oa[11]<<11) + 
                (Oa[10]<<10) + (Oa[9]<<9) + (Oa[8]<<8) + (Oa[7]<<7) + (Oa[6]<<6) + 
                (Oa[5]<<5) + (Oa[4]<<4) + (Oa[3]<<3) + (Oa[2]<<2) + (Oa[1]<<1) + Oa[0] +
                19'b100_1110_1000_0000_0000;
    
    // Reference result : Direct multiplication 9 times 
    assign O9m = multiplier * multiplicand * 9;

    // Test procedure
    integer err = 0, i = 0, j = 0, err2 = 0;
    initial
	begin		
		$readmemh("C://MY/Vivado/UniNPU/UniNPU.srcs/data/input_ppg.txt", mat_in);
        #10
		begin
            // First loop : check initial; 20 test vectors
			for (i=0; i<20; i=i+1)
			begin
				{multiplier, multiplicand} = mat_in[i];
                #(1);
                if (O9o != O9m) err = err + 1;  // Compare final adder tree result to expected value
				#(9);
			end
		end
        
        // Exhaustive test loop : test all 256x256 input combinations
        i = 0;
        multiplicand = 8'b1111_1111;    // Start from -1 (Two's complement)
        multiplier = 8'b1111_1111;

        begin
			for (i=0; i<256; i=i+1)
			begin
                multiplier = multiplier + 1;    // Increment multiplier
                for (j=0; j<256; j=j+1)
                begin
                    multiplicand = multiplicand + 1;    // Increment multiplicand
                    #(1);
                    if (O9o != O9m) err2 = err2 + 1;  // Check for any mismatches
                    #(9);
                end
			end
		end

	end

endmodule
```

<div align="left">
    <strong>Simulation : Compare PPG with Direct </strong>
  <img src="/assets/images/npu/56.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulation : 256 x 256 case</strong>
  <img src="/assets/images/npu/57.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **tb_multiplier (Testbench for multiplier module)**  
  - `multiplier` 모듈이 9쌍의 8비트 곱셈을 올바르게 수행하는지 검증  
  - 다양한 입력값으로 PPG/Adder Tree 방식의 곱셈과 직접 곱셈 결과를 비교하여 정확성 확인  

  - **데이터 처리 과정**  
    - **1단계: 입력 및 초기화**  
      - 8비트 signed 입력: `multiplier`, `multiplicand`  
      - 이 둘을 9번씩 복제하여 72비트 입력(`multiplier9`, `multiplicand9`)으로 확장  
      - 입력 벡터는 `input_ppg.txt`에서 16비트 단위로 읽어옴  
        - 한 줄: 상위 8비트 `multiplier`, 하위 8비트 `multiplicand`  

    - **2단계: multiplier 모듈 연결**  
      - `multiplier` 모듈은 9쌍의 입력으로 9쌍의 Partial Product 결과 (`O0`~`O14`)를 출력  
      - 각 출력은 비트폭이 다르고, Partial Product의 합산 결과로 구성  

    - **3단계: Partial Product의 비트 합산**  
      - `addport1`~`addport5` 함수로 9비트 입력들의 비트합을 계산  
      - `O14`~`O0` 출력들을 적절히 블록으로 나누어 합산 결과를 `Oa[0:14]`에 저장  
        - 예: `O10` (36비트) → 4개의 9비트 블록으로 나눠서 합산  

    - **4단계: 최종 결과 계산**  
      - `Oa[0:14]`의 합산 결과를 시프트 연산으로 다시 합침  
      - 최종적으로 Partial Product를 모두 시프트·가산하여 최종 결과(`O9o`)를 생성  
      - 오프셋 보정 상수를 추가로 더함  
      - 참조값(`O9m`)은 직접 곱셈 결과(`multiplier * multiplicand`)에 9를 곱하고 상수를 더한 값  

    - **5단계: 검증**  
      - **첫 번째 루프 (파일 기반)**  
        - `input_ppg.txt`의 앞 20개의 입력 벡터를 로드  
        - Partial Product 기반 결과(`O9o`)와 직접 곱셈 기반 결과(`O9m`)를 비교  
        - 불일치 시 `err` 카운트 증가  
      - **두 번째 루프 (exhaustive 테스트)**  
        - `multiplier`, `multiplicand`를 -128~127 범위로 모든 입력쌍 순회  
        - 각 경우마다 `O9o` vs `O9m` 비교 → 불일치 시 `err` 카운트 증가  

    - **최종 출력**  
      - `err`: 최종적으로 몇 번의 불일치가 있었는지 기록  
      - 테스트가 끝나면 `tb_multiplier`는 `multiplier` 모듈의 Booth 기반 병렬 곱셈이 정확한지 여부를 판별  

&nbsp;

## 2. Progress so far   

&nbsp;

<div align="left">
  <img src="/assets/images/npu/4.jpg" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **현재 설계 진행도**
  - 현재까지 Processing Element의 Multiplier 설계를 완료    
  - Feature map과 weight data를 받아 곱셈 값을 출력      
  - 곱셈 연산은 Partial Product를 생성하는 과정이며 이는 Adder tree stage에서 합산할 예정       