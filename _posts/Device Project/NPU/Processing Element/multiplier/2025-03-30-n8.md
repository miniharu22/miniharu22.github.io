---
layout : single
title: "[Verilog] Multiplier Design"
categories: 
  - Universal NPU-CNN Accelarator
toc: true
toc_sticky: true
use_math: true
---

Modified Booth Algorithm을 적용한 Signed Multiplier 설계    

## 0. Modified Booth Encoder       

```verilog
// Modified Booth Encoder
// Encodes a 3bit Booth window into sign, magnitude, and zero flags for partial product generation
module MBE_enc(A, B, C, S);

    input A, B, C;      // Three consecutive input bits (Booth Window)
    output [2:0] S;     // Output encoding  
                        // S[2] = sign (0: +, 1: -)
                        // S[1] = mult (0 : 0 or 2, 1: 1)
                        // S[0] = zero (0: not zero, 1: zero)

    // Determine sign of the partial product
    // Sign is determined by the most significant bit of the 3bit Booth window
    assign S[2] = A;    

    // Determine if the multiflier is 1 (mult=1) or 2 (mult=0)
    // If B and C differ, then multiplier is 1 (odd case); else it's 0 or 2
    assign S[1] = B ^ C;

    // Determine if the partial product is zero (zero = 1)
    // Partial product is zero if all 3bits are the same 
    assign S[0] = (A & B & C) | ((~A) & (~B) & (~C));

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/51.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **MBE_enc**   
  - Booth window(3bit)에 따라 Partial Product의 부호/크기/0여부를 결정짓는 모듈   
  - 해당 모듈의 결과를 바탕으로 Partial Product Generator module에서 실제 partial product를 생성함    
  - **데이터 처리 과정**
    - **1단계: 입력 비트**
      - 입력: 3개의 비트 (`A`, `B`, `C`)
        - `A`: Booth window의 최상위 비트
        - `B`: 중간 비트
        - `C`: 최하위 비트

    - **2단계: 부호 비트 결정 (Sign)**
      - `S[2] = A`
      - 부호 비트는 `A` 비트로 결정됨
        - 0 → 양수
        - 1 → 음수

    - **3단계: 1배 곱셈 여부 결정**
      - `S[1] = B ^ C`
      - 중간 비트와 최하위 비트를 XOR 연산
        - 서로 다르면 → 1배 연산 (±A)
        - 서로 같으면 → 0배나 2배 연산 (0, ±2A)

    - **4단계: Zero flag 결정**
      - `S[0] = (A & B & C) | ((~A) & (~B) & (~C))`
      - 세 비트가 모두 같으면 → partial product는 0
        - 모두 0(000) 또는 모두 1(111) → 0

    - **5단계: 최종 출력**
      - `S[2:0]` 출력
        - `S[2]`: 부호 (0=+, 1=-)
        - `S[1]`: 1배 곱셈 여부 (1=1배, 0=0 또는 2배)
        - `S[0]`: zero flag (1=0, 0=not zero)

&nbsp;

## 1. Partial Product Generator   

```verilog
// Partial Product Generator module
// Generates partial products based on Modified Booth Encoding   
module PPG(multiplicand, multiplier, pp0,  pp1,  pp2,  pp3, neg0, neg1, neg2, neg3);

    // 8bit signed multiplicand & multiplier
    input signed [7:0] multiplicand, multiplier;    
    
    // Array of 3bit signals to store the Booth-encoded controls for each partial product
    wire [2:0] S[0:3];

    // Line 0 : Booth encoding for 4 partial product groups using MBE_enc
    wire [2:0] S0;

    MBE_enc Me0(multiplier[1], multiplier[0], 1'b0, S[0]);          // 1st 3bit window (LSB Side)
    MBE_enc Me1(multiplier[3], multiplier[2], multiplier[1], S[1]); // 2nd 3bit window
    MBE_enc Me2(multiplier[5], multiplier[4], multiplier[3], S[2]); // 3rd 3bit window
    MBE_enc Me3(multiplier[7], multiplier[6], multiplier[5], S[3]); // 4th 3bit window (MSB Side)

    // Array of 8bit signals for signed correction (Two's complement if negative)
    wire [7:0] sign[0:3];

    // Generate sign-adjusted multiplicand for each partial product
    genvar i;
    generate
    for(i=0; i<4; i=i+1) begin : signa
        // If partial product is negative, compute two's complement of multiplicand
        // Equivalent to XOR with sign bit replicated 8 times (same as sign extension)
        assign sign[i] = multiplicand ^ {S[i][2], S[i][2], S[i][2], S[i][2], S[i][2], S[i][2], S[i][2], S[i][2]};
    end
    endgenerate

    // Array of 9bit partial product results
    wire [8:0] m[0:3];

    genvar j;
    generate
    for(j=0; j<4; j=j+1) begin : multia // Determine partial product based on Booth encoding
        assign m[j] = S[j][0] ? 9'b0_0000_0000 :        // If S[j][0] is 1, output zero
                      S[j][1] ? {sign[j][7],sign[j]} :  // If S[j][i] is 1, output 1xmultiplicand
                                {sign[j], S[j][2]};     // Else, output shifted multiplicand for 2x (same as sign-extended form)

    end
    endgenerate

    // Outputs for partial products
    output [10:0] pp0;
    output [8:0] pp1, pp2, pp3;

    // Adjust the sign & format of each partial product for the next stage of multiplication
    assign pp0 = {~m[0][8], m[0][8], m[0]}; // 11bit output : sign correction + 9bit partial product
    assign pp1 = {~m[1][8], m[1][7-:8]};    // 9bit output : sign correction + 8bits
    assign pp2 = {~m[2][8], m[2][7-:8]};    // 9bit output
    assign pp3 = {~m[3][8], m[3][7-:8]};    // 9bit output

    // Flags indicating if the partial product is negative
    output neg0, neg1, neg2, neg3;

    // Determine if the partial product is negative (sign bit 1 and zero flag 0)
    assign neg0 = (S[0][2]) & (~S[0][0]);
    assign neg1 = (S[1][2]) & (~S[1][0]);
    assign neg2 = (S[2][2]) & (~S[2][0]);
    assign neg3 = (S[3][2]) & (~S[3][0]);

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/52.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **PPG**   
  - Modified Booth Encoding (MBE) 방식에 따라 Partial Product를 실제로 생성하는 모듈
  - MBE_enc 모듈로부터 받은 부호/크기/zero flag 정보를 기반으로 Partial Product를 만들어냄
  - 최종적으로 4개의 Partial Product (pp0~pp3)를 출력    
  - **데이터 처리 과정**
    - **1단계: 입력**
      - 입력: 8비트 부호 있는 피승수(`multiplicand`), 8비트 부호 있는 승수(`multiplier`)
      - Booth window로 나눈 multiplier의 4개 창을 처리
        - Booth window:
          - window 0: (multiplier[1], multiplier[0], 0)
          - window 1: (multiplier[3], multiplier[2], multiplier[1])
          - window 2: (multiplier[5], multiplier[4], multiplier[3])
          - window 3: (multiplier[7], multiplier[6], multiplier[5])

    - **2단계: MBE 인코딩 (Booth 인코딩)**
      - 각 Booth window를 MBE_enc 모듈로 전달하여 Partial Product의 형태를 결정 (S[0]~S[3])
        - 출력: 
          - `S[i][2]`: 부호 비트 (0=+, 1=-)
          - `S[i][1]`: 1배 연산 여부 (1=1배, 0=0 또는 2배)
          - `S[i][0]`: zero flag (1=0, 0=not zero)

    - **3단계: 부호 보정 (sign 처리)**
      - Partial Product가 음수일 경우, multiplicand를 1의 보수 형태로 변환 (XOR)
        - `{S[i][2], S[i][2], ..., S[i][2]}`로 sign-extension 후 XOR

    - **4단계: Partial Product 생성**
      - MBE 인코딩 결과에 따라 partial product를 3가지 경우로 나눔:
        - `S[i][0]=1` → Partial Product는 0
        - `S[i][1]=1` → Partial Product = sign-extended multiplicand (1배 연산)
        - else → Partial Product = sign-extended multiplicand의 왼쪽 시프트 형태 (2배 연산)

    - **5단계: Partial Product 출력 조합**
      - partial product를 보정된 형태로 출력
        - pp0: 11비트 (sign correction + 9비트 product)
        - pp1, pp2, pp3: 9비트

    - **6단계: 부호 플래그 출력**
      - partial product가 실제로 음수인 경우를 각 neg0~neg3로 출력
        - `negX = (S[X][2]) & (~S[X][0])` 형태로 음수 여부를 표시

    - **최종 출력**
      - 4개의 partial product와 부호 표시 (neg0~neg3)를 다음 단계에 넘김

&nbsp;

## 2. Multiplier module   

```verilog
// Multiplier module
// Perform parallel multiplications of 9 paris of 8bit numbers using MBA-based PPG
// Arrange the partial products for futher adder tree processing
module multiplier(multiplicand, multiplier,
                        O14, O13, O12, O11, O10, O9, O8, O7, O6, O5, O4, O3, O2, O1, O0);

    // Input : Concatednated 72bit multiplicand & multiplier
    input [8*9-1:0] multiplicand, multiplier;   

    // Seperate the 72bit inputs into 9 seperate 8bit signed number
    wire signed [8-1:0] md[0:8], mr[0:8];

    // Partial Products and negative flags for each pair
    wire [10:0] pp0[0:8];
    wire [8:0] pp1[0:8], pp2[0:8], pp3[0:8];
    wire neg0[0:8], neg1[0:8], neg2[0:8], neg3[0:8];

    // Generate partial products for each of the 9 pair using PPG
    genvar i;
    generate
    for(i=0; i<9; i=i+1) begin : app
        // Extract 8bit segments for each multiplicand & multiplier
        assign md[i] = multiplicand[8*9-1 - 8*i -: 8];
        assign mr[i] = multiplier[8*9-1 - 8*i -: 8];
        // Generate 4 partial products & negative flags for each pair
        PPG P0(md[i], mr[i], pp0[i],  pp1[i],  pp2[i],  pp3[i], neg0[i], neg1[i], neg2[i], neg3[i]);
    end
    endgenerate

    // Transpoese P.P to bit-slice format for efficient additon
    wire [8:0] pp0_trans[0:11];
    wire [8:0] pp1_trans[0:9], pp2_trans[0:9], pp3_trans[0:9];

    genvar j;
    generate
    for(j=0; j<11; j=j+1) begin : transpose_pp0
        // Fort each bit position j,
        // Collect that bit from all 9 pp0   
        assign pp0_trans[j] = {pp0[0][j], pp0[1][j], pp0[2][j], pp0[3][j], pp0[4][j], pp0[5][j], pp0[6][j], pp0[7][j], 
                    pp0[8][j]};
    end


    for(j=0; j<9; j=j+1) begin : transpose_pp123
        // Same for pp1, pp2, pp3, 
        // Collect bit position j from each PPG output
        assign pp1_trans[j] = {pp1[0][j], pp1[1][j], pp1[2][j], pp1[3][j], pp1[4][j], pp1[5][j], pp1[6][j], pp1[7][j], 
                    pp1[8][j]};
        assign pp2_trans[j] = {pp2[0][j], pp2[1][j], pp2[2][j], pp2[3][j], pp2[4][j], pp2[5][j], pp2[6][j], pp2[7][j], 
                    pp2[8][j]};
        assign pp3_trans[j] = {pp3[0][j], pp3[1][j], pp3[2][j], pp3[3][j], pp3[4][j], pp3[5][j], pp3[6][j], pp3[7][j], 
                    pp3[8][j]};
    end

    endgenerate

    // Final output
    // Combined P.P & negative flags for next adder tree stage
    output [8:0] O14, O13;
    output [17:0] O12, O11;
    output [35:0] O10, O9, O8, O7;
    output [44:0] O6;
    output [26:0] O5;
    output [35:0] O4;
    output [17:0] O3;
    output [26:0] O2;
    output [8:0] O1;
    output [17:0] O0;

    // Assign outputs by concatenating bit slices of partial products
    assign O14 = pp3_trans[8];     // Highest order partial product bits
    assign O13 = pp3_trans[7];

    assign O12 = {pp3_trans[6], pp2_trans[8]};  // Combine slices of pp3 and pp2
    assign O11 = {pp3_trans[5], pp2_trans[7]};

    assign O10 = {pp3_trans[4], pp2_trans[6], pp1_trans[8], pp0_trans[10]};
    assign O9 = {pp3_trans[3], pp2_trans[5], pp1_trans[7], pp0_trans[9]};
    assign O8 = {pp3_trans[2], pp2_trans[4], pp1_trans[6], pp0_trans[8]};
    
    assign O7 = {pp3_trans[1], pp2_trans[3], pp1_trans[5], pp0_trans[7]};
    assign O6 = {pp3_trans[0], pp2_trans[2], pp1_trans[4], pp0_trans[6],
                 neg3[0], neg3[1], neg3[2], neg3[3], neg3[4], neg3[5], neg3[6], neg3[7], neg3[8]}; // Include negative corrections

    assign O5 = {pp2_trans[1], pp1_trans[3], pp0_trans[5]};
    assign O4 = {pp2_trans[0], pp1_trans[2], pp0_trans[4],
                 neg2[0], neg2[1], neg2[2], neg2[3], neg2[4], neg2[5], neg2[6], neg2[7], neg2[8]};
    
    assign O3 = {pp1_trans[1], pp0_trans[3]};
    assign O2 = {pp1_trans[0], pp0_trans[2],
                 neg1[0], neg1[1], neg1[2], neg1[3], neg1[4], neg1[5], neg1[6], neg1[7], neg1[8]};
    
    assign O1 = pp0_trans[1];
    assign O0 = {pp0_trans[0],
                 neg0[0], neg0[1], neg0[2], neg0[3], neg0[4], neg0[5], neg0[6], neg0[7], neg0[8]};


endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/53.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **multiplier**   
  - 9쌍의 8비트 곱셈을 동시에 수행하는 MBE 기반의 병렬 곱셈 모듈
  - 각 쌍의 Partial Product를 생성하고, bit-slice 형태로 배열해 다음 Adder Tree 단계로 넘김
  - 최종적으로 여러 비트폭의 출력 (O0~O14)을 만들어냄
  - **데이터 처리 과정**
    - **1단계: 입력 처리**
      - 입력: 9개의 8비트 `multiplicand`와 9개의 8비트 `multiplier` (각 72비트)
      - 72비트를 8비트씩 9개로 분리하여 각 쌍으로 처리
        - `md[i]`: multiplicand의 i번째 8비트
        - `mr[i]`: multiplier의 i번째 8비트

    - **2단계: Partial Product 생성 (PPG 사용)**
      - 각 multiplicand/multiplier 쌍을 PPG 모듈에 입력
      - PPG 모듈에서:
        - 4개의 Partial Product (`pp0`~`pp3`)를 생성
        - 각 Partial Product의 음수 여부를 나타내는 부호 플래그 (`neg0`~`neg3`)도 생성
      - 9쌍 각각에 대해 이 과정을 수행 (총 9개의 PPG)

    - **3단계: Partial Product 전치 (Transpose)**
      - 생성된 Partial Product를 bit-slice 형식으로 재배열
      - Partial Product의 각 비트 위치를 9개의 쌍에서 한 줄로 모음
        - ex) `pp0_trans[0]`: 9개의 `pp0`의 최하위 비트 모음
        - `pp1_trans`, `pp2_trans`, `pp3_trans`도 같은 방식으로 전치
      - 목적: 다음 단계의 덧셈에서 같은 위치의 비트끼리 더하기 위해!

    - **4단계: 출력 신호 조합**
      - 전치된 Partial Product 비트들을 조합하여 최종 출력 신호로 만듦
      - 출력 신호 폭 및 구성:
        - `O14`, `O13`: 9비트 (최상위 Partial Product 비트)
        - `O12`, `O11`: 18비트
        - `O10`~`O7`: 36비트
        - `O6`: 45비트 (Partial Product + 보정 플래그 `neg3`)
        - `O5`: 27비트
        - `O4`: 36비트 (Partial Product + 보정 플래그 `neg2`)
        - `O3`: 18비트
        - `O2`: 27비트 (Partial Product + 보정 플래그 `neg1`)
        - `O1`: 9비트
        - `O0`: 18비트 (Partial Product + 보정 플래그 `neg0`)

    - **5단계: 보정 플래그 포함**
      - 각 출력 신호에는 Partial Product의 부호 보정을 위한 `negX` 플래그도 포함
      - Booth 알고리즘의 2의 보수 처리에 필요한 정보

    - **최종 출력**
      - 최종적으로, 15개의 출력 신호(`O0`~`O14`)를 생성
      - 이 출력들은 Adder Tree나 다음 단계의 처리에서 최종 곱셈 결과를 만드는 데 사용됨

&nbsp;


