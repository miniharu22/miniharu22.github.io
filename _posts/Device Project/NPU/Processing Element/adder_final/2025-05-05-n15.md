---
layout : single
title: "[Verilog] Entire Computation Testbench"
categories: 
  - Universal NPU-CNN Accelarator
toc: true
toc_sticky: true
use_math: true
---

Multiplier ~ Final Adder까지의 모든 동작 과정을 테스트벤치로 검증    

## 0. Entire Computation Testbench    

```verilog
`timescale 1ns/10ps

module tb_totaladder;

	reg clk, reset;
    wire [8*9-1:0] multiplicand9, multiplier9;
    reg signed [7:0] multiplicand, multiplier;
    reg [15:0] mat_in[0:140];
    reg signed [15:0] bias;

    // Partial Products from multiplier
    wire [8:0] O14, O13;
    wire [17:0] O12, O11;
    wire [35:0] O10, O9, O8, O7;
    wire [44:0] O6;
    wire [26:0] O5;
    wire [35:0] O4;
    wire [17:0] O3;
    wire [26:0] O2;
    wire [8:0] O1;
    wire [17:0] O0;

    // Partial sums from adder tree stage1
    wire ao18;
    wire [1:0] ao17;
    wire [2:0] ao16;
    wire [4:0] ao15, ao14;
    wire [6:0] ao13;
    wire [8:0] ao12;
    wire [9:0] ao11;
    wire [10:0] ao10;
    wire [11:0] ao9;
    wire [10:0] ao8;
    wire [9:0] ao7;
    wire [9:0] ao6;
    wire [8:0] ao5;
    wire [7:0] ao4;
    wire [5:0] ao3;

    // Outputs from adder tree stage2
    wire o19;
    wire [2:0] o18, o17;
    wire [3:0] o16, o15, o14, o13, o12, o11, o10, o9, o8, o7;
    wire [2:0] o6, o5;
    wire [1:0] o4;
    wire o3;

    // Outputs from adder tree stage3
    wire [1:0] a19, a18, a17, a16, a15, a14, a13, a12, a11, a10, a9, a8, a7, a6;
    wire a5; 

    // 14bit output from final adder
    wire [13:0] out;

    // Pre-outputs with clipping in Set bound
    reg signed [13:1] pre_output, pre_outputm;

    // Expand 8bit inputs to 72bit for multiplier
    assign multiplier9 = {9{multiplier}};
    assign multiplicand9 = {9{multiplicand}};

    // Instantiate multiplier & adder tree stages & final adder
    multiplier M0(multiplicand9, multiplier9,
                        O14, O13, O12, O11, O10, O9, O8, O7, O6, O5, O4, O3, O2, O1, O0);

    addertree_stage1 AT10(O14, O13, O12, O11, O10, O9, O8, O7, O6, O5, O4, O3, O2, O1, O0, 
                         bias,
                         ao18, ao17, ao16, ao15, ao14, ao13, ao12, ao11, ao10, ao9, ao8, ao7, ao6, ao5, ao4, ao3);

    addertree_stage2 AT20(ao18, ao17, ao16, ao15, ao14, ao13, ao12, ao11, ao10, ao9, ao8, ao7, ao6, ao5, ao4, ao3,
                        pre_output,
                        o19, o18, o17, o16, o15, o14, o13, o12, o11, o10, o9, o8, o7, o6, o5, o4, o3
                        );

    addertree_stage3 AT30(o19, o18, o17, o16, o15, o14, o13, o12, o11, o10, o9, o8, o7, o6, o5, o4, o3,
                        a19, a18, a17, a16, a15, a14, a13, a12, a11, a10, a9, a8, a7, a6, a5
                        );

    adder_final AF0(a19, a18, a17, a16, a15, a14, a13, a12, a11, a10, a9, a8, a7, a6,
                    out);

	initial
	begin
		clk = 1;
        multiplier = 8'b0000_0000;
        multiplicand = 8'b0000_0000;
        pre_output = 0;
        pre_outputm = 0;
	end
	
	always #5 clk = ~clk;


    ////////////////////////////////
    //Reference Data (Direct)  
    ////////////////////////////////

    // Reference result : Direct Calculation with clipped pre-output
    wire signed [19:0] O9m;
    assign O9m = multiplier * multiplicand * 9 + bias + (pre_outputm<<6);

    ////////////////////////////////
    //Set bound for Reference
    ////////////////////////////////

    reg signed [13:1] outm;

    always @(*) begin
        if (O9m > 20'sd262143) outm = 13'sd4095;
        else if (O9m < -20'sd262144) outm = -13'sd4096;
        else outm = O9m[18-:13];
    end

    always begin 
        #10 pre_outputm = outm; 
        #10 pre_outputm = outm; 
        #10 pre_outputm = outm;
        #10 pre_outputm = 0; 
    end


    /////////////////////////////////////////////////
    //Set bound for Logic (Muliply-Accumulate)
    /////////////////////////////////////////////////
    
    wire signed [13:1] outa;

    assign outa = out[13-:2] == 2'b01 ? 'b0_1111_1111_1111 :
                  out[13-:2] == 2'b10 ? 'b1_0000_0000_0000 : 
                  out[12-:13];

    always begin 
        #10 pre_output = outa; 
        #10 pre_output = outa; 
        #10 pre_output = outa; 
        #10 pre_output = 0; 
    end


    //////////////////
    //Comparison Test
    //////////////////

    integer err = 0, i = 0, j = 0, err2 = 0;
    initial
	begin
        bias = 16'b0000_0000_0000_0000;		
		$readmemh("C://MY/Vivado/UniNPU/UniNPU.srcs/data/input_ppg.txt", mat_in);
        #20
		begin
            // Test loop : Comparison for randomly 30 cases
			for (i=0; i<30; i=i+1)
			begin
				{multiplier, multiplicand} = mat_in[i];
                #(1);
                if (outa - outm > 'sd16 | outa - outm > 'sd16) err = err + 1;
				#(9);
			end
		end
        i = 0;
        multiplicand = 8'b1111_1111;
        multiplier = 8'b1111_1111;

        begin
            // Test loop : Comparison for 256x256 combination cases
			for (i=0; i<256; i=i+1)
			begin
                multiplier = multiplier + 1;
                for (j=0; j<256; j=j+1)
                begin
                    multiplicand = multiplicand + 1;
                    bias = bias + 5;
                    #(1);
                    if (outa - outm > 'sd16 | outa - outm > 'sd16) err2 = err2 + 1;
                    #(9);
                end
			end
		end

	end

endmodule
```

<div align="left">
    <strong>Simulaton : Multiplication</strong>
  <img src="/assets/images/npu/88.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulaton : Adder Tree Stage1</strong>
  <img src="/assets/images/npu/90.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulaton : Adder Tree Stage2</strong>
  <img src="/assets/images/npu/91.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulaton : Adder Tree Stage3</strong>
  <img src="/assets/images/npu/92.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulaton : Final Adder & Set Bound</strong>
  <img src="/assets/images/npu/93.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulaton : Randomly 30 Case </strong>
    <strong>Direct Calculation vs Logic Computation</strong>
  <img src="/assets/images/npu/94.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulaton : 256x256 Case Test</strong>
    <strong>Direct Calculation vs Logic Computation</strong>
  <img src="/assets/images/npu/95.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
  <img src="/assets/images/npu/96.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 


- **tb_totaladder (Testbench for Entire Adder Path)**  
    - 전체 multiplier, addertree (stage1~3), 최종 adder_final을 포함한 전체 누적 합산 경로를 검증하는 테스트벤치  
    - 각 단계별로 partial sum을 누적하고, 최종 결과를 참조값과 비교하여 정확도를 검증  
    - 누적되는 부분합과 clipping 처리까지 전체적인 동작을 시뮬레이션
    - **데이터 처리 과정**  
      - **1단계: 입력 및 초기화**  
        - 8비트 signed 입력: `multiplicand`, `multiplier`  
        - 입력 데이터는 파일(`input_ppg.txt`)에서 읽어와 `mat_in` 배열(141개) 저장  
        - bias (16비트 signed)도 함께 사용  
        - Clock (`clk`) 생성: 10ns 주기 (`always #5 clk=~clk`)     
      - **2단계: Multiplier 계산**  
        - 8비트 입력을 72비트(9개씩 복제)로 확장 → Multiplier로 입력  
        - Partial Product 출력: `O0 ~ O14` (9~45비트 크기)  
      - **3단계: addertree stage1 처리**  
        - Partial Product들을 addertree_stage1에 입력  
        - Partial Sum 출력: `ao18 ~ ao3`  
      - **4단계: addertree stage2 처리**  
        - Stage1의 Partial Sum과 이전 출력(`pre_output`)을 입력으로 사용  
        - 누적 Partial Sum 출력: `o19 ~ o3`  
      - **5단계: addertree stage3 처리**  
        - Stage2의 Partial Sum을 Carry-Save 형태(`a19 ~ a5`)로 변환  
        - Stage3에서 Carry-Save 합산을 수행  
      - **6단계: 최종 adder 처리**  
        - Stage3 결과(`a19 ~ a5`)를 Carry Look Ahead 방식으로 최종 14비트 결과(`out`)로 계산  
      - **7단계: Clipping 처리 (set bound)**  
        - Reference 결과(`O9m`)와 Logic 결과(`outa`) 모두 Clipping 처리, 이후 Stage2에 입력하여 누적 합산    
            - Clipping 범위: ±4096 (`±13비트`)  
        - `pre_outputm`, `pre_output`으로 4번씩 갱신 후 0으로 초기화  
      - **8단계: 최종 비교 및 오류 카운트**  
        - Logic 결과(`outa`) vs Reference 결과(`outm`)  
        - 오차가 ±16 초과하면 `err`와 `err2` 1씩 증가  
        - 2가지 루프 수행:  
            - `input_ppg.txt`의 30개 테스트 벡터  
            - `multiplier`, `multiplicand`의 모든 조합(256x256=65536개)  
      - **최종 출력**  
        - `err` & `err2` : 최종 adder까지 포함한 전체 경로의 정확도 오류 개수  
        - 전체 모듈의 누적 합산 정확도를 검증  

&nbsp;

## 1. Progress so far    

&nbsp;

<div align="left">
  <img src="/assets/images/npu/10.jpg" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **현재 설계 진행도**    
  - 현재까지 Process Element의 Multiplier에서 Final Adder까지 Convoulution 연산 모듈 전부 설계 완료   
  - **Multiplier** 
    - Feature map과 weight data를 받아 Partial Product를 출력    
  - **Adder Tree Stage1**   
    - Partial Product와 bias를 받아 합산, 1st Partial Sum을 출력      
  - **Adder Tree Stage2**   
    - Stage1의 Partial Sum을 받아 합산, 2nd Partial Sum을 출력    
    - 최종 합산 값을 Clipping 처리한 data를 받아, 누적 합산을 진행      
  - **Adder Tree Stage3**   
    - Stage2의 Partial Sum을 받아 합산, 3rd Partial Sum을 출력    
  - **Final Adder**    
    - Stage3의 Partial Sum을 받아 14bit data로 변환    
    - 정보의 Clipping이 요구되는 경우, Set Bound를 거쳐 데이터 처리 → Stage2로 Feedback   

&nbsp;
