---
layout : single
title: "[Verilog] CLA Adder + Fianl Adder (w/Testbench)"
categories: 
  - Universal NPU-CNN Accelarator
toc: true
toc_sticky: true
use_math: true
---

Critical Path를 단축하기 위한 CLA Adder와 Partial Sum을 최종적으로 합산하는 adder 모듈 구현    

## 0. 2bit CLA Generator       

```verilog
// 2bit Carry Look Ahead Adder     
// Calculate fast carry propagation for 2bit addition     
module CLG2(x1, y1, x2, y2, c1,
            s1, s2, c3);

    input x1, y1, x2, y2, c1;   // two 2bit numbers & carry-in
    wire p1, c2, p2, g1, g2;    
    output s1, s2, c3;          // two sum bits & carry out

    // Carry Propagation (p)
    assign p1 = x1 ^ y1;        
    assign p2 = x2 ^ y2;
    // Carry Generator (g)
    assign g1 = x1 & y1;
    assign g2 = x2 & y2;

    // Calculate carry bits
    assign c2 = g1 | (p1 & c1);
    assign c3 = g2 | (p2 & g1) | (p2 & p1 & c1);

    // Calculate sum bits
    assign s1 = c1 ^ p1;
    assign s2 = c2 ^ p2;

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/80.png" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **CLG2 (2bit Carry Look Ahead Generator)**
  - 2개의 비트쌍(`x1/y1`, `x2/y2`)을 입력으로 받아 빠른 캐리 전파(Carry Propagation)를 수행하는 2비트 CLA Adder 모듈
  - 두 자리 이진수 덧셈에서 캐리 비트를 빠르게 계산하여 덧셈 속도를 향상시킴

  - **데이터 처리 과정**
    - **1단계: 입력 및 기본 신호 생성**
      - 입력 비트:
        - 첫번째 자리: `x1`, `y1`
        - 두번째 자리: `x2`, `y2`
        - 캐리 입력: `c1`
      - 각 자리의 Propagate(p)와 Generate(g) 신호를 계산
        - Propagate: `p1 = x1 ^ y1`, `p2 = x2 ^ y2`
        - Generate: `g1 = x1 & y1`, `g2 = x2 & y2`

    - **2단계: 캐리 비트 계산**
      - 첫번째 자리의 캐리 출력: `c2 = g1 | (p1 & c1)`
      - 두번째 자리의 최종 캐리 출력: `c3 = g2 | (p2 & g1) | (p2 & p1 & c1)`
      - 이로써 각각의 자리에서 발생하는 캐리 비트를 동시에 계산

    - **3단계: 합(sum) 비트 계산**
      - 첫번째 자리의 합: `s1 = c1 ^ p1`
      - 두번째 자리의 합: `s2 = c2 ^ p2`

    - **최종 출력**
      - 첫번째 자리 합: `s1`
      - 두번째 자리 합: `s2`
      - 최종 캐리 출력: `c3`


&nbsp;

## 1. 4bit CLA Generator   

```verilog
// 4bit Carry Look Ahead Adder
// Calculate fast carry propagation for 4bit addition
module CLG4(x1, y1, x2, y2, x3, y3, x4, y4, c1,
            s1, s2, s3, s4, c5);

    input x1, y1, x2, y2, x3, y3, x4, y4, c1;           // two 4bit numbers & carry-in
    wire p1, p2, p3, p4, g1, g2, g3, g4, c2, c3, c4;  
    output s1, s2, s3, s4, c5;                          // four sum bits & carry-out

    // Carry Propagation (p)
    assign p1 = x1 ^ y1;
    assign p2 = x2 ^ y2;
    assign p3 = x3 ^ y3;
    assign p4 = x4 ^ y4;
    // Carry Generator (g)
    assign g1 = x1 & y1;
    assign g2 = x2 & y2;
    assign g3 = x3 & y3;
    assign g4 = x4 & y4;

    // Calculate carry bits for each stage
    assign c2 = g1 | (p1 & c1);
    assign c3 = g2 | (p2 & g1) | (p2 & p1 & c1);
    assign c4 = g3 | (p3 & g2) | (p3 & p2 & g1) | (p3 & p2 & p1 & c1);
    assign c5 = g4 | (p4 & g3) | (p4 & p3 & g2) | (p4 & p3 & p2 & g1) | (p4 & p3 & p2 & p1 & c1);

    // Calculate sum bits
    assign s1 = c1 ^ p1;
    assign s2 = c2 ^ p2;
    assign s3 = c3 ^ p3;
    assign s4 = c4 ^ p4;

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/81.png" width="80%" height="80%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **CLG4 (4bit Carry Look Ahead Generator)**
  - 4비트 이진 덧셈에서 캐리 비트를 빠르게 계산하기 위한 Carry Look Ahead Generator
  - 4자리의 이진수 덧셈을 병렬적으로 빠르게 처리하기 위한 모듈   

  - **데이터 처리 과정**
    - **1단계: 입력 및 Propagate/Generate 신호 생성**
      - 입력 비트:
        - 각 자리의 입력: `x1/y1`, `x2/y2`, `x3/y3`, `x4/y4`
        - 캐리 입력: `c1`
      - 각 자리의 Propagate 신호:
        - `p1 = x1 ^ y1`
        - `p2 = x2 ^ y2`
        - `p3 = x3 ^ y3`
        - `p4 = x4 ^ y4`
      - 각 자리의 Generate 신호:
        - `g1 = x1 & y1`
        - `g2 = x2 & y2`
        - `g3 = x3 & y3`
        - `g4 = x4 & y4`

    - **2단계: 각 자리의 캐리 비트 계산**
      - 첫번째 자리 캐리 출력: `c2 = g1 | (p1 & c1)`
      - 두번째 자리 캐리 출력: `c3 = g2 | (p2 & g1) | (p2 & p1 & c1)`
      - 세번째 자리 캐리 출력: `c4 = g3 | (p3 & g2) | (p3 & p2 & g1) | (p3 & p2 & p1 & c1)`
      - 네번째 자리 캐리 출력:  
        `c5 = g4 | (p4 & g3) | (p4 & p3 & g2) | (p4 & p3 & p2 & g1) | (p4 & p3 & p2 & p1 & c1)`

    - **3단계: 합(sum) 비트 계산**
      - 각 자리의 합:
        - `s1 = c1 ^ p1`
        - `s2 = c2 ^ p2`
        - `s3 = c3 ^ p3`
        - `s4 = c4 ^ p4`

    - **최종 출력**
      - 각 자리의 합 비트: `s1`, `s2`, `s3`, `s4`
      - 최종 캐리 출력 비트: `c5`

&nbsp;

## 2. 4bit CLA Generator without carry-out   

```verilog
// 4bit Carry Look Ahead Adder variant (No carry-out output)
// Same as CLG4 but omit the carry-out signal
module CLG4_3(x1, y1, x2, y2, x3, y3, x4, y4, c1,
            s1, s2, s3, s4);

    input x1, y1, x2, y2, x3, y3, x4, y4, c1;           // two 4bit numbers & carry-out
    wire p1, p2, p3, p4, g1, g2, g3, g4, c2, c3, c4;
    output s1, s2, s3, s4;                              // 4 sum bits

    // Carry Propagation (p)
    assign p1 = x1 ^ y1;
    assign p2 = x2 ^ y2;
    assign p3 = x3 ^ y3;
    assign p4 = x4 ^ y4;
    // Carry Generator (g)
    assign g1 = x1 & y1;
    assign g2 = x2 & y2;
    assign g3 = x3 & y3;

    // Calculate carry bits for each stage
    assign c2 = g1 | (p1 & c1);
    assign c3 = g2 | (p2 & g1) | (p2 & p1 & c1);
    assign c4 = g3 | (p3 & g2) | (p3 & p2 & g1) | (p3 & p2 & p1 & c1);

    // Calculate sum bits
    assign s1 = c1 ^ p1;
    assign s2 = c2 ^ p2;
    assign s3 = c3 ^ p3;
    assign s4 = c4 ^ p4;

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/82.png" width="80%" height="80%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **CLG4_3 (4bit Carry Look Ahead Generator for 3-bit carry output only)**  
  - 4비트 이진 덧셈에서 Carry Lookahead 방식으로 캐리를 빠르게 계산하는 하드웨어 블록  
  - 최종 carry 출력 비트 `c5`를 제외한 3비트 캐리만 계산하는 구조  
  - 일반적인 CLG4보다 출력 구조가 간소화되어, 일부 캐리 출력만 필요한 경우 사용  

  - **데이터 처리 과정**  
    - **1단계: 입력 및 Propagate/Generate 신호 생성**  
      - 입력 비트:  
        - 각 자리의 입력: `x1/y1`, `x2/y2`, `x3/y3`, `x4/y4`  
        - 캐리 입력: `c1`  
      - 각 자리의 Propagate 신호:  
        - `p1 = x1 ^ y1`  
        - `p2 = x2 ^ y2`  
        - `p3 = x3 ^ y3`  
        - `p4 = x4 ^ y4`  
      - 각 자리의 Generate 신호:  
        - `g1 = x1 & y1`  
        - `g2 = x2 & y2`  
        - `g3 = x3 & y3`  
        - (참고: `g4`는 계산하지 않음)  

    - **2단계: 각 자리의 캐리 비트 계산**  
      - 첫번째 자리 캐리 출력:  
        - `c2 = g1 | (p1 & c1)`  
      - 두번째 자리 캐리 출력:  
        - `c3 = g2 | (p2 & g1) | (p2 & p1 & c1)`  
      - 세번째 자리 캐리 출력:  
        - `c4 = g3 | (p3 & g2) | (p3 & p2 & g1) | (p3 & p2 & p1 & c1)`  
      - 참고: 최종 캐리 `c5`는 계산하지 않음    

    - **3단계: 합(sum) 비트 계산**  
      - 각 자리의 합:  
        - `s1 = c1 ^ p1`  
        - `s2 = c2 ^ p2`  
        - `s3 = c3 ^ p3`  
        - `s4 = c4 ^ p4`  

    - **최종 출력**  
      - 합 비트: `s1`, `s2`, `s3`, `s4`  
      - 캐리 출력 `c5`는 출력하지 않음   

&nbsp;

## 3. Final Adder    

```verilog
// Fianl adder module based on CLA Generators
// Take in multiple pairs of 2bit inputs from previous adder stages
// Combine them to produce the final 14bit output    
module adder_final(i19, i18, i17, i16, i15, i14, i13, i12, i11, i10, i9, i8, i7, i6,
                    out);

    // 2bits wide-input : partial sums from the previous adder stage
    input [1:0] i19, i18, i17, i16, i15, i14, i13, i12, i11, i10, i9, i8, i7, i6;
    wire c0, c1, c2, c3, c4;
    output [13:0] out;  // Final output sum (14bit)

    // Sum i6, producing output bit [0] & carry(c0)
    HA HA0(i6[0], i6[1], out[0], c0);

    // Sum i7 with carry-in c0, producing output bit [1] & carry(c1)
    FA FA0(i7[0], i7[1], c0, out[1], c1);

    // Sum i8 ~ i9 with carry-in c1, producing output bits [2:3] & carry(c2)
    CLG2 C0(i8[0], i8[1], i9[0], i9[1], c1, out[2], out[3], c2);

    // Sum i10 ~ i11 with carry-in c2, producing output bits [4:5] & carry(c3)
    CLG2 C1(i10[0], i10[1], i11[0], i11[1], c2, out[4], out[5], c3);

    // Sum i12 ~ i15 with carry-in c3, producing output bits [6:9] & carry(c4)
    CLG4 C2(i12[0], i12[1], i13[0], i13[1], i14[0], i14[1], i15[0], i15[1], c3, out[6], out[7], out[8], out[9], c4);

    // Sum i16 ~ i19 with carry-in c4, producing output bits [10:13]
    CLG4_3 C3(i16[0], i16[1], i17[0], i17[1], i18[0], i18[1], i19[0], i19[1], c4, out[10], out[11], out[12], out[13]);

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/83.png" width="80%" height="80%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **adder_final**
  - 여러 개의 carry-save 형태의 partial sum 결과들을 빠르게 최종 합산하는 모듈
  - Carry Look Ahead 구조(CL2, CL4, CL4_3)와 Full Adder, Half Adder를 계층적으로 사용해 최종 14비트 결과를 출력
  - 최종적으로 14비트 결과 (`out`)를 생성하여 출력

  - **데이터 처리 과정**
    - **1단계: 입력**
      - 입력: 2비트 쌍 형태의 partial sum 입력 14개 (`i6 ~ i19`)
      - 각 입력은 이전 adder tree stage에서 carry-save 형태로 생성된 partial sum
    
    - **2단계: 최종 덧셈 단계**
      - 작은 그룹별로 Carry Look Ahead Adder (CLG2, CLG4, CLG4_3) 모듈로 처리:
        - `CLG4_3`: i16, i17, i18, i19를 처리하여 out[10:13] 생성
        - `CLG4`: i12, i13, i14, i15를 처리하여 out[6:9] 생성
        - `CLG2`: i10, i11을 처리하여 out[4:5] 생성
        - `CLG2`: i8, i9를 처리하여 out[2:3] 생성
      - 마지막으로 Full Adder와 Half Adder를 사용:
        - `FA`: i7과 carry를 더해 out[1] 생성
        - `HA`: i6을 더해 out[0] 생성

    - **3단계: carry 전파 및 최종 결과 출력**
      - 각 adder의 carry 출력은 상위 adder의 입력으로 연결
      - carry 전파가 빠른 Carry Look Ahead 구조로 최종 carry 전파를 최소화
      - 최종적으로 14비트 결과 `out`을 빠르게 생성

    - **4단계: 최종 출력**
      - `out[13:0]`: 최종 14비트 결과값을 출력   

&nbsp;


## 4. Final Adder Testbench    

```verilog
`timescale 1ns/10ps

// Final Adder Testbench
module tb_adderfinal;

	reg clk, reset;

    // 2bit carry-save input vectors for adder_final input
    wire [1:0] x0, x1, x2, x3, x4, x5, x6, x7, x8, x9, x10, x11, x12, x13;
    // 14bit output of adder_final module & expected output
    wire [13:0] out, p_out;
	
	// Instantiate the adder_final module
    adder_final A0(x0, x1, x2, x3, x4, x5, x6, x7, x8, x9, x10, x11, x12, x13,
                    out);

    // 14bit input operands for testing
    reg [13:0] in0, in1;

    // assign the LSB of carry-save inputs to in0
    assign {x0[0], x1[0], x2[0], x3[0], x4[0], x5[0], x6[0], x7[0], x8[0], x9[0], x10[0], x11[0], x12[0], x13[0]} = in0;
    // assign the MSB of carry-save inputs to in1
    assign {x0[1], x1[1], x2[1], x3[1], x4[1], x5[1], x6[1], x7[1], x8[1], x9[1], x10[1], x11[1], x12[1], x13[1]} = in1;

    // Compute the expected output   
    wire [14:0] outb;           // 15bit to capture possible carry-out
    assign outb = in0 + in1;    
    assign p_out = outb[13:0];  // Expected 14bit sum

    integer err = 0, i = 0, j = 0;

	initial
	begin
    // Initialize onputs
    in0 = 14'b00_0000_0000_0000;
    in1 = 14'b00_0000_0000_0000;		
    #10
            // Test loop : 14bit input combinations
			for (i=0; i<16384; i=i+1)
			begin
                in0 = in0 + 1;
                for (j=0; j<16384; j=j+1)
                begin
                    in1 = in1 + 1;
                    #(1);
                    if (out != p_out) err = err + 1;
                    #(9);
                end
			end
		end

endmodule
```

<div align="left">
    <strong>Simulaton : 16384x16384 combination cases</strong>
  <img src="/assets/images/npu/85.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
  <img src="/assets/images/npu/86.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulaton : Full View</strong>
  <img src="/assets/images/npu/87.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 


- **tb_adderfinal (Testbench for adder_final)**  
  - `adder_final` 모듈의 최종 동작을 검증하기 위한 테스트벤치  
  - carry-save 형태의 2비트 입력 벡터들을 모두 합산하는 동작을 확인  
  - `adder_final`의 출력이 실제로 기대값과 일치하는지 검증  

  - **데이터 처리 과정**  
    - **1단계: 입력 및 초기화**  
      - 14비트 signed 입력값: `in0`, `in1`  
      - 각 14비트 입력의 각 비트를 2비트 carry-save 입력으로 분해  
          - LSB 비트 → `{x0[0], x1[0], ..., x13[0]} = in0`  
          - MSB 비트 → `{x0[1], x1[1], ..., x13[1]} = in1`  
      - Clock (`clk`) 생성: 10ns 주기 (`always #5 clk=~clk`)

    - **2단계: adder_final 모듈과 연결**  
      - `adder_final`은 2비트 carry-save 형태의 입력 14쌍을 받아서  
          - 최종적으로 14비트 `out` 결과를 출력  
      - 입력: `x0 ~ x13`  
      - 출력: `out[13:0]`

    - **3단계: 참조 결과 생성 (Expected Result)**  
      - 참조값: `p_out`  
          - `+` 연산자로 직접 덧셈 수행  
          - 15비트로 연산 (`outb`) 후, 하위 14비트만 `p_out`로 사용  
      - 실제 합산 결과(`out`)와 참조값(`p_out`) 비교

    - **4단계: 첫 번째 테스트 루프 (Exhaustive Test)**  
      - `in0`와 `in1`의 모든 14비트 조합 (32,768 × 32,768 케이스)  
      - 각 입력 조합마다:  
          - `in0`과 `in1`을 1씩 증가  
          - `adder_final` 결과(`out`) vs 참조값(`p_out`) 비교  
          - 결과가 불일치하면 → 오류 카운터(`err`) 증가  
          - 10ns씩 지연

    - **5단계: 최종 출력**  
      - `err`: 입력값 조합에 따른 오차 발생 횟수  
      - 모든 결과를 통해 `adder_final`의 최종 carry-save to binary 변환 정확성을 검증  

&nbsp;





