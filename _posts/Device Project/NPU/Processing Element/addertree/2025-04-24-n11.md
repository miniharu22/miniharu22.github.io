---
layout : single
title: "[Verilog] Adder Tree Stage #2 + Set Bound Path (w/Testbench)"
categories: 
  - Universal NPU-CNN Accelarator
toc: true
toc_sticky: true
use_math: true
---

두번째 Adder Tree Stage 설계 + Set bound를 통과한 pre-output 합산 기능 구현    

## 0. Adder Tree Stage2 module     

```verilog
// Adder Tree Stage 2 Module
// Combine the intermediate partial sums & previous output(clipped data) for next stage
module addertree_stage2(x18, x17, x16, x15, x14, x13, x12, x11, x10, x9, x8, x7, x6, x5, x4, x3,
                        pre_output,
                        o19, o18, o17, o16, o15, o14, o13, o12, o11, o10, o9, o8, o7, o6, o5, o4, o3
                        );

    // Partial sums from previous stage
    input x18;
    input [1:0] x17;
    input [2:0] x16;
    input [4:0] x15, x14;
    input [6:0] x13;
    input [8:0] x12;
    input [9:0] x11;
    input [10:0] x10;
    input [11:0] x9;
    input [10:0] x8;
    input [9:0] x7;
    input [9:0] x6;
    input [8:0] x5;
    input [7:0] x4;
    input [5:0] x3;

    // Clipped data from previous cycle    
    input [13:1] pre_output;

    // Output of 2nd stage
    wire p19;   // Carry out bit from x18 addition
    output o19;
    output [2:0] o18, o17;
    output [3:0] o16, o15, o14, o13, o12, o11, o10, o9, o8, o7;
    output [2:0] o6, o5;
    output [1:0] o4;
    output o3;

    // Half Adder to compute carry out for x18 additon with 
    assign o19 = ~(p19 ^ pre_output[13]);
    HA A180(x18, pre_output[13], o18[0], p19);

    // Full Adder for x17 inputs & previous data
    FA A170(x17[0], x17[1], pre_output[12], o17[0], o18[1]);

    // 4-to-3 adder for x16 & previous data
    adder_4to3 A160({x16, pre_output[11]}, o18[2], o17[1], o16[0]);

    // 6-to-3 adder for x15 & previous data
    adder_6to3 A150({x15, pre_output[10]}, o17[2], o16[1], o15[0]);

    // 6-to-3 adder for x14 & previous data
    adder_6to3 A140({x14, pre_output[9]}, o16[2], o15[1], o14[0]);

    // 8-to-4 adder for x13 & previous data
    adder_8to4 A130({x13, pre_output[8]}, o16[3], o15[2], o14[1], o13[0]);

    // 10-to-4 adder for x12 & previous data
    adder_10to4 A120({x12, pre_output[7]}, o15[3], o14[2], o13[1], o12[0]);

    // 11-to-4 adder for x11 & previous data
    adder_11to4 A110({x11, pre_output[6]}, o14[3], o13[2], o12[1], o11[0]);

    // 12-to-4 adder for x10 & previous data
    adder_12to4 A100({x10, pre_output[5]}, o13[3], o12[2], o11[1], o10[0]);

    // 13-to-4 adder for x9 & previous data
    adder_13to4 A90({x9, pre_output[4]}, o12[3], o11[2], o10[1], o9[0]);

    // 12-to-4 adder for x8 & previous data
    adder_12to4 A80({x8, pre_output[3]}, o11[3], o10[2], o9[1], o8[0]);

    // 11-to-4 adder for x7 & previous data
    adder_11to4 A70({x7, pre_output[2]}, o10[3], o9[2], o8[1], o7[0]);

    // 11-to-4 adder for x6 & previous data
    adder_11to4 A60({x6, pre_output[1]}, o9[3], o8[2], o7[1], o6[0]);

    // 9-to-4 adder for x5 
    adder_9to4 A50(x5, o8[3], o7[2], o6[1], o5[0]);

    // 8-to-4 adder for x4
    adder_8to4 A40(x4, o7[3], o6[2], o5[1], o4[0]);

    // 6-to-4 adder for x3
    adder_6to3 A30(x3, o5[2], o4[1], o3);                                                                                                                                       
endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/68.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **addertree_stage2**
  - addertree_stage1에서 출력된 partial sum과 이전 결과(`pre_output`)를 받아 추가적인 덧셈 과정을 통해 누적 합산을 수행하는 두 번째 adder tree stage 모듈  
  - `pre_output` : 이전 사이클에서 합산이 완료된 output을 Set Bound를 통해 클리핑, Feedback path를 통해 다시 누적 합산을 수행    
  - CNN 가속기에서 여러 채널 및 입력을 병렬로 처리하며, pipelining된 누적 합산 과정을 구현  

  - **데이터 처리 과정**
    - **1단계: 입력 및 이전 출력 결합**
      - 입력:  
        - partial sum 입력: `x3 ~ x18` (비트폭 1bit ~ 12bit)  
        - 이전 cycle의 누적 출력: `pre_output[13:1]`  
      - 각 입력은 적절한 adder 모듈과 결합됨  
      - 가장 상위 carry/bit 보정은 별도의 처리 (`o19`, `p19`)  

    - **2단계: Carry 처리**
      - `x18`과 `pre_output[13]`를 Half Adder로 계산  
        - carry-out: `p19`  
        - sum: `o18[0]`  
      - `o19`는 carry 보정 비트의 NOT XOR 처리로 출력 (`o19 = ~(p19 ^ pre_output[13])`)

    - **3단계: 순차적인 adder 처리**
      - 입력 그룹과 `pre_output`을 순차적으로 Full Adder 및 다중 입력 Adder로 처리  
      - 주요 adder 모듈 예시: 
        - `HA`, `FA`, `adder_4to3`, `adder_6to3`, `adder_8to4`, `adder_10to4`, `adder_11to4`, `adder_12to4`, `adder_13to4`, `adder_9to4`  
      - 각 단계의 결과는 다음 단계의 입력으로 전달됨 (Carry와 Sum으로 출력)

    - **4단계: 누적 partial sum 출력**
      - 단계별로 생성된 partial sum은  
        - `o18`, `o17`, `o16`, …, `o3`로 출력됨  
      - 출력 비트폭과 개수는 입력 및 adder 처리 결과에 따라 다름  
      - 최종적으로 `o19`~`o3`까지의 출력이 stage2의 누적 partial sum으로 제공됨

    - **5단계: 다음 누적 처리로 전달**
      - stage2에서 출력된 partial sum (`o19`~`o3`)은  
        - 다음 stage의 adder tree stage로 전달  
      - 이를 통해 CNN 연산의 channel 방향 누적 합산이 pipelining 방식으로 진행됨    


&nbsp;

## 1. Adder Tree Stage2 Testbench    

```verilog
`timescale 1ns/10ps

// Testbench for addertree stage1 & stage2
module tb_addertree2;

	reg clk, reset;
    wire [8*9-1:0] multiplicand9, multiplier9;
    reg signed [7:0] multiplicand, multiplier;
    reg [15:0] mat_in[0:140];
    reg signed [15:0] bias;

    // Output from multiplier
    wire [8:0] O14, O13;
    wire [17:0] O12, O11;
    wire [35:0] O10, O9, O8, O7;
    wire [44:0] O6;
    wire [26:0] O5;
    wire [35:0] O4;
    wire [17:0] O3;
    wire [26:0] O2;
    wire [8:0] O1;
    wire [17:0] O0;

    // Output from addertree_stage1
    wire ao18;
    wire [1:0] ao17;
    wire [2:0] ao16;
    wire [4:0] ao15, ao14;
    wire [6:0] ao13;
    wire [8:0] ao12;
    wire [9:0] ao11;
    wire [10:0] ao10;
    wire [11:0] ao9;
    wire [10:0] ao8;
    wire [9:0] ao7;
    wire [9:0] ao6;
    wire [8:0] ao5;
    wire [7:0] ao4;
    wire [5:0] ao3;

    // Output from addertree_stage2
    wire o19;
    wire [2:0] o18, o17;
    wire [3:0] o16, o15, o14, o13, o12, o11, o10, o9, o8, o7;
    wire [2:0] o6, o5;
    wire [1:0] o4;
    wire o3;

    // Feedback register for accumulation with clipped data (previous results)
    reg signed [13:1] pre_output, pre_outputm;

    assign multiplier9 = {9{multiplier}};
    assign multiplicand9 = {9{multiplicand}};

    multiplier M0(multiplicand9, multiplier9,
                        O14, O13, O12, O11, O10, O9, O8, O7, O6, O5, O4, O3, O2, O1, O0);

    addertree_stage1 AT10(O14, O13, O12, O11, O10, O9, O8, O7, O6, O5, O4, O3, O2, O1, O0, 
                         bias,
                         ao18, ao17, ao16, ao15, ao14, ao13, ao12, ao11, ao10, ao9, ao8, ao7, ao6, ao5, ao4, ao3);

    addertree_stage2 AT20(ao18, ao17, ao16, ao15, ao14, ao13, ao12, ao11, ao10, ao9, ao8, ao7, ao6, ao5, ao4, ao3,
                        pre_output,
                        o19, o18, o17, o16, o15, o14, o13, o12, o11, o10, o9, o8, o7, o6, o5, o4, o3
                        );

    // Compute the sum of bits in each addertree output using function
    wire [5:0] aao[19:3];

    assign aao[19] = addall(o19);
    assign aao[18] = addall(o18);
    assign aao[17] = addall(o17);
    assign aao[16] = addall(o16);
    assign aao[15] = addall(o15);
    assign aao[14] = addall(o14);
    assign aao[13] = addall(o13);
    assign aao[12] = addall(o12);
    assign aao[11] = addall(o11);
    assign aao[10] = addall(o10);
    assign aao[9] = addall(o9);
    assign aao[8] = addall(o8);
    assign aao[7] = addall(o7);
    assign aao[6] = addall(o6);
    assign aao[5] = addall(o5);
    assign aao[4] = addall(o4);
    assign aao[3] = addall(o3);

	initial
	begin
		clk = 1;
        multiplier = 8'b0000_0000;
        multiplicand = 8'b0000_0000;
        pre_output = 0;
        pre_outputm = 0;
	end
	
	always #5 clk = ~clk;


    // Function to add bits in an input vector
    function [5:0] addall;
    input [11:0] in;
    begin
        addall = in[0] + in[1] + in[2] + 
              in[3] + in[4] + in[5] + 
              in[6] + in[7] + in[8] +
              in[9] + in[10] + in[11] ;
    end
    endfunction

    // Calculate final results : Direct Calculation vs Addertree Stages Output
    wire signed [19:0] O9m, Oao;
    assign O9m = multiplier * multiplicand * 9 + bias + (pre_outputm<<6);
    assign Oao = (aao[19]<<19) + (aao[18]<<18) +(aao[17]<<17) +(aao[16]<<16) +(aao[15]<<15) +
                    (aao[14]<<14) +(aao[13]<<13) +(aao[12]<<12) +(aao[11]<<11) +
                    (aao[10]<<10) +(aao[9]<<9) +(aao[8]<<8) +(aao[7]<<7) +
                    (aao[6]<<6) +(aao[5]<<5) +(aao[4]<<4) +(aao[3]<<3);

    // Clipping for the reference data 
    reg signed [13:1] outm;
    always @(*) begin
        if (O9m > 20'sd262143) outm = 13'sd4095;
        else if (O9m < -20'sd262144) outm = -13'sd4096;
        else outm = O9m[18-:13];
    end

    // Clipping for the addertree stage output
    wire signed [13:1] outa;
    assign outa = Oao[19-:2] == 2'b01 ? 'b0_1111_1111_1111 : Oao[19-:2] == 2'b10 ? 'b1_0000_0000_0000 : Oao[18-:13];

    // Set Bound : Set pre-output values to addertree_stage2 
    always begin #10 pre_output = outa; #10 pre_output = outa; #10 pre_output = outa; #10 pre_output = 0; end
    always begin #10 pre_outputm = outm; #10 pre_outputm = outm; #10 pre_outputm = outm; #10 pre_outputm = 0; end


    integer err = 0, i = 0, j = 0, err2 = 0;
    initial
	begin
        bias = 16'b0000_0000_0000_0000;		
		$readmemh("C://MY/Vivado/UniNPU/UniNPU.srcs/data/input_ppg.txt", mat_in);
        #20
		begin
            // Test cases with randomly 30 vectors
			for (i=0; i<30; i=i+1)
			begin
				{multiplier, multiplicand} = mat_in[i];
                #(1);
                if (outa - outm > 'sd16 | outa - outm > 'sd16) err = err + 1;
				#(9);
			end
		end

        // test all 8bit input combinations (256x256)
        i = 0;
        multiplicand = 8'b1111_1111;
        multiplier = 8'b1111_1111;

        begin
			for (i=0; i<256; i=i+1)
			begin
                multiplier = multiplier + 1;
                for (j=0; j<256; j=j+1)
                begin
                    multiplicand = multiplicand + 1;
                    bias = bias + 5;
                    #(1);
                    if (outa - outm > 'sd16 | outa - outm > 'sd16) err2 = err2 + 1;
                    #(9);
                end
			end
		end

	end

endmodule
```

<div align="left">
    <strong>Simulaton : Multiplication</strong>
  <img src="/assets/images/npu/69.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulaton : Adder Tree Stage1</strong>
  <img src="/assets/images/npu/70.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulaton : Adder Tree Stage2</strong>
  <img src="/assets/images/npu/71.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulaton : Randomly 30 Case </strong>
    <strong>Direct Calculation vs Addertree Stages Output</strong>
  <img src="/assets/images/npu/72.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulaton : 256x256 Case Test</strong>
  <img src="/assets/images/npu/73.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
  <img src="/assets/images/npu/74.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 


- **tb_addertree2 (Testbench for addertree_stage2)**  
  - `addertree_stage2` 모듈의 동작을 검증하기 위한 테스트벤치  
  - Multiplier의 Partial Product를 `addertree_stage1`에서 처리 후, 다시 `addertree_stage2`를 통해 Partial Sum을 출력     
  - 출력된 결과 data는 Set bound, 즉 클리핑 처리를 진행, 다시 `addertree_stage2`에 입력하여 누적 합산을 진행    
  - 최종적으로 `addertree_stage2`의 결과와 기대 결과의 일치 여부를 확인  

  - **데이터 처리 과정**  
    - **1단계: 입력 및 초기화**  
      - 8비트 signed 입력: `multiplicand`, `multiplier`  
      - 파일(`input_ppg.txt`)에서 16비트 데이터 읽어와 `mat_in` 배열(141개) 저장  
      - `bias`(16비트 signed) 및 `pre_output`, `pre_outputm`(13비트 signed)도 함께 사용  
      - Clock (`clk`) 생성: 10ns 주기 (`always #5 clk=~clk`)  

    - **2단계: Multiplier & Addertree 연결**  
      - `multiplier` 모듈: 8비트 입력을 72비트로 확장 (9개씩 복제)  
      - `addertree_stage1` 모듈: Multiplier에서 나온 Partial Product들을 받아 첫 번째 Partial Sum 출력  
      - `addertree_stage2` 모듈: `addertree_stage1`에서 출력된 Partial Sum과 `pre_output`을 받아 최종 Partial Sum 출력  

    - **3단계: Partial Sum 비트 합산**  
      - `addertree_stage2`의 최종 Partial Sum (`o19~o3`)의 각 비트를 `addall` 함수로 합산  
      - 결과를 `aao` 배열에 저장  
        - 예: `aao[19] = addall(o19)`, `aao[3] = addall(o3)` 등  

    - **4단계: 최종 결과 계산**  
      - `Oao`: `aao`의 각 요소를 해당 비트 위치만큼 시프트 후 모두 더해서 최종 결과 생성  
      - 수식: Oao = (aao[19]<<19) + (aao[18]<<18) + ... + (aao[3]<<3)  
      - `O9m`: 참조값으로, 아래와 같이 계산  
        - `O9m = multiplier * multiplicand * 9 + bias + (pre_outputm<<6)`  
        - pre_outputm은 이전 cycle에서 나온 출력으로 누적값 형태로 더해줌  

    - **5단계: Clipping**  
      - 참조 출력 (`O9m`)과 addertree 출력 (`Oao`)를 13비트 signed로 Clipping 처리  
        - `O9m` → `outm`  
        - `Oao` → `outa`  
        - 결과값의 MSB 두 비트를 통해 Clipping 처리 (Overflow 방지)  

    - **6단계: pre_output 업데이트**  
      - `outa` 및 `outm`을 3번 반복해서 `pre_output`, `pre_outputm`으로 업데이트  
      - 마지막 cycle에는 0으로 초기화   

    - **7단계: 첫 번째 테스트 루프 (파일 기반)**  
      - `input_ppg.txt`의 앞 30개의 테스트 벡터를 입력  
      - 각 입력에 대해:  
        - `outa`와 `outm`의 차이가 16을 초과할 경우 → `err`를 1씩 증가  
      - 각 테스트마다 10ns씩 지연  

    - **8단계: 두 번째 테스트 루프 (exhaustive 테스트)**  
      - `multiplier`와 `multiplicand`를 -128~127 범위로 모두 순회 (256×256=65536 케이스)  
      - `bias`는 각 케이스마다 5씩 증가  
      - 각 입력에 대해:  
        - `outa`와 `outm`의 차이가 16을 초과할 경우 → `err2`를 1씩 증가  
      - 각 테스트마다 10ns씩 지연  

    - **최종 출력** 
      - `err` & `err2`: addertree_stage2의 결과와 참조 결과의 차이가 16을 초과한 횟수     
      - 전체적으로 addertree_stage2 모듈의 정확성과 누적 Partial Sum의 신뢰도를 검증      

&nbsp;

## 2. Progress so far    

&nbsp;

<div align="left">
  <img src="/assets/images/npu/8.jpg" width="70%" height="70%" alt=""/>
  <p><em></em></p>
</div>

&nbsp;

- **현재 설계 진행도**    
  - 현재까지 Process Element의 Multiplier와 Adder Tree Stage1 & Stage2 설계 완료    
  - **Multiplier** 
    - Feature map과 weight data를 받아 Partial Product를 출력    
  - **Adder Tree Stage1**   
    - Partial Product와 bias를 받아 합산, 1st Partial Sum을 출력      
  - **Adder Tree Stage2**   
    - Stage1의 Partial Sum을 받아 합산, 2nd Partial Sum을 출력    
    - 최종 합산 값을 Clipping 처리한 data를 받아, 누적 합산을 진행    

&nbsp;


