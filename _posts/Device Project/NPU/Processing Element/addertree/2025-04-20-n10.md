---
layout : single
title: "[Verilog] Adder Tree Stage #1 (w/Testbench)"
categories: 
  - Universal NPU-CNN Accelarator
toc: true
toc_sticky: true
use_math: true
---

Multiplier의 출력값인 Partial Product를 합산하여 1차적으로 Partial Sum을 연산하는 모듈을 설계    

## 0. Adder Tree Stage1 module   

```verilog
// Adder Tree stage1 module
// Receive the partial product outputs (from multiplier) and bias
// Use compressor adder modules to generate partial sums for further accumulation
module addertree_stage1(x14, x13, x12, x11, x10, x9, x8, x7, x6, x5, x4, x3, x2, x1, x0, 
                        bias,
                        o18, o17, o16, o15, o14, o13, o12, o11, o10, o9, o8, o7, o6, o5, o4, o3);

    input[8:0]  x14, x13;
    input[17:0] x12, x11;
    input[35:0] x10, x9, x8, x7;
    input[44:0] x6;
    input[26:0] x5;
    input[35:0] x4;
    input[17:0] x3;
    input[26:0] x2;
    input[8:0]  x1;
    input[17:0] x0;

    input[15:0] bias;

    output        o18;
    output [1:0]  o17;
    output [2:0]  o16;
    output [4:0]  o15, o14;
    output [6:0]  o13;
    output [8:0]  o12;
    output [9:0]  o11;
    output [10:0] o10;
    output [11:0] o9;
    output [10:0] o8;
    output [9:0]  o7;
    output [9:0]  o6;
    output [8:0]  o5;
    output [7:0]  o4;
    output [5:0]  o3;

    wire [3:0] o2;
    wire [1:0] o1;
    wire       o0;

    // Output logic with bias
    assign o18 = ~bias[15];
    assign o17[1] = bias[15];
    assign o16[2] = bias[15];

    // Adder modules for each partial product group
    // x14 & x13 : 11bit to 4bit adder modules with bias inclued
    adder_11to4 A140({x14, bias[14], 1'b1}, o17[0], o16[0], o15[0], o14[0]);
    adder_11to4 A130({x13, bias[13], 1'b1}, o16[1], o15[1], o14[1], o13[0]);

    // x12 : split into 15bit and 4bit segments
    adder_15to4 A120({x12[17:4], bias[12]}, o15[2], o14[2], o13[1], o12[0]);
    adder_4to3 A121(x12[3:0], o14[3], o13[2], o12[1]);

    // Set additional bits for o15
    assign o15[3] = bias[15];
    assign o15[4] = 1'b1;

    // x11: split into 15bit and 5bit segments
    adder_15to4 A110({x11[17:5], bias[11], 1'b1}, o14[4], o13[3], o12[2], o11[0]);
    adder_5to3 A111(x11[4:0], o13[4], o12[3], o11[1]);
    
    // x10: split into three segments - 15, 15, 7
    adder_15to4 A100({x10[35:22], bias[10]}, o13[5], o12[4], o11[2], o10[0]);
    adder_15to4 A101(x10[21:7], o13[6], o12[5], o11[3], o10[1]);
    adder_7to3 A102(x10[6:0], o12[6], o11[4], o10[2]);                                              

    // x9: split into three segments - 15, 15, 7
    adder_15to4 A90({x9[35:22], bias[9]}, o12[7], o11[5], o10[3], o9[0]);
    adder_15to4 A91(x9[21:7], o12[8], o11[6], o10[4], o9[1]);
    adder_7to3 A92(x9[6:0], o11[7], o10[5], o9[2]);                                                                   

    // x8: split into three segments - 15, 15, 7
    adder_15to4 A80({x8[35:22], bias[8]}, o11[8], o10[6], o9[3], o8[0]);
    adder_15to4 A81(x8[21:7], o11[9], o10[7], o9[4], o8[1]);
    adder_7to3 A82(x8[6:0], o10[8], o9[5], o8[2]);                                             

    // x7: split into three segments - 15, 15, 7
    adder_15to4 A70({x7[35:22], bias[7]}, o10[9], o9[6], o8[3], o7[0]);
    adder_15to4 A71(x7[21:7], o10[10], o9[7], o8[4], o7[1]);
    adder_7to3 A72(x7[6:0], o9[8], o8[5], o7[2]);

    // x6: 46bit input split into three 15bit segments
    adder_15to4 A60(x6[44:30], o9[9], o8[6], o7[3], o6[0]);
    adder_15to4 A61(x6[29:15], o9[10], o8[7], o7[4], o6[1]);
    adder_15to4 A62(x6[14:0], o9[11], o8[8], o7[5], o6[2]);

    // x5: 28bit input split into two 14bit segments
    adder_14to4 A50({x5[26:14], bias[5]}, o8[9], o7[6], o6[3], o5[0]);
    adder_14to4 A51(x5[13:0], o8[10], o7[7], o6[4], o5[1]);

    // x4: 37bit input split into three segments - 15, 15, 7
    adder_15to4 A40({x4[35:22], bias[4]}, o7[8], o6[5], o5[2], o4[0]);
    adder_15to4 A41(x4[21:7], o7[9], o6[6], o5[3], o4[1]);
    adder_7to3 A42(x4[6:0], o6[7], o5[4], o4[2]);

    // x3: 19bit input split into 15 and 4
    adder_15to4 A30({x3[17:4], bias[3]}, o6[8], o5[5], o4[3], o3[0]);
    adder_4to3 A31(x3[3:0], o5[6], o4[4], o3[1]);

    // Set o6[9] directly from bias[6]
    assign o6[9] = bias[6];

    // x2: 28bit input split into two 14bit segments
    adder_14to4 A20({x2[26:14], bias[2]}, o5[7], o4[5], o3[2], o2[0]);
    adder_14to4 A21(x2[13:0], o5[8], o4[6], o3[3], o2[1]);

    // x1: 9bit input to adder_10to4
    adder_10to4 A10({x1[8:0], bias[1]}, o4[7], o3[4], o2[2], o1[0]);

    // x0: 19bit input split for final adder stage
    adder_15to4 A00({x0[17:4], bias[0]}, o3[5], o2[3], o1[1], o0);

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/58.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 


- **addertree_stage1**
  - Multiplier에서 생성된 partial product를 입력으로 받아, 여러 개의 작은 adder 모듈을 통해 계층적으로 partial sum을 생성해주는 첫 번째 덧셈 계층 모듈  
  - 하드웨어 연산을 병렬/순차적으로 처리할 수 있도록 입력을 나눠서 효율적으로 합산   
  - 최종적으로 `o18~o3`의 작은 partial sum을 출력하여 다음 adder tree stage로 넘김

  - **데이터 처리 과정**
    - **1단계: 입력 및 bias 결합**
      - 입력:  
        - partial product 입력: `x0 ~ x14` (비트폭 다양: 9bit ~ 45bit)  
        - bias 입력: `bias[15:0]`  
      - 일부 입력 그룹에는 bias를 상위 비트로 추가하여 함께 처리  
        - 예: `{x10[35:22], bias[10]}`  

    - **2단계: 입력 그룹 분할**
      - 비트폭이 큰 입력을 작은 덧셈 그룹으로 나눔  
        - 예:  
          - `x10 (36bit)` → `15bit + 15bit + 7bit`  
          - `x7 (36bit)` → `15bit + 15bit + 7bit`  
        - 일부 입력은 bias 결합 후 처리  
      - 나눠진 그룹은 각기 다른 adder 모듈로 전송됨

    - **3단계: adder 모듈 처리**
      - 작은 그룹을 적절한 adder 모듈로 연결  
        - 예:  
          - `adder_15to4`, `adder_14to4`, `adder_11to4`, `adder_10to4`  
          - `adder_7to3`, `adder_5to3`, `adder_4to3`  
      - 각 adder 모듈은 입력 그룹을 받아 작은 partial sum으로 압축  
      - 일부 bias의 최상위 비트는 출력으로 직접 연결됨  
        - 예:  
          - `o18 = ~bias[15]`  
          - `o17[1] = bias[15]`  
          - `o16[2] = bias[15]`

    - **4단계: partial sum 출력**
      - adder 모듈에서 생성된 작은 partial sum은  
        - `o18 ~ o3`로 출력 (출력 비트폭: 1~12bit)  
      - 출력의 비트폭과 위치는 입력 그룹 및 adder 모듈의 처리 결과에 따라 결정됨

    - **5단계: 다음 stage로 전달**
      - 최종적으로 생성된 partial sum (`o18~o3`)은  
        - 다음 adder tree stage로 입력되어 최종적인 누적 합산 처리 (channel 방향 누적 합산)  

&nbsp;

## 1. Adder Tree Stage1 Testbench (w/multiplier)   

```verilog
`timescale 1ns/10ps

// Testbench for multiplier & addertree stage1 modules
module tb_addertree1;

	reg clk, reset;
    wire [8*9-1:0] multiplicand9, multiplier9;  // 72bit input : 9 copies of 8bit values
    reg signed [7:0] multiplicand, multiplier;  // Single 8bit signed inputs
    reg [15:0] mat_in[0:140];                   // Test vectors from input file
    reg signed [15:0] bias;                     // 16bit signed bias

    // Outputs from the multiplier
    wire [8:0] O14, O13;
    wire [17:0] O12, O11;
    wire [35:0] O10, O9, O8, O7;
    wire [44:0] O6;
    wire [26:0] O5;
    wire [35:0] O4;
    wire [17:0] O3;
    wire [26:0] O2;
    wire [8:0] O1;
    wire [17:0] O0;
    
    // Outputs from the addertree_stage1 
    wire ao18;
    wire [1:0] ao17;
    wire [2:0] ao16;
    wire [4:0] ao15, ao14;
    wire [6:0] ao13;
    wire [8:0] ao12;
    wire [9:0] ao11;
    wire [10:0] ao10;
    wire [11:0] ao9;
    wire [10:0] ao8;
    wire [9:0] ao7;
    wire [9:0] ao6;
    wire [8:0] ao5;
    wire [7:0] ao4;
    wire [5:0] ao3;

    // Replicate 8bit input 9 times for multiplier module 
    assign multiplier9 = {9{multiplier}};
    assign multiplicand9 = {9{multiplicand}};

    // Instantiate the multiplier module
    multiplier M0(multiplicand9, multiplier9,
                        O14, O13, O12, O11, O10, O9, O8, O7, O6, O5, O4, O3, O2, O1, O0);
    
    // Instantiate the addertree_stage1 module
    addertree_stage1 AT10(O14, O13, O12, O11, O10, O9, O8, O7, O6, O5, O4, O3, O2, O1, O0, 
                         bias,
                         ao18, ao17, ao16, ao15, ao14, ao13, ao12, ao11, ao10, ao9, ao8, ao7, ao6, ao5, ao4, ao3);

    // Array to hold the sum of bits of each addertree output
    wire [5:0] aao[18:0];

    // Sum all bits in each addertree output
    assign aao[18] = addall(ao18);
    assign aao[17] = addall(ao17);
    assign aao[16] = addall(ao16);
    assign aao[15] = addall(ao15);
    assign aao[14] = addall(ao14);
    assign aao[13] = addall(ao13);
    assign aao[12] = addall(ao12);
    assign aao[11] = addall(ao11);
    assign aao[10] = addall(ao10);
    assign aao[9] = addall(ao9);
    assign aao[8] = addall(ao8);
    assign aao[7] = addall(ao7);
    assign aao[6] = addall(ao6);
    assign aao[5] = addall(ao5);
    assign aao[4] = addall(ao4);
    assign aao[3] = addall(ao3);

	initial
	begin
		clk = 1;
        multiplier = 8'b0000_0000;
        multiplicand = 8'b0000_0000;
	end
	
	always #5 clk = ~clk;

    // Function to sum the bits of 12bit input
    function [5:0] addall;
    input [11:0] in;
    begin
        addall = in[0] + in[1] + in[2] + 
              in[3] + in[4] + in[5] + 
              in[6] + in[7] + in[8] +
              in[9] + in[10] + in[11] ;
    end
    endfunction

    // Expected and actual outputs for comparison
    wire signed [18:0] O9m, Oao;
    assign O9m = multiplier * multiplicand * 9 + bias;  
    assign Oao = (aao[18]<<18) +(aao[17]<<17) +(aao[16]<<16) +(aao[15]<<15) +
                    (aao[14]<<14) +(aao[13]<<13) +(aao[12]<<12) +(aao[11]<<11) +
                    (aao[10]<<10) +(aao[9]<<9) +(aao[8]<<8) +(aao[7]<<7) +
                    (aao[6]<<6) +(aao[5]<<5) +(aao[4]<<4) +(aao[3]<<3);


    integer err = 0, i = 0, j = 0, err2 = 0;

    initial
	begin
        bias = 16'b0000_0000_0000_0000;		
		$readmemh("C://MY/Vivado/UniNPU/UniNPU.srcs/data/input_ppg.txt", mat_in);
        #10
		begin
            // First loop : 20 test vectors from input_ppg.txt
			for (i=0; i<20; i=i+1)
			begin
				{multiplier, multiplicand} = mat_in[i];
                #(1);
                // If the difference is larger than 32, count as error
                if (Oao - O9m > 'sd32 | O9m - Oao > 'sd32) err2 = err2 + 1;
				#(9);
			end
		end

        // Exhaustive test loop : all 256x256 combinations
        i = 0;
        multiplicand = 8'b1111_1111;
        multiplier = 8'b1111_1111;

        begin
			for (i=0; i<256; i=i+1)
			begin
                multiplier = multiplier + 1;
                for (j=0; j<256; j=j+1)
                begin
                    multiplicand = multiplicand + 1;
                    bias = bias + 5;
                    #(1);
                    if (Oao - O9m > 'sd32 | O9m - Oao > 'sd32) err2 = err2 + 1;
                    #(9);
                end
			end
		end

	end

endmodule
```

<div align="left">
    <strong>Simulaton : Multiplication</strong>
  <img src="/assets/images/npu/61.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulaton : Adder Tree Stage1</strong>
  <img src="/assets/images/npu/62.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulaton : Randomly 20 Case </strong>
    <strong>Direct Calculation vs Addertree Stage Output</strong>
  <img src="/assets/images/npu/63.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulaton : 256x256 Case Test</strong>
  <img src="/assets/images/npu/64.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
  <img src="/assets/images/npu/65.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 


- **tb_addertree1 (Testbench for addertree_stage1)**  
  - `addertree_stage1` 모듈의 동작을 검증하기 위한 테스트벤치  
  - Multiplier의 Partial Product를 adder tree로 합산하여 최종적으로 기대 결과와 비교  
  - 최종적으로 addertree_stage1 모듈의 누적 Partial Sum과 직접 곱셈 결과의 일치 여부를 확인  

  - **데이터 처리 과정**  
    - **1단계: 입력 및 초기화**  
      - 8비트 signed 입력: `multiplicand`, `multiplier`  
      - 파일(`input_ppg.txt`)에서 16비트 데이터 읽어와 `mat_in` 배열(141개) 저장  
      - `bias`(16비트 signed)도 같이 사용  
      - Clock (`clk`) 생성: 10ns 주기 (`always #5 clk=~clk`)  

    - **2단계: Multiplier & Addertree 연결**  
      - `multiplier` 모듈: 8비트 입력을 72비트로 확장 (9개씩 복제)  
      - `addertree_stage1` 모듈: Multiplier에서 나온 Partial Product들을 받아 누적 Partial Sum 출력  
      - Partial Sum 출력: `ao18`, `ao17`, …, `ao3` 등으로 표현  

    - **3단계: Partial Sum 비트 합산**  
      - Partial Sum들의 각 비트를 `addall` 함수로 합산해 `aao` 배열에 저장  
      - 예: `aao[18] = addall(ao18)`, `aao[3] = addall(ao3)` 등  

    - **4단계: 최종 결과 계산**  
      - `Oao`: `aao`의 각 요소를 해당 비트 위치만큼 시프트 후 모두 더해서 최종 결과 생성  
      - 수식: Oao = (aao[18]<<18) + (aao[17]<<17) + ... + (aao[3]<<3)   
        
    - **5단계: 참조 결과 계산 (Expected Result)**  
      - `O9m = multiplier * multiplicand * 9 + bias`  
      - 정확한 곱셈 결과를 바탕으로 adder tree의 결과(`Oao`)를 검증  

    - **6단계: 첫 번째 테스트 루프 (파일 기반)**  
      - `input_ppg.txt`의 앞 20개의 테스트 벡터를 입력  
      - 각 입력에 대해:  
        - `Oao`와 `O9m`의 차이가 32를 초과할 경우 → `err2` 1씩 증가  
      - 각 테스트마다 10ns씩 지연  

    - **7단계: 두 번째 테스트 루프 (exhaustive 테스트)**  
      - `multiplier`와 `multiplicand`를 -128~127 범위로 모두 순회 (256×256=65536 케이스)  
      - `bias`는 각 케이스마다 5씩 증가  
      - 각 입력에 대해:  
        - `Oao`와 `O9m`의 차이가 32를 초과할 경우 → `err2` 1씩 증가  
      - 각 테스트마다 10ns씩 지연  

    - **최종 출력**  
      - `err` & `err2`: addertree_stage1의 결과와 참조 결과의 차이가 32를 초과한 횟수  
      - 전체적으로 addertree_stage1 모듈의 정확성과 누적 Partial Sum의 신뢰도를 검증  

&nbsp;

## 2. Progress so far    

&nbsp;

<div align="center">
  <img src="/assets/images/npu/5.jpg" width="70%" height="70%" alt=""/>
  <p><em>addertree_stage1의 output은 ao18~ao3 (잘못 적음)</em></p>
</div>

&nbsp;

- **현재 설계 진행도**    
  - 현재까지 Process Element의 Multiplier와 Adder Tree Stage1 설계 완료    
  - **Multiplier** 
    - Feature map과 weight data를 받아 Partial Product를 출력    
  - **Adder Tree Stage1**   
    - Partial Product와 bias를 받아 합산, Partial Sum을 출력   

&nbsp;

