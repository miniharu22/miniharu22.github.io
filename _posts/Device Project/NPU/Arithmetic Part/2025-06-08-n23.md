---
layout : single
title: "[Verilog] Arithmetic Part (w/Testbench)"
categories: 
  - Universal NPU-CNN Accelarator
toc: true
toc_sticky: true
use_math: true
---

8개의 Arithmetic Core를 지닌 Arithmetic Part 모듈 설계 + 테스트벤치 작성      

## 0. Arithmetic Part Module    

```verilog
// Arithmetic Part module including eight Arithmetic cores
module AP(in, weight, bias, bound_level, step, en, en_relu, en_mp, out, out_en, clk, reset);

    parameter cell_bit = 8;
    parameter N_cell = 9;
    parameter biasport = 16;
    parameter N_core = 8;
    parameter outport = 8;

    input [cell_bit*N_cell-1:0] in;             // 72bits Input feature map
    input [cell_bit*N_cell*N_core-1:0] weight;  // 72bits weight x 8
    input [biasport*N_core-1:0] bias;           // 16bit bias x 8
    input [2:0] bound_level;
    input [2:0] step;
    input en, en_relu, en_mp;
    input clk, reset;

    output [outport*N_core-1:0] out;            // 8bit output x 8
    output [N_core-1:0] out_en;                 

    genvar i;
    generate
    for(i=0; i<N_core; i=i+1) begin : generate_ac
        arithmetic_core_mod ac (in, 
                                weight[cell_bit*N_cell*N_core-cell_bit*N_cell*i-1-:cell_bit*N_cell], 
                                bias[biasport*N_core-biasport*i-1-:biasport],
                                bound_level, 
                                step, 
                                en, 
                                en_relu, 
                                en_mp,
                                out[outport*N_core-outport*i-1-:outport], 
                                out_en[N_core-i-1], 
                                clk, 
                                reset);
    end
    endgenerate

endmodule
```

<div align="left">
    <strong>RTL Schematic</strong>
  <img src="/assets/images/npu/115.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

- **AP (Arithmetic Part) 모듈**  
  - 8개의 `arithmetic_core_mod`를 병렬로 생성하여 CNN-like 연산을 병렬 처리하는 상위 모듈  
  - 각 코어는 동일한 입력 feature map을 공유하며, 개별 filter weight와 bias를 사용  
  - 제어 신호를 통해 ReLU, Max pooling 단계를 활성화 가능      

  - **데이터 처리 과정**  
    - **1단계: 입력**  
      - **입력 feature map**: `in` (9개의 입력 cell, 각 8비트)  
      - **가중치**: `weight` (8개의 filter, 각 filter는 9개의 8비트 weight)  
      - **편향**: `bias` (8개의 16비트 bias 값)  
      - **제어 신호**:  
        - `bound_level`: clipping 단계 제어 (3비트)  
        - `step`: 누적합산 단계 제어 (3비트)  
        - `en`: 연산 활성화  
        - `en_relu`: ReLU 활성화  
        - `en_mp`: Max pooling 활성화  
        - `clk`, `reset`: 동작 클럭 및 리셋  

    - **2단계: 코어별 연산 처리**  
      - `generate` 블록으로 8개의 코어(`arithmetic_core_mod`)를 생성  
      - 각 코어는 같은 입력 feature map(`in`)을 사용  
      - 각 코어는 자신에게 해당하는 filter weight와 bias를 선택적으로 받아 처리  
      - 비트 슬라이싱을 통해 가중치, bias, 출력 포트를 정확히 할당  
      - 코어별 결과:  
        - 출력값: `out` (코어별 8비트 결과, 총 64비트)  
        - 출력 유효 신호: `out_en` (코어별 1비트)  

    - **3단계: 내부 처리 (코어 내부)**  
      - 각 코어(`arithmetic_core_mod`)는 다음과 같은 연산을 수행:  
        - 입력 feature map과 가중치의 곱셈 및 누적합산 (Convolution 연산)  
        - clipping 처리 & 9개 이상의 weight 누적 합산 (bound_level 및 step 사용)  
        - ReLU 처리(`en_relu=1`일 때 음수 제거)  
        - Max pooling 처리(`en_mp=1`일 때 2x2 영역의 최댓값 출력)  
        - 최종 출력값과 유효 신호 생성  

    - **4단계: 출력**  
      - 8개의 코어가 각자 출력값(`out`)과 출력 유효 신호(`out_en`)를 병렬로 생성  
      - 결과적으로 최종 출력값은 8개의 코어 출력이 직렬로 결합된 형태 (64비트)     

&nbsp;

## 1. Arithmetic Part Testbench    

```verilog
`timescale 1ns/10ps

module tb_ap();

    parameter cell_bit = 8;
    parameter N_cell = 9;
    parameter biasport = 16;
    parameter N_core = 8;
    parameter outport = 8;

    reg [cell_bit*N_cell-1:0] in;               // 72bit Input feature map
    reg [cell_bit*N_cell*N_core-1:0] weight;    // 72bit weight x 8
    reg [biasport*N_core-1:0] bias;             // 16bit bias x 8 
    reg [2:0] bound_level;      
    reg [2:0] step;
    reg en, en_relu, en_mp;
    reg clk, reset;

    wire [outport*N_core-1:0] out;              // 8bit output x 8
    wire [N_core-1:0] out_en;

    always #5 clk <= ~clk;


    reg [223:0] mat_in[0:27];   // 28 rows of 224bit(28 x 8bit pixel) input feature map 
    reg [7:0] inp[0:27][0:27];  // 28x28 matrix with 8bit pixels from mat_in
      
    reg [7:0] weight0[0:15];    // 1st layer weight : 16rows of 8bit 
    reg [15:0] bias0[0:1];      // 1st layer bias : 2rows of 16bit
    reg [7:0] out0[0:143];      // 1st layer output : 144rows of 8bit

    reg [7:0] in2[0:143];
    reg [7:0] weight20[0:8], weight21[0:8], weight22[0:8], weight23[0:8], weight24[0:8], weight25[0:8], weight26[0:8], weight27[0:8]; // 2nd layer weight : 9rows of 8bit
    reg [15:0] bias2[0:7];      // 2nd layer bias : 8rows of 16bit

    reg signed [7:0] out20[0:24], out21[0:24], out22[0:24], out23[0:24], out24[0:24], out25[0:24], out26[0:24], out27[0:24];

    AP AP0(in, 
           weight, 
           bias, 
           bound_level, 
           step, 
           en, 
           en_relu, 
           en_mp, 
           out, 
           out_en, 
           clk, 
           reset);

    integer i=0, j=0, layer01 = 0, layer02 = 0, f, f0, f1, f2, f3, f4, f5, f6, f7, k;


    ///////////////////////////
    // Simulation Start
    ///////////////////////////

    initial 
        begin


            /////////////////////////////
            // Simulation Initialization
            /////////////////////////////

            clk = 1;
            reset = 0;
            en_relu = 0;
            en_mp = 0;
            en = 0;
            #12
            reset = 1;


            /////////////////////////////
            // Data Loader for 1st layer
            /////////////////////////////

            #9
            $readmemh("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/input_map_hex.txt", mat_in);
            $readmemb("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/l0c0 weight.txt", weight0);
            $readmemb("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/l0 bias.txt", bias0);

            en_relu = 1;            // Enable ReLU 
            en_mp = 1;              // Enable Maxpooling
            en = 1;
            bound_level = 3'b011;   // Clip to -64 ~ 63
            step = 3'b001;          // Multi-clk Accumulation


            /////////////////////////////
            // Convert : mat_in → inp
            /////////////////////////////

            for (i=0; i<28; i=i+1)
                begin
                    for (j=0; j<28; j=j+1)
                    begin
                        inp[i][j] = mat_in[i][223-8*j-:8];  // Extract each 8bit pixels
                        inp[i][j] = inp[i][j] >> 1;         // Right Shift by 1
                    end
                end


            ////////////////////////////////////////////////
            // 1st layer Convolution 
            // stride = 2
            // 28x28 → 12x12
            ////////////////////////////////////////////////

            for (i=0; i<24; i=i+2)  
                begin
                    for (j=0; j<24; j=j+2)  
                        begin
                            in = {inp[i][j], inp[i][j+1], inp[i][j+2], inp[i][j+3], 
                                  inp[i+1][j], inp[i+1][j+1], inp[i+1][j+2], inp[i+1][j+3], 
                                  8'b0000_0000};
                            weight[cell_bit*N_cell*N_core-1 -:cell_bit*N_cell] = {weight0[0], weight0[1], weight0[2], 
                                                                                  weight0[3], weight0[4], weight0[5], 
                                                                                  weight0[6], weight0[7], 8'b0000_0000};
                            weight[cell_bit*N_cell*N_core-1-cell_bit*N_cell :0] = 0;
                            bias[biasport*N_core-1-:biasport] = bias0[0];
                            bias[biasport*N_core-1-biasport:0] = 0;


                            #10
                            in = {inp[i+2][j], inp[i+2][j+1], inp[i+2][j+2], inp[i+2][j+3], 
                                  inp[i+3][j], inp[i+3][j+3], inp[i+3][j+2], inp[i+3][j+3], 
                                  8'b0000_0000};
                            weight[cell_bit*N_cell*N_core-1 -:cell_bit*N_cell] = {weight0[8], weight0[9], weight0[10], 
                                                                                  weight0[11], weight0[12], weight0[13], 
                                                                                  weight0[14], weight0[15], 8'b0000_0000};                                                                                
                            bias[biasport*N_core-1-:biasport] = 0;
                            

                            #10;
                            in = {inp[i][j+1], inp[i][j+1+1], inp[i][j+1+2], inp[i][j+1+3], 
                                  inp[i+1][j+1], inp[i+1][j+1+1], inp[i+1][j+1+2], inp[i+1][j+1+3], 
                                  8'b0000_0000};

                            weight[cell_bit*N_cell*N_core-1 -:cell_bit*N_cell] = {weight0[0], weight0[1], weight0[2], 
                                                                                  weight0[3], weight0[4], weight0[5], 
                                                                                  weight0[6], weight0[7], 8'b0000_0000};
                            weight[cell_bit*N_cell*N_core-1-cell_bit*N_cell :0] = 0;
                            bias[biasport*N_core-1-:biasport] = bias0[0];
                            bias[biasport*N_core-1-biasport:0] = 0;


                            #10
                            in = {inp[i+2][j+1], inp[i+2][j+1+1], inp[i+2][j+1+2], inp[i+2][j+1+3], 
                                  inp[i+3][j+1], inp[i+3][j+1+3], inp[i+3][j+1+2], inp[i+3][j+1+3], 
                                  8'b0000_0000};
                            weight[cell_bit*N_cell*N_core-1 -:cell_bit*N_cell] = {weight0[8], weight0[9], weight0[10], 
                                                                                  weight0[11], weight0[12], weight0[13], 
                                                                                  weight0[14], weight0[15], 8'b0000_0000};
                            bias[biasport*N_core-1-:biasport] = 0;
                            
                            
                            #10;
                            in = {inp[i+1][j], inp[i+1][j+1], inp[i+1][j+2], inp[i+1][j+3], 
                                  inp[i+1+1][j], inp[i+1+1][j+1], inp[i+1+1][j+2], inp[i+1+1][j+3], 
                                  8'b0000_0000};
                            weight[cell_bit*N_cell*N_core-1 -:cell_bit*N_cell] = {weight0[0], weight0[1], weight0[2], 
                                                                                  weight0[3], weight0[4], weight0[5], 
                                                                                  weight0[6], weight0[7], 8'b0000_0000};
                            weight[cell_bit*N_cell*N_core-1-cell_bit*N_cell :0] = 0;
                            bias[biasport*N_core-1-:biasport] = bias0[0];
                            bias[biasport*N_core-1-biasport:0] = 0;


                            #10
                            in = {inp[i+1+2][j], inp[i+1+2][j+1], inp[i+1+2][j+2], inp[i+1+2][j+3], 
                                  inp[i+1+3][j], inp[i+1+3][j+3], inp[i+1+3][j+2], inp[i+1+3][j+3], 
                                  8'b0000_0000};
                            weight[cell_bit*N_cell*N_core-1 -:cell_bit*N_cell] = {weight0[8], weight0[9], weight0[10], 
                                                                                  weight0[11], weight0[12], weight0[13], 
                                                                                  weight0[14], weight0[15], 8'b0000_0000};
                            bias[biasport*N_core-1-:biasport] = 0;
                            
                            #10;
                            in = {inp[i+1][j+1], inp[i+1][j+1+1], inp[i+1][j+1+2], inp[i+1][j+1+3], 
                                  inp[i+1+1][j+1], inp[i+1+1][j+1+1], inp[i+1+1][j+1+2], inp[i+1+1][j+1+3], 
                                  8'b0000_0000};
                            weight[cell_bit*N_cell*N_core-1 -:cell_bit*N_cell] = {weight0[0], weight0[1], weight0[2], 
                                                                                  weight0[3], weight0[4], weight0[5], 
                                                                                  weight0[6], weight0[7], 8'b0000_0000};
                            weight[cell_bit*N_cell*N_core-1-cell_bit*N_cell :0] = 0;
                            bias[biasport*N_core-1-:biasport] = bias0[0];
                            bias[biasport*N_core-1-biasport:0] = 0;

                            #10
                            in = {inp[i+1+2][j+1], inp[i+1+2][j+1+1], inp[i+1+2][j+1+2], inp[i+1+2][j+1+3], 
                                  inp[i+1+3][j+1], inp[i+1+3][j+1+3], inp[i+1+3][j+1+2], inp[i+1+3][j+1+3], 
                                  8'b0000_0000};
                            weight[cell_bit*N_cell*N_core-1 -:cell_bit*N_cell] = {weight0[8], weight0[9], weight0[10], 
                                                                                  weight0[11], weight0[12], weight0[13], 
                                                                                  weight0[14], weight0[15], 8'b0000_0000};
                            bias[biasport*N_core-1-:biasport] = 0;
                            
                            #10;
                        end
                end

            en = 0;
            #100


            /////////////////////////////////////////
            // Data Loader for 1st layer
            // 1st layer output → 2nd layer input
            /////////////////////////////////////////

            $readmemb("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/output layer01.txt", in2);
            for (i = 0; i < 12; i = i+1)
                begin
                    for (j = 0; j < 12; j = j+1)
                        begin
                            inp[i][j] = in2[i*12+j];
                        end
                end

            $readmemb("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/l2c0 weight.txt", weight20);
            $readmemb("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/l2c1 weight.txt", weight21);
            $readmemb("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/l2c2 weight.txt", weight22);
            $readmemb("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/l2c3 weight.txt", weight23);
            $readmemb("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/l2c4 weight.txt", weight24);
            $readmemb("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/l2c5 weight.txt", weight25);
            $readmemb("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/l2c6 weight.txt", weight26);
            $readmemb("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/l2c7 weight.txt", weight27);
            $readmemb("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/l2 bias.txt", bias2);

            reset = 0;
            #10
            reset = 1;
            en = 1;
            bound_level = 3'b101;   // Clip to -4 ~ 63
            step = 3'b000;          // No multi-clk accumulation (3x3 Filter)


            /////////////////////////////////////////
            // Split weight data to eight 3x3 filter
            /////////////////////////////////////////

            weight[cell_bit*N_cell*N_core-1 -:cell_bit*N_cell] = {weight20[0], weight20[1], weight20[2], 
                                                                  weight20[3], weight20[4], weight20[5], 
                                                                  weight20[6], weight20[7], weight20[8]};

            weight[cell_bit*N_cell*N_core-1-cell_bit*N_cell*1 -:cell_bit*N_cell] = {weight21[0], weight21[1], weight21[2], 
                                                                                    weight21[3], weight21[4], weight21[5], 
                                                                                    weight21[6], weight21[7], weight21[8]};

            weight[cell_bit*N_cell*N_core-1-cell_bit*N_cell*2 -:cell_bit*N_cell] = {weight22[0], weight22[1], weight22[2], 
                                                                                    weight22[3], weight22[4], weight22[5], 
                                                                                    weight22[6], weight22[7], weight22[8]};

            weight[cell_bit*N_cell*N_core-1-cell_bit*N_cell*3 -:cell_bit*N_cell] = {weight23[0], weight23[1], weight23[2], 
                                                                                    weight23[3], weight23[4], weight23[5], 
                                                                                    weight23[6], weight23[7], weight23[8]};

            weight[cell_bit*N_cell*N_core-1-cell_bit*N_cell*4 -:cell_bit*N_cell] = {weight24[0], weight24[1], weight24[2], 
                                                                                    weight24[3], weight24[4], weight24[5], 
                                                                                    weight24[6], weight24[7], weight24[8]};

            weight[cell_bit*N_cell*N_core-1-cell_bit*N_cell*5 -:cell_bit*N_cell] = {weight25[0], weight25[1], weight25[2], 
                                                                                    weight25[3], weight25[4], weight25[5], 
                                                                                    weight25[6], weight25[7], weight25[8]};

            weight[cell_bit*N_cell*N_core-1-cell_bit*N_cell*6 -:cell_bit*N_cell] = {weight26[0], weight26[1], weight26[2], 
                                                                                    weight26[3], weight26[4], weight26[5], 
                                                                                    weight26[6], weight26[7], weight26[8]};

            weight[cell_bit*N_cell*N_core-1-cell_bit*N_cell*7 -:cell_bit*N_cell] = {weight27[0], weight27[1], weight27[2], 
                                                                                    weight27[3], weight27[4], weight27[5], 
                                                                                    weight27[6], weight27[7], weight27[8]};

            bias = {bias2[0], bias2[1], bias2[2], bias2[3], bias2[4], bias2[5], bias2[6], bias2[7]};


            ////////////////////////////////////////////////
            // 2st layer Convolution 
            // stride = 2
            // 12x12 → 5x5
            ////////////////////////////////////////////////

            for (i=0; i<10; i=i+2)
                begin
                    for (j=0; j<10; j=j+2)
                        begin
                            in = {inp[i][j], inp[i][j+1], inp[i][j+2], 
                                  inp[i+1][j], inp[i+1][j+1], inp[i+1][j+2],
                                  inp[i+2][j], inp[i+2][j+1], inp[i+2][j+2]};

                            #10
                            in = {inp[i][j+1], inp[i][j+1+1], inp[i][j+1+2], 
                                  inp[i+1][j+1], inp[i+1][j+1+1], inp[i+1][j+1+2],
                                  inp[i+2][j+1], inp[i+2][j+1+1], inp[i+2][j+1+2]};

                            #10
                            in = {inp[i+1][j], inp[i+1][j+1], inp[i+1][j+2], 
                                  inp[i+1+1][j], inp[i+1+1][j+1], inp[i+1+1][j+2],
                                  inp[i+1+2][j], inp[i+1+2][j+1], inp[i+1+2][j+2]};

                            #10
                            in = {inp[i+1][j+1], inp[i+1][j+1+1], inp[i+1][j+1+2], 
                                  inp[i+1+1][j+1], inp[i+1+1][j+1+1], inp[i+1+1][j+1+2],
                                  inp[i+1+2][j+1], inp[i+1+2][j+1+1], inp[i+1+2][j+1+2]};

                            #10;
                        end
                end
            
            en = 0;

        end



    ///////////////////////////////
    // Store 1st layer Output 
    ///////////////////////////////
    

    always @(posedge clk) begin
        if (layer01 < 144 & out_en[7] == 1) begin
            out0[layer01] <= out[outport*N_core-1-:8];
            layer01 <= layer01 + 1;
        end
        else if (layer01 == 144) begin
            f = $fopen("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/output layer01.txt", "w");
            for (k = 0; k < 144; k = k + 1) begin
                $fdisplay(f, "%b", out0[k]);
            end
            $fclose(f);
        end

        ///////////////////////////////
        // Store 2st layer Output 
        ///////////////////////////////

        if (layer01 == 144 & out_en == 8'b1111_1111 & layer02 < 25) begin
            out20[layer02] <= out[outport*N_core-1-:8];
            out21[layer02] <= out[outport*N_core-1-8*1-:8];
            out22[layer02] <= out[outport*N_core-1-8*2-:8];
            out23[layer02] <= out[outport*N_core-1-8*3-:8];
            out24[layer02] <= out[outport*N_core-1-8*4-:8];
            out25[layer02] <= out[outport*N_core-1-8*5-:8];
            out26[layer02] <= out[outport*N_core-1-8*6-:8];
            out27[layer02] <= out[outport*N_core-1-8*7-:8];

            layer02 <= layer02 + 1;
        end
        else if (layer02 == 25) begin
            //write
            f0 = $fopen("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/output layer20.txt", "w");
            f1 = $fopen("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/output layer21.txt", "w");
            f2 = $fopen("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/output layer22.txt", "w");
            f3 = $fopen("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/output layer23.txt", "w");
            f4 = $fopen("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/output layer24.txt", "w");
            f5 = $fopen("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/output layer25.txt", "w");
            f6 = $fopen("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/output layer26.txt", "w");
            f7 = $fopen("C://MY/Vivado/UniNPU/UniNPU.srcs/data/AP_Data/output layer27.txt", "w");
            for (k = 0; k < 25; k = k + 1) begin
                $fdisplay(f0, "%d", out20[k]);
                $fdisplay(f1, "%d", out21[k]);
                $fdisplay(f2, "%d", out22[k]);
                $fdisplay(f3, "%d", out23[k]);
                $fdisplay(f4, "%d", out24[k]);
                $fdisplay(f5, "%d", out25[k]);
                $fdisplay(f6, "%d", out26[k]);
                $fdisplay(f7, "%d", out27[k]);
            end
            $fclose(f0);
            $fclose(f1);
            $fclose(f2);
            $fclose(f3);
            $fclose(f4);
            $fclose(f5);
            $fclose(f6);
            $fclose(f7);
        end
    end
    
endmodule
```

**Input Feature map**     
fffffffcfffffefffefffdfffffffefefffffefffffdfdfffffffdff  
fefefffffffffffcfffefefffffefffffbfffffcfffffffbffffffff  
fffefffffffbfefffffdfffefbfcbc8586745f3e0c1c75f6fbfbfdff  
fcfffffcfffffff7f4f8f4cebfa5300a0f09090a0101193371ffffff  
fffffefbfffdee71353333221e161e190005040004000d1d5df9ffff  
fffffefffbffe865221918150b0822220906080502020c1a5ffefffd  
fffefffdcdc25214060707070600d4c6bbb6a66e0e0118427afffdff  
fefffbff62440c050407076295b4fffdffb6653d030668f7ecfefffe  
ffffffcb301907030f7ba1d3ededfefdffad503300219afffaffffff  
fdfdfc72120200207fdff6fefefffed6bc71170e105acafefffdfffe  
fffffc9d31222aa1e6fafffeffffce5c2319030d5bd9f9fcfffeffff  
fcfffeeccfcacbedfffffffeffff6e1003040230b2fffbfffefefefb  
fefffffefffffffdfffeffffffff150200336e97eefffbfffffffefe  
fffffffffffffdfdfffefffdfffc0005004d9ec6ffffffffffffffff  
fffffffffffefdfefdfefeffffd600020158c2d9ffffffffffffffff  
fffffffffffffffffefffffefd9b022c519ef9f7ffffffffffffffff  
fffffffffffffefffabba562461788ddfdfffeffffffffffffffffff  
fffffffffffdfffff78056170101d7fffdfffeffffffffffffffffff  
fffffffffffffefcf05e2d11020aedfefdfffffeffffffffffffffff  
fffffffffffeffffe83d0003000afdfeffffffffffffffffffffffff  
fffffffffffffff6bf3000090b45fcfffeffffffffffffffffffffff  
fffffffffffff678121a1f84afe2fbfcfffffdffffffffffffffffff  
ffffffffffffc3530e6b91cbe7f6fffffffffcfeffffffffffffffff  
fffffffffffe911314a8def3fffdfffffcffffffffffffffffffffff  
ffffffffffffa41d31bee0f8fffffffffffffefeffffffffffffffff  
ffffffffffffca2585ebfffffdfffefffefeffffffffffffffffffff  
fffffffffffffffffffffffffffffffffffffffffefffffffffffefe  
fffffffffffffffffffffbfffff9fffffffefdfffffeffffffffffff  
{:.notice}    

&nbsp;

<div align="left">
    <strong>Simulaton : 1st Layer Convolution</strong>
    <br>
    <strong>28x28 → 12x12</strong>
  <img src="/assets/images/npu/116.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 

<div align="left">
    <strong>Simulaton : 2nd Layer Convolution</strong>
    <br>
    <strong>12x12 → 5x5</strong>
  <img src="/assets/images/npu/117.png" width="100%" height="100%" alt=""/>
  <p><em></em></p>
</div>
{: .notice} 


- **tb_ap (Testbench for AP Module)**  
  - CNN 모듈(AP)의 동작을 시뮬레이션하고, 입력 이미지 데이터를 레이어 단위로 처리하여 결과를 출력 파일로 저장하는 테스트 환경  
  - 입력 feature map과 filter weight, bias, clipping 설정 등을 읽어와 레이어별 convolution 연산, ReLU, Max Pooling 등을 제어  
  - 두 단계 (Layer 1, Layer 2)의 CNN 연산을 순차적으로 수행하며 각 결과를 txt 파일에 저장 및 파일 출력  

  - **데이터 처리 과정**  

    - **1단계: 시뮬레이션 초기화**  
      - 클럭 생성: `always #5 clk <= ~clk;`  
      - 신호 초기화: `reset = 0; en = 0; en_relu = 0; en_mp = 0;`  
      - 일정 시간 후 리셋: `#12 reset = 1;`  

    - **2단계: 입력 로딩 및 전처리**  
      - 입력 이미지(`mat_in`)는 28x28 (총 224비트/줄)  
      - `inp[28][28]` 배열로 8비트 단위로 분할 (`mat_in[i][223-8*j -: 8]`)  
      - 각 픽셀은 1비트 우측 시프트로 스케일링 (`>> 1`)  

    - **3단계: Layer 1 연산 (stride=2)**  
      - **필터**: `weight0[0:15]` (2개의 3x3 필터 블록)  
      - **바이어스**: `bias0[0:1]`  
      - `en_relu=1`, `en_mp=1`, `bound_level=3'b011` (-64~63 클리핑), `step=3'b001`  
      - 3x3 윈도우 기반의 인풋 구성 후 `in`, `weight`, `bias` 할당  
      - 총 12x12 = 144개의 출력을 생성하여 `out0`에 저장  

    - **4단계: Layer 1 결과 저장 및 Layer 2 입력 구성**  
      - `out0` → `in2[0:143]` → 다시 `inp[12][12]`로 재구성  
      - Layer 2 weight 파일(`weight20`~`weight27`)과 bias 파일(`bias2`) 로드  

    - **5단계: Layer 2 연산 (stride=2)**  
      - **필터 수**: 8개 (각각 3x3) → `weight[cell_bit*N_cell*N_core-1 -: ...]` 슬라이싱  
      - **바이어스**: 8개 (`bias = {bias2[0], ..., bias2[7]}`)  
      - `en=1`, `en_relu=1`, `en_mp=1`, `bound_level=3'b101`, `step=3'b000`  
      - 총 5x5 = 25개의 출력 * 8채널 → `out20` ~ `out27`에 저장  

    - **6단계: 출력 저장**  
      - Layer 1: `out0[0:143]` → `output layer01.txt`  
      - Layer 2: 각 코어 결과 `out20`~`out27` → `output layer20.txt` ~ `output layer27.txt`  
      - 저장 조건: `out_en` 활성 및 카운터 `layer01`, `layer02` 기반  

    - **7단계: 제어 흐름 및 유효 신호 관리**  
      - 출력 유효 판단: `if (out_en[7])`, `if (out_en == 8'b1111_1111)`  
      - 연산 완료 후 출력 카운터 증가, 조건 충족 시 결과 파일로 저장  
      - 연산 완료 후 `en = 0`으로 비활성화  

&nbsp;




